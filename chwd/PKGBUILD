# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>
# Contributor: Vasiliy Stelmachenok <ventureo@yandex.ru>

pkgname=chwd
pkgver=1.7.0
pkgrel=2
pkgdesc="CachyOS Hardware Detection Tool"
arch=(x86_64)
url="https://github.com/cachyos/chwd"
license=(GPL3)
depends=('gcc-libs' 'pciutils' 'lua' 'pacman')
makedepends=('git' 'cargo' 'mold' 'clang' 'llvm')
provides=('chwd-db')
replaces=('chwd-db')
conflicts=('chwd-db')
groups=('cachyos')
source=("chwd-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('a2a9065b2bfdb46b47981e496f4ac099e1298a6f6da65c94741cd5a286c5afb0')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"

  cd scripts/chwd-kernel
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Compile with clang
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto"

  cd scripts/chwd-kernel
  cargo build --release --frozen

  cd ../../

  cargo build --release --frozen
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/chwd/LICENSE"

  install -Dm755 "target/release/chwd" "$pkgdir/usr/bin/chwd"
  install -Dm755 scripts/chwd "$pkgdir"/var/lib/chwd/scripts/chwd

  install -Dm644 target/release/build/chwd-*/out/chwd.bash "$pkgdir/usr/share/bash-completion/completions/chwd"
  install -Dm644 target/release/build/chwd-*/out/chwd.fish "$pkgdir/usr/share/fish/vendor_completions.d/chwd.fish"
  install -Dm644 target/release/build/chwd-*/out/_chwd "$pkgdir/usr/share/zsh/site-functions/_chwd"

  install -d -m755 "$pkgdir"/var/lib/chwd/{db,local}/pci

  cp -r profiles/pci "$pkgdir"/var/lib/chwd/db/

  cd scripts/chwd-kernel
  install -Dm755 "target/release/chwd-kernel" "$pkgdir/usr/bin/chwd-kernel"
}


# vim:set sw=2 sts=2 et:
