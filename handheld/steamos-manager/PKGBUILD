## Valve
# Maintainer: Vicki Pfau <vi@endrift.com>
# Maintainer: Jeremy Whiting <jeremy.whiting@collabora.com>
## CachyOS
# Maintainer: Eric Naim <dnaim@cachyos.org>

pkgname=steamos-manager
pkgver=25.9.0
pkgrel=3
pkgdesc='SteamOS Manager daemon for running various system management tasks'
arch=('x86_64')
url='https://store.steampowered.com/steamos/'
license=('MIT')
depends=(
    'dbus'
    'gamescope-session-steam-git'
    'gcc-libs'
    'glibc'
    'speech-dispatcher'
    'steamos-networking-tools' # For steamos-wifi-set-backend
    'systemd'
    'systemd-libs'
    'wireless_tools')  # For iwconfig
optdepends=('jupiter-hw-support: jupiter support')  # Needed for jupiter-get-als-gain, jupiter-biosupdate, steamos-format-device, steamos-trim-devices
makedepends=('git' 'cargo' 'clang')
source=("git+https://gitlab.steamos.cloud/holo/steamos-manager.git#tag=v${pkgver}"
        "support-gamescope-session-steam.patch"
        "msi-claw.patch"
        "no-orca-service.patch")
sha256sums=('a2a7927ad72ac68ef16ead076798e852071f6437a85ac0c588d81a0d39d0c5e9'
            '712b59fe077f03f623537de93494aafca20d76fb1227f86438188c82b9d49e83'
            'bb7175aa993f486069808127ecf49a71b18ed07ed4e1dac89239e2ae5f51acaf'
            '10a36852b2cddf2ecc4f76df0cbabd9dd1f53c507590ecb0e58e6524511ba38f')
install='steamos-manager.install'

prepare() {
    cd "${pkgname}"

    # Add support for XBOX Rog Ally/X
    git cherry-pick -n 0500f0eb84fafefa15fbd9028c2619646845bf2d

    patch -Np1 -i ../support-gamescope-session-steam.patch
    patch -Np1 -i ../msi-claw.patch
    patch -Np1 -i ../no-orca-service.patch

    cargo fetch --locked --target "x86_64-unknown-linux-gnu"
}

build() {
    cd "${pkgname}"
    make build
}

package () {
    cd "${pkgname}"
    make install DESTDIR="${pkgdir}"

    install -d -m0755 "$pkgdir/usr/lib/systemd/user/gamescope-session-plus@steam.service.wants/"
    ln -s ../steamos-manager.service "$pkgdir/usr/lib/systemd/user/gamescope-session-plus@steam.service.wants/"
}
