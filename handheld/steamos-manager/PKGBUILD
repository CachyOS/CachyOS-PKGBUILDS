## Valve
# Maintainer: Vicki Pfau <vi@endrift.com>
# Maintainer: Jeremy Whiting <jeremy.whiting@collabora.com>
## CachyOS
# Maintainer: Eric Naim <dnaim@cachyos.org>

pkgname=steamos-manager
pkgver=25.12.0
pkgrel=1
pkgdesc='SteamOS Manager daemon for running various system management tasks'
arch=('x86_64')
url='https://store.steampowered.com/steamos/'
license=('MIT')
depends=(
    'dbus'
    'gamescope-session-steam-git'
    'gcc-libs'
    'glibc'
    'speech-dispatcher'
    'steamos-networking-tools' # For steamos-wifi-set-backend
    'systemd'
    'systemd-libs'
    'wireless_tools')  # For iwconfig
optdepends=('jupiter-hw-support: jupiter support')  # Needed for jupiter-get-als-gain, jupiter-biosupdate, steamos-format-device, steamos-trim-devices
makedepends=('git' 'cargo' 'clang')
source=("git+https://gitlab.steamos.cloud/holo/steamos-manager.git#tag=v${pkgver}"
        "support-gamescope-session-steam.patch"
        "msi-claw.patch")
sha256sums=('34ffb754d4ed440c0bb0af71444f93bac2f62d31c92499f7f6c990fd0d22acf3'
            '44be95baeb7d020f5b630191c50e6ff421afc7f122e5279f35f297613bfa281e'
            'bb7175aa993f486069808127ecf49a71b18ed07ed4e1dac89239e2ae5f51acaf')
install='steamos-manager.install'

prepare() {
    cd "${pkgname}"

    patch -Np1 -i ../support-gamescope-session-steam.patch
    patch -Np1 -i ../msi-claw.patch

    cargo fetch --locked --target "x86_64-unknown-linux-gnu"
}

build() {
    cd "${pkgname}"
    make build
}

package () {
    cd "${pkgname}"
    make install DESTDIR="${pkgdir}"

    install -d -m0755 "$pkgdir/usr/lib/systemd/user/gamescope-session-plus@steam.service.wants/"
    ln -s ../steamos-manager.service "$pkgdir/usr/lib/systemd/user/gamescope-session-plus@steam.service.wants/"
}
