# Maintainer: Peter Jung <admin@ptr1337.dev>

pkgname=jupiter-hw-support
pkgver=20260211.1
pkgrel=3
arch=('any')
license=('MIT')
depends=(
    'python-evdev'
    'python>=3.13'
    'dmidecode' # for jupiter-biosupdate
    'python-crcmod' 'python-click' 'python-progressbar'
    'python-hid>=1.0.6-2' # for jupiter-controller-update
    'jq' # for jupiter-controller-update, jupiter-biosupdate
    'alsa-utils' # for the sound workarounds
    'f3' 'parted' 'e2fsprogs' # for sdcard formatting
    'udisks2>=2.9.4-1.1' # for mounting external drives with the 'as-user' option
)
optdepends=('grub-steamos')
makedepends=('rsync' 'git' 'xorg-xcursorgen')
pkgdesc='CachyOS - Deckify Steam Deck HW Support'
source=("git+https://github.com/evlaV/jupiter-hw-support.git#tag=jupiter-${pkgver}"
        "steamos-btrfs-automount.patch"
        "cachyos-automount.patch"
        "udev-automount-rules.patch"
        "priv-write.patch"
        "cachyos-bios.patch")
#install="$pkgname.install"
options=(!strip !debug)
sha256sums=('f31be3945132f3966da89dbe3f583ce9a3f8343da2ade6c464a51cddf684f174'
            'fe064240bf77dabfa4d161b10c33c82385a0ca22a49350e70ab9301b0e98dc7b'
            'c7db0e977fb0c3a1526b8482e349f73304fc6397d7337c48fe0c990bde64412c'
            'd6b5636eb09d2190b10eb665238c30960babba96424fc9768e36966495fa795e'
            'd3098f26eaa26d9eccfedeac792e47a03317bdd16b17ba03be0fbd1ed6621c5d'
            '0d4660622887a360bef8f913e7bbd47ac262632acff503a3ca8cecbb6f4a943d')
prepare() {
  cd "${pkgname}"

  patch --forward --strip=1 --input=../steamos-btrfs-automount.patch
  patch --forward --strip=1 --input=../cachyos-automount.patch
  patch --forward --strip=1 --input=../udev-automount-rules.patch
  patch --forward --strip=1 --input=../priv-write.patch
  patch --forward --strip=1 --input=../cachyos-bios.patch
}

package() {
  rsync -a "$srcdir"/jupiter-hw-support/* "$pkgdir"

  cd $pkgdir/usr/share/steamos/
  xcursorgen $pkgdir/usr/share/steamos/steamos-cursor-config $pkgdir/usr/share/icons/steam/cursors/default

  cd "$pkgdir/usr/share/jupiter_bios_updater"

  # Remove gtk2 binary and respective build/start script - unused
  # Attempts to use gtk2 libraries which are not on the device.
  rm h2offt-g H2OFFTx64-G.sh

  # Driver module -- doesn't currently build, and not supported
  rm -rf driver
}
