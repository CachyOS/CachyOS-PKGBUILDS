# Maintainer: Peter Jung <admin@ptr1337.dev>

pkgname=jupiter-hw-support
_srctag=jupiter-20240624.1
_srcver=${_srctag#jupiter-}
pkgver=${_srcver//-/.}
pkgrel=1
arch=('any')
license=('MIT')
depends=('python-evdev'
         'dmidecode' # for jupiter-biosupdate
         'python-crcmod' 'python-click' 'python-progressbar' 'python-hid'
         'jq' # for jupiter-controller-update, jupiter-biosupdate
         'alsa-utils' # for the sound workarounds
         'parted' 'e2fsprogs' # for sdcard formatting
         'udisks2>=2.9.4-1.1' # for mounting external drives with the 'as-user' option
         'ntfs-3g' #To make sure NTFS-3G is installed on CachyOS
         'f3' #Used by format to check for bogus sdcards
        )
optdepends=('grub-steamos')
makedepends=('rsync' 'git' 'openssh' 'xorg-xcursorgen')
pkgdesc='CachyOS - Deckify Steam Deck HW Support'
source=("git+https://gitlab.com/evlaV/jupiter-hw-support.git#tag=$_srctag"
        "pythonhid.patch"
        "steamos-btrfs-automount.patch"
        "cachyos-automount.patch"
        "udev-automount-rules.patch"
        "priv-write.patch")
#install="$pkgname.install"
options=(!strip !debug)
sha256sums=('f935d558e4384843f48f31f602c000f1e8cae4a8d7a25fdb32961ba45251be7a'
            '05f7a3d1fcff857aad4e5a8dae400f6acbf32c7e505db347f441dd33a6601df4'
            'fe064240bf77dabfa4d161b10c33c82385a0ca22a49350e70ab9301b0e98dc7b'
            'c7db0e977fb0c3a1526b8482e349f73304fc6397d7337c48fe0c990bde64412c'
            'd6b5636eb09d2190b10eb665238c30960babba96424fc9768e36966495fa795e'
            'd3098f26eaa26d9eccfedeac792e47a03317bdd16b17ba03be0fbd1ed6621c5d')
prepare() {
  patch --directory=$pkgname --forward --strip=1 --input=../pythonhid.patch
  patch --directory=$pkgname --forward --strip=1 --input=../steamos-btrfs-automount.patch
  patch --directory=$pkgname --forward --strip=1 --input=../cachyos-automount.patch
  patch --directory=$pkgname --forward --strip=1 --input=../udev-automount-rules.patch
  patch --directory=$pkgname --forward --strip=1 --input=../priv-write.patch
}

package() {
  rm -rf "$srcdir"/jupiter-hw-support/usr/bin/jupiter-biosupdate
  rm -rf "$srcdir"/jupiter-hw-support/usr/bin/steam-http-loader
  rm -rf "$srcdir"/jupiter-hw-support/usr/bin/steamos-select-branch
  rm -rf "$srcdir"/jupiter-hw-support/usr/bin/steamos-session-select
  rm -rf "$srcdir"/jupiter-hw-support/usr/bin/steamos-update
  rm -rf "$srcdir"/jupiter-hw-support/usr/bin/steamos-polkit-helpers/jupiter-biosupdate
  rm -rf "$srcdir"/jupiter-hw-support/usr/bin/steamos-polkit-helpers/steamos-select-branch
  rm -rf "$srcdir"/jupiter-hw-support/usr/bin/steamos-polkit-helpers/steamos-update
  rsync -a "$srcdir"/jupiter-hw-support/* "$pkgdir"

  cd $pkgdir/usr/share/steamos/
  xcursorgen $pkgdir/usr/share/steamos/steamos-cursor-config $pkgdir/usr/share/icons/steam/cursors/default

  cd "$pkgdir/usr/share/jupiter_bios_updater"

  # Remove gtk2 binary and respective build/start script - unused
  # Attempts to use gtk2 libraries which are not on the device.
  rm h2offt-g H2OFFTx64-G.sh

  # Driver module -- doesn't currently build, and not supported
  rm -rf driver
}
