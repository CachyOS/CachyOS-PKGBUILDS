# Maintainer: Joaquín I. Aramendía <samsagax at gmail dot com>

pkgname=gamescope-session-git
_gitdir=gamescope-session
pkgver=r231.127f432
pkgrel=1
pkgdesc="Common gamescope session files"
arch=('any')
url="https://github.com/ChimeraOS/gamescope-session"
license=('MIT')
groups=()
depends=('gamescope')
makedepends=('git')
optdepends=('mangohud: for performance overlay')
install=gamescope-session.install
source=("${_gitdir}::git+https://github.com/ChimeraOS/${_gitdir}.git"
		"powerbuttond.patch"
		"force-wsi.patch"
		"deck-quirks.patch")
sha512sums=('SKIP'
			'1fd74f3312a5315447bce7c89c2501df54af15deb9f0a96c1e0a4d2d2304b77e472d3da381b3b98e2c60f83cd72b5ddf2e4a675d5e1cf575b0f5826e876dec08'
			'016b37516b86420c56be63e16533cc21cb1aac287f0957fb75b529a688e484e31fc833d531411eb7bfbb3b301905efbf34405eb160f880bd26269ec8437a0ea7'
			'd0235dd01487795fa2d0018e96dd18a9c86d1904bf67d14dd33dc19bfb78f6ce5cfccc20e1854f3a7a17cba1d33eb8aa3fab2add2e9a75424a8a5396f3723111')

pkgver() {
	cd "$srcdir/${_gitdir}"

# Git, no tags available
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  patch --directory=$_gitdir --no-backup-if-mismatch --forward --strip=1 --input=../powerbuttond.patch
  patch --directory=$_gitdir --no-backup-if-mismatch --forward --strip=1 --input=../force-wsi.patch
  patch --directory=$_gitdir --no-backup-if-mismatch --forward --strip=1 --input=../deck-quirks.patch
}

package() {
	cd "$srcdir/${_gitdir}"
	cp -rv "${srcdir}/${_gitdir}/usr" "${pkgdir}/usr"
	install -Dm644 "${srcdir}/${_gitdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
