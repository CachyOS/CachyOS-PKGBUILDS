# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>

pkgname=steam
pkgver=1.0.0.82
pkgrel=2
pkgdesc="Valve's digital software delivery system"
url='https://steampowered.com/'
arch=('x86_64')
license=('custom')
depends=(
  bash
  desktop-file-utils
  diffutils
  hicolor-icon-theme
  curl
  dbus
  freetype2
  gdk-pixbuf2
  ttf-font
  zenity
  lsb-release
  nss
  usbutils
  xorg-xrandr
  vulkan-driver
  vulkan-icd-loader
  lsof
  python
  xdg-user-dirs
  glibc
  libxcrypt
  libxcrypt-compat
  gcc-libs
)
depends_x86_64=(
  lib32-glibc
  lib32-libxcrypt
  lib32-libxcrypt-compat
  lib32-libgl
  lib32-gcc-libs
  lib32-libx11
  lib32-libxss
  lib32-alsa-plugins
  lib32-libgpg-error
  lib32-nss
  lib32-vulkan-driver
  lib32-vulkan-icd-loader
)
optdepends=('steam-native-runtime: steam native runtime support')
# the beta directory is a superset of stable and also contains the stable releases
source=(https://repo.steampowered.com/${pkgname}/archive/stable/${pkgname}_${pkgver}{.tar.gz,.dsc}
        steam-runtime.sh
        fix-downloadspeed.patch)
sha512sums=('32f84cbd5c93fb706520ca333c13553d61fe97b57b5dcfe4b83f8155d7c3c11e5dcbe71afa55ea8b7c11e933ba962e1d83cd6e3a9f86714e0afcbaf308c7b668'
            '0f8b285182c54ba0fbfd34b0ec4f6a6b6d11007c888f3a118b58d90e58d16f9d2b513c339e2b356b94adf7159bbc7eb8bda9f9ec27175701aa24b3b5a101c047'
            '5e75c019e9fe8c67d686c4e3343dac1180a69a4bdb7d39b333415c63201eef9b98da5619dbf6fd8daa6884e65bc7f8afc9e52778682425e5a75987d527eae6f0'
            '94bd9aad8f2ad9169f40537007db75f66e3539bc547c064a7e87035db56570ca6fe8a950389ed2017da692b0d05a034b4b8357bd39ebe575bd6e02a05cda3b29')
b2sums=('b526537fd9dd603182a28d35aeac67f778af75ba866a23597f7ae3f52b1b3bc3f548501723459ba35c337c3bdbe62bc359798149ca0d1c4bcee4599c9b84183d'
        '6d9ec684e53966d735fcd0974a6cd3efc13a7822a3cd9257ec991aa2d06c74215efda0fc4831f1bcf0d246e7bd0ac23baed7aa3a266fcdbac43e2723c7338796'
        'c6bac99336b7c30fec7cdbaf9e949555c687dd9dff50bcae136134d6314f4b841f5fc66ddb2caac1b003690b926fd4afbdc11da143b4674db4b75f27709fdd23'
        'b5a2551aa7402d1e5fbefea6a765475a645380c7004ff6cbb020713cc0db7587c1103c76d7b6cdaa267d797ecd1b96d3fba5ac8808ec09a688ece1f5e956fc4b')
validpgpkeys=('BA1816EF8E75005FCF5E27A1F24AEA9FB05498B7') # linux@steampowered.com

prepare() {
  cd ${pkgname}-launcher

  # Steam currently has massive problems with HTTP2 and its download speed
  # This patch does disable http2 and adds another improvement
  # There are still issues with DNS Caching, due making 1000s of dns request while downloading
  # DNS Caching, with dnsmasq or resolved can help here
  patch -Np1 < ../fix-downloadspeed.patch

  # separated runtime/native desktop files
  sed -r 's|(Name=Steam)|\1 (Runtime)|' -i steam.desktop
  sed -r 's|(/usr/bin/steam)|\1-runtime|' -i steam.desktop
}

package() {
  cd ${pkgname}-launcher
  make DESTDIR="${pkgdir}" install

  install -Dm 755 "${srcdir}/steam-runtime.sh" "${pkgdir}/usr/bin/steam-runtime"
  install -d "${pkgdir}/usr/lib/steam"
  mv "${pkgdir}/usr/bin/steam" "${pkgdir}/usr/lib/steam/steam"
  ln -sf /usr/bin/steam-runtime "${pkgdir}/usr/bin/steam"

  install -Dm 644 COPYING steam_subscriber_agreement.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 debian/changelog -t "${pkgdir}/usr/share/doc/${pkgname}"

  # blank steamdeps because apt-get
  ln -sf /usr/bin/true "${pkgdir}/usr/bin/steamdeps"

  install -Dm 644 subprojects/steam-devices/60-steam-input.rules \
    "${pkgdir}/usr/lib/udev/rules.d/70-steam-input.rules"
  install -Dm 644 subprojects/steam-devices/60-steam-vr.rules \
    "${pkgdir}/usr/lib/udev/rules.d/70-steam-vr.rules"
}

# vim: ts=2 sw=2 et:
