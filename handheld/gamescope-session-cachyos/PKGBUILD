# Maintainer: Eric Naim <dnaim@cachyos.org>

pkgname=gamescope-session-cachyos
pkgver=1.1.0
pkgrel=2
pkgdesc="Steam Big Picture session based on gamescope from SteamOS"
arch=('any')
url="https://github.com/CachyOS/gamescope-session"
license=('MIT') # this is what Valve put on their gamescope package
depends=(
    'bash'
    'coreutils'
    'dbus'
    'gamescope'
    'plasma-workspace' # plasma.desktop
    'python'
    'qt6-tools' # qdbus6
    'steam'
    'systemd'
    'util-linux'
)
makedepends=('git')
optdepends=(
    'ibus: Multilingual input support'
    'galileo-mura: Mura compensation for Steam Deck OLED'
    'mangohud: Mangoapp'
)
replaces=('gamescope-session-git' 'gamescope-session-steam-git')
conflicts=('gamescope-session-git' 'gamescope-session-steam-git')
source=("${pkgname}::git+${url}.git#tag=${pkgver}")
sha256sums=('36e7c824b7a46d9fcc7e803432b7862a355c2de7ffd9fa95e89c712d78c16acc')

prepare() {
    cd "${pkgname}"
    git cherry-pick -n 98e039d870aeadd6dd88fcb974d04270db3132bb
}

package() {
    cd "${pkgname}"
    cp -a usr/ "${pkgdir}/usr"
}
