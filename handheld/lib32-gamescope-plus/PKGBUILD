# Maintainer: Eric Naim <dnaim@cachyos.org>
# Contributor: Matthew Schwartz <matthew.schwartz@linux.dev>

pkgname=lib32-gamescope-plus
pkgver=3.15.13.plus1
pkgrel=2
pkgdesc="WSI Layer for use with gamescope (32-bit)"
arch=(x86_64)
url="https://github.com/ChimeraOS/gamescope"
license=('BSD-2-Clause')
depends=(
    'lib32-wayland'
    'lib32-libx11'
    'lib32-libxcb'
    'lib32-vulkan-icd-loader')
makedepends=(
    'cmake'
    'meson'
    'ninja'
    'wayland-protocols'
    'vulkan-headers'
    'lib32-glm')
provides=('lib32-gamescope')
conflicts=('lib32-gamescope')
source=("git+${url}#tag=${pkgver}")
sha256sums=('2d3deeacc515e45e90c51535c171fe0d36f1d0e3a62127ec54997b213eb08c23')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

    arch-meson gamescope build \
        --libdir /usr/lib32 \
        -Denable_gamescope=false \
        -Denable_gamescope_wsi_layer=true \
        -Denable_openvr_support=false \
        -Dpipewire=disabled
    meson compile -C build
}

package() {
    DESTDIR="$pkgdir" meson install -C build --skip-subprojects

    rm -rf "$pkgdir"/usr/share/gamescope
    rm -rf "$pkgdir"/usr/include
    rm -rf "$pkgdir"/usr/lib/libwlroots*
    rm -rf "$pkgdir"/usr/lib32/libwlroots*
    rm -rf "$pkgdir"/usr/lib/pkgconfig
    rm -rf "$pkgdir"/usr/lib32/pkgconfig
}
