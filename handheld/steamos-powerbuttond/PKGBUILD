# Maintainer: Peter Jung <admin@ptr1337.dev>
# Maintainer: Eric Naim <dnaim@cachyos.org>

pkgname=steamos-powerbuttond
pkgver=3.1
pkgrel=3
arch=('x86_64')
url="https://gitlab.steamos.cloud/jupiter/powerbuttond"
pkgdesc="Power button daemon"
license=('BSD-2-Clause')
depends=('glibc' 'libevdev' 'systemd-libs')
source=("git+https://gitlab.com/evlaV/powerbuttond.git#tag=v${pkgver}"
        "support-more.patch::https://github.com/matte-schwartz/steamos-powerbuttond/commit/0f7874e44162.patch"
        "remove-dead-code.patch::https://github.com/matte-schwartz/steamos-powerbuttond/commit/b02cb4ab6cb4.patch"
        "support-gamescope-session-plus.patch")
sha512sums=('7cbd87c05bde6dae6da2ddd38311497cd49a7df775985ab8a4ac90dea0f642c5fccd7183a58c9c92c95276f60b3cc1befee3301276afe35ede9ebc8e889b86db'
            '9575acad86a04bc6961c4920f4ca63524926346911d96a897e1d2bae638c2efbf8b64233765a4dad564b83a36b81c4b783a128a23afba0514f0f42e2ec76eb8d'
            'f4641b5c4b0b5e6df71279f8ad63aafb09ceac14efb22171edfdca52dedbcde29f0817295d37dffaa075ee1c55e601331adc15eeb978ce3d7ba2699cebf8f810'
            '808ef4e23b897a94296314695ad8f388d5ffcc775f22f45c8e7b2cf6a95f9d8abf726139247cf9e041be940a3abad1e699db99e900d427b2334a2c118131fa41')
provides=("powerbuttond")
conflicts=("powerbuttond")
replaces=("powerbuttond")

prepare() {
    cd "${srcdir}/powerbuttond"
    patch -Np1 -i ../support-more.patch
    patch -Np1 -i ../remove-dead-code.patch
    patch -Np1 -i ../support-gamescope-session-plus.patch
}

build() {
    make -C "${srcdir}/powerbuttond"
}

package() {
    make -C "${srcdir}/powerbuttond" install DESTDIR="${pkgdir}"
    #Symlink for gamescope-session
    install -dm755 "${pkgdir}/usr/bin"
    ln -s "/usr/lib/hwsupport/steamos-powerbuttond" "${pkgdir}/usr/bin/powerbuttond"

    # Install license to the correct directory
    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}-temp"
    mv "${pkgdir}/usr/share/licenses/steamos-powerbuttond" "${pkgdir}/usr/share/licenses/${pkgname}-temp/LICENSE"
    mv "${pkgdir}/usr/share/licenses/${pkgname}-temp" "${pkgdir}/usr/share/licenses/${pkgname}"
}
