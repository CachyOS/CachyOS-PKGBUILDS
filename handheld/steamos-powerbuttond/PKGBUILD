# Maintainer: Peter Jung <admin@ptr1337.dev>
# Maintainer: Eric Naim <dnaim@cachyos.org>

pkgname=steamos-powerbuttond
pkgver=3.1
pkgrel=3
arch=('x86_64')
url="https://gitlab.steamos.cloud/jupiter/powerbuttond"
pkgdesc="Power button daemon"
license=('BSD-2-Clause')
depends=('glibc' 'libevdev' 'systemd-libs')
makedepends=('git')
source=("git+https://gitlab.steamos.cloud/holo/powerbuttond.git#tag=v${pkgver}"
        "support-gamescope-session-plus.patch")
sha512sums=('7cbd87c05bde6dae6da2ddd38311497cd49a7df775985ab8a4ac90dea0f642c5fccd7183a58c9c92c95276f60b3cc1befee3301276afe35ede9ebc8e889b86db'
            '808ef4e23b897a94296314695ad8f388d5ffcc775f22f45c8e7b2cf6a95f9d8abf726139247cf9e041be940a3abad1e699db99e900d427b2334a2c118131fa41')
provides=("powerbuttond")
conflicts=("powerbuttond")
replaces=("powerbuttond")

prepare() {
    cd "${srcdir}/powerbuttond"
    # Add support for more devices
    git cherry-pick -n 0f7874e441628f18c40fa280573194ffc0ff1bef
    git cherry-pick -n b02cb4ab6cb4fbfb40e4c9612af3e6499714ef87

    # Install license to correct directory
    git cherry-pick -n 5a971c50893dd1706d69f3dd61f4bb6001acabf1

    # Support gamescope-session-plus
    patch -Np1 -i ../support-gamescope-session-plus.patch
}

build() {
    make -C "${srcdir}/powerbuttond"
}

package() {
    make -C "${srcdir}/powerbuttond" install DESTDIR="${pkgdir}"
    # Symlink for gamescope-session
    install -dm755 "${pkgdir}/usr/bin"
    ln -s "/usr/lib/hwsupport/steamos-powerbuttond" "${pkgdir}/usr/bin/powerbuttond"
}
