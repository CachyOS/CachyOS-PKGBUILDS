# Maintainer: Peter Jung <admin@ptr1337.dev>
# Maintainer: Eric Naim <dnaim@cachyos.org>

pkgname=steamos-powerbuttond
pkgver=3.5
pkgrel=1
arch=('x86_64')
url="https://gitlab.steamos.cloud/jupiter/powerbuttond"
pkgdesc="Power button daemon"
license=('BSD-2-Clause')
depends=('glibc' 'libevdev' 'systemd-libs')
makedepends=('git')
source=("git+https://gitlab.steamos.cloud/holo/powerbuttond.git#tag=v${pkgver}"
        "support-gamescope-session-plus.patch")
sha512sums=('f2f272696fa97ff3da969bef8c5c04e053393b50e014fa888d5b72285196bd32e2319d97bfc1df758d291933c81dc87fb1bf0874dd0fda4516025aaed4e0afe6'
            'cefc1a0270b0a5aede1091726274cd739cc17c3a6ae5a7ff74981985298b538dc1aae475ca374169ed2afd3d7c3e8d4bae5f002df950afe50a41b5ba3b8fe69f')
provides=("powerbuttond")
conflicts=("powerbuttond")
replaces=("powerbuttond")

prepare() {
    cd "${srcdir}/powerbuttond"

    # Support gamescope-session-plus
    patch -Np1 -i ../support-gamescope-session-plus.patch
}

build() {
    make -C "${srcdir}/powerbuttond"
}

package() {
    make -C "${srcdir}/powerbuttond" install DESTDIR="${pkgdir}"
}
