# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=cachy-update
pkgver=3.15.0
pkgrel=1
pkgdesc="An update notifier & applier that assists you with important pre / post update tasks"
url="https://github.com/CachyOS/cachy-update"
arch=('any')
license=('GPL-3.0-or-later')
depends=('bash' 'systemd' 'pacman' 'pacman-contrib' 'archlinux-contrib' 'curl' 'fakeroot'
         'htmlq' 'diffutils' 'hicolor-icon-theme' 'python' 'python-pyqt6' 'qt6-svg' 'glib2' 'xdg-utils')
makedepends=('scdoc')
checkdepends=('bats')
conflicts=('arch-update')
optdepends=('paru: AUR Packages support'
            'yay: AUR Packages support'
            'pikaur: AUR Packages support'
            'flatpak: Flatpak Packages support'
            'libnotify: Desktop notifications support on new available updates'
            'vim: Default diff program for pacdiff'
            'neovim: Default diff program for pacdiff if EDITOR=nvim'
            'qt6-wayland: Systray applet support on Wayland'
            'sudo: Privilege elevation'
            'opendoas: Privilege elavation')
source=("git+https://github.com/CachyOS/cachy-update#commit=0ae4e47b9dda45410943a5bb61b359c678194f07")
sha256sums=('f37570528d5715c796092268d3919f12635f4f386dff9c5c5f85b17cf885121b')

prepare() {
	cd "${pkgname}"
	make clean
}

build() {
	cd "${pkgname}"
	make
}

check() {
	cd "${pkgname}"
	make test
}

package() {
	cd "${pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
}
