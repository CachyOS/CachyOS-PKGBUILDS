# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=cachy-update
pkgver=3.17.3
pkgrel=1
pkgdesc="An update notifier & applier that assists you with important pre / post update tasks"
url="https://github.com/CachyOS/cachy-update"
arch=('any')
license=('GPL-3.0-or-later')
depends=('bash' 'systemd' 'pacman' 'pacman-contrib' 'archlinux-contrib' 'curl' 'fakeroot'
         'htmlq' 'diffutils' 'hicolor-icon-theme' 'python' 'python-pyqt6' 'qt6-svg' 'glib2' 'xdg-utils')
makedepends=('scdoc')
checkdepends=('bats')
conflicts=('arch-update')
optdepends=('paru: AUR Packages support'
            'yay: AUR Packages support'
            'pikaur: AUR Packages support'
            'flatpak: Flatpak Packages support'
            'libnotify: Desktop notifications support on new available updates'
            'vim: Default diff program for pacdiff'
            'neovim: Default diff program for pacdiff if EDITOR=nvim'
            'sudo: Privilege elevation'
            'sudo-rs: Privilege elevation'
            'opendoas: Privilege elavation')
source=("git+https://github.com/CachyOS/cachy-update#commit=991e6c45665364b286fd87bc389360b4b0a236a7")
sha256sums=('3c538af88fb78f3a872c25971c1c451dee08cd0dc3a3e1238e9c3e7251ac5956')

prepare() {
	cd "${pkgname}"
	make clean
}

build() {
	cd "${pkgname}"
	make
}

check() {
	cd "${pkgname}"
	make test
}

package() {
	cd "${pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
}
