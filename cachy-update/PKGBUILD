# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=cachy-update
pkgver=3.15.1
pkgrel=1
pkgdesc="An update notifier & applier that assists you with important pre / post update tasks"
url="https://github.com/CachyOS/cachy-update"
arch=('any')
license=('GPL-3.0-or-later')
depends=('bash' 'systemd' 'pacman' 'pacman-contrib' 'archlinux-contrib' 'curl' 'fakeroot'
         'htmlq' 'diffutils' 'hicolor-icon-theme' 'python' 'python-pyqt6' 'qt6-svg' 'glib2' 'xdg-utils')
makedepends=('scdoc')
checkdepends=('bats')
conflicts=('arch-update')
optdepends=('paru: AUR Packages support'
            'yay: AUR Packages support'
            'pikaur: AUR Packages support'
            'flatpak: Flatpak Packages support'
            'libnotify: Desktop notifications support on new available updates'
            'vim: Default diff program for pacdiff'
            'neovim: Default diff program for pacdiff if EDITOR=nvim'
            'qt6-wayland: Systray applet support on Wayland'
            'sudo: Privilege elevation'
            'sudo-rs: Privilege elevation'
            'opendoas: Privilege elavation')
source=("git+https://github.com/CachyOS/cachy-update#commit=b069cbba53f43af846c9201465c0c23acac9202f")
sha256sums=('948498d3637da0af83e48a1f656a47041153486d85819df969526ec5c8889885')

prepare() {
	cd "${pkgname}"
	make clean
}

build() {
	cd "${pkgname}"
	make
}

check() {
	cd "${pkgname}"
	make test
}

package() {
	cd "${pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
}
