# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: loathingkernel <loathingkernel _a_ gmail _d_ com>

pkgname=umu-launcher
pkgver=1.2.8
pkgrel=2
pkgdesc="This is the Unified Launcher for Windows Games on Linux, to run Proton with fixes outside of Steam"
arch=('x86_64')
url="https://github.com/Open-Wine-Components/umu-launcher"
license=('GPL-3.0-only')
depends=(
  bash
  desktop-file-utils
  diffutils
  hicolor-icon-theme
  curl
  dbus
  freetype2
  gdk-pixbuf2
  ttf-font
  zenity
  lsb-release
  nss
  usbutils
  xorg-xrandr
  vulkan-driver
  vulkan-icd-loader
  lsof
  python
  xdg-user-dirs
  glibc
  libxcrypt
  libxcrypt-compat
  gcc-libs
  python-xlib
  python-xxhash
  python-pyzstd
  python-cbor2
  python-urllib3
)
depends_x86_64=(
  lib32-glibc
  lib32-libxcrypt
  lib32-libxcrypt-compat
  lib32-libgl
  lib32-gcc-libs
  lib32-libx11
  lib32-libxss
  lib32-alsa-plugins
  lib32-libgpg-error
  lib32-nss
  lib32-vulkan-driver
  lib32-vulkan-icd-loader
)
makedepends=(
  cargo
  git
  scdoc
  python-build
  python-installer
  python-hatchling
  python-maturin
  python-pip
)
optdepends=(
  'python-truststore: Verify certificates using OS trust stores'
)
source=(
    "git+https://github.com/Open-Wine-Components/umu-launcher.git#tag=$pkgver"
    umu-proton-cachyos.patch
)
sha256sums=('40f30899593f2dafda7dcb2074b0b6897d2a5d050ff8bd1413010e925475860e'
            'c0722c2a14ded0aa386e31f120eb7df6a8fe36d3b080db49c5e6e3c9fab41e2d')

prepare() {
  cd "$srcdir"/umu-launcher

  patch -Np1 -i "$srcdir/umu-proton-cachyos.patch"

  export RUSTUP_TOOLCHAIN=stable
  export CARGO_HOME="${srcdir}"/umu-cargo
  cargo update
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$srcdir"/umu-launcher
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_HOME="${srcdir}"/umu-cargo
  ./configure.sh --prefix=/usr --use-system-pyzstd --use-system-urllib
  make
}

package() {
  cd "$srcdir"/umu-launcher
  make DESTDIR="$pkgdir" install
}

