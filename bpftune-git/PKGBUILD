# Maintainer: Kirill Fries-Raevskiy <exocarnivore@gmail.com>
_pkgbase="bpftune"
pkgname="$_pkgbase-git"
pkgver=r545.870d466
pkgrel=1
pkgdesc="BPF/tracing tools for auto-tuning Linux"
arch=("x86_64")
url="https://github.com/oracle-samples/bpftune"
license=('GPLv2 WITH Linux-syscall-note')
depends=("libbpf" "libnl" "libcap")
makedepends=('git' 'bpf' 'clang' 'llvm' 'llvm-libs' 'python-docutils')
provides=("$_pkgbase")
conflicts=("$_pkgbase")
options=('strip')
source=('git+https://github.com/oracle/bpftune'
        'makefile-tweaks.patch')
sha256sums=('SKIP'
            '009a8a03d2b8fee5b06a4cb264d4c2966472e9a512af9a7cd91364cb0c2e11e8')

pkgver() {
    cd "$_pkgbase"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_pkgbase"
    msg2 "Remove native march and use O3 flag"
    patch -Np1 < ../makefile-tweaks.patch
    sed -i 's/\/sbin/\/bin/g' src/Makefile
    sed -i 's/\/sbin/\/bin/g' src/bpftune.service

}

build() {
    cd "$srcdir/$_pkgbase"
    export CC="clang"
    make libdir=lib
}

package() {
    cd "$srcdir/$_pkgbase"
    make DESTDIR="$pkgdir" libdir=lib install
}
