# Maintainer: Kirill Fries-Raevskiy <exocarnivore@gmail.com>
_pkgbase="bpftune"
pkgname="$_pkgbase-git"
pkgver=r464.f2b5ba7
pkgrel=1
pkgdesc="BPF/tracing tools for auto-tuning Linux"
arch=("x86_64")
url="https://github.com/oracle-samples/bpftune"
license=('GPLv2 WITH Linux-syscall-note')
#groups=()
depends=("libbpf" "libnl" "libcap")
makedepends=('git' 'bpf' 'clang' 'llvm' 'llvm-libs' 'python-docutils')
provides=("$_pkgbase")
conflicts=("$_pkgbase")
#replaces=()
#backup=()
options=('strip')
#install=
source=('git+https://github.com/oracle-samples/bpftune'
		'fix-makefile.patch')
#noextract=()
sha256sums=('SKIP'
            'e565afc2bc7bda439b49a73c5bc9719662684ee06d3bababcc2a7496b4723882')

CC=clang

pkgver() {
    cd "$_pkgbase"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_pkgbase"
    patch -Np1 < ../fix-makefile.patch
    sed -i 's/\/lib64/\/lib/' include/bpftune/libbpftune.h
    sed -i 's/\/lib64/\/lib/g' src/Makefile
    sed -i 's/\/sbin/\/bin/g' src/Makefile
    sed -i 's/\/sbin/\/bin/g' src/bpftune.service

}

build() {
    cd "$srcdir/$_pkgbase"
    export CFLAGS="-march=x86-64 -mtune=generic"
    export CXXLAGS="-march=x86-64 -mtune=generic"
    make -j1 || true
    make
}

package() {
    cd "$srcdir/$_pkgbase"
    make DESTDIR="$pkgdir" install
}
