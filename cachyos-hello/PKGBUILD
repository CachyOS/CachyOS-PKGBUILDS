# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=cachyos-hello
_pkgname=CachyOS-Welcome
pkgver=0.16.4
pkgrel=1
pkgdesc='Welcome screen for CachyOS'
arch=('x86_64')
groups=('cachyos')
license=(GPL-3.0-or-later)
url="https://github.com/cachyos/cachyos-welcome"
depends=(
  glib2
  gtk3
  pciutils
  procps-ng
)
makedepends=(
  cargo
  clang
  clang
  git
  lld
  llvm
  meson
)
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('8dc849d4fa202dfd8f1e464176f20a10bc3f5989a4b91f9936b7175e498aae5534d1233d2956293b5160e5663aabd6cdb71b04829e5dedf55480c22f8f62025d')
provides=('cachyos-hello')
conflicts=('cachyos-hello')
options=(strip)

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # Compile with clang
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"

  meson setup --buildtype=release --prefix=/usr build
  meson compile -C build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"/build

  # Compile with clang
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"
  DESTDIR="${pkgdir}" meson install

  install -Dvm644 ../$pkgname.desktop \
    "$pkgdir/etc/skel/.config/autostart/$pkgname.desktop"
}

# vim:set sw=2 sts=2 et:
