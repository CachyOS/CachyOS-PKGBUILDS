# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=cachyos-hello
_pkgname=CachyOS-Welcome
pkgver=0.17.2
pkgrel=1
pkgdesc='Welcome screen for CachyOS'
arch=('x86_64')
groups=('cachyos')
license=(GPL-3.0-or-later)
url="https://github.com/cachyos/cachyos-welcome"
depends=(
  glib2
  gtk3
  pciutils
  procps-ng
)
makedepends=(
  cargo
  clang
  git
  lld
  llvm
  meson
)
source=("${pkgname}::git+$url.git#tag=v${pkgver}?signed")
sha512sums=('acfa97e6da3f8cbd5e5185ad36352936543cae0d81e19e73778438e6a409fdae9a69fbeac2ce8f7f200a14f2084ac19513641f346aa6e677febbf48038e8d1a5')
validpgpkeys=(
  'B1B70BB1CD56047DEF31DE2EB62C3D10C54D5DA9'  # Vladislav Nepogodin <nepogodin.vlad@gmail.com>
)

build() {
  cd "${srcdir}/${pkgname}"

  # Compile with clang
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"

  meson setup --buildtype=release --prefix=/usr build
  meson compile -C build
}

package() {
  cd "${srcdir}/${pkgname}"/build

  # Compile with clang
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"
  DESTDIR="${pkgdir}" meson install

  install -Dvm644 ../$pkgname.desktop \
    "$pkgdir/etc/skel/.config/autostart/$pkgname.desktop"
}

# vim:set sw=2 sts=2 et:
