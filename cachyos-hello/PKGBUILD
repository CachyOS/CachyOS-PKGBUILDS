# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=cachyos-hello
_pkgname=CachyOS-Welcome
pkgver=0.19.2
pkgrel=1
pkgdesc='Welcome screen for CachyOS'
arch=('x86_64')
groups=('cachyos')
license=(GPL-3.0-or-later)
url="https://github.com/cachyos/cachyos-welcome"
depends=(
  glib2
  gtk3
  pciutils
  procps-ng
)
makedepends=(
  cargo
  clang
  git
  lld
  llvm
  meson
)
source=("${pkgname}::git+$url.git#tag=v${pkgver}?signed"
        0001-deps-use-latest-alpm-crate-commit.patch)
sha512sums=('a50061bb7ddb5f0523756014c85e36f2df916fcdd9ac75d7efa5f15556f61f79c8e819ede592355f8d6d58f31e0ec8b7483cf71eea44f6194bd260dce885646a'
            'f29eed07769eaee51c21cf8ed2144dcad9b5815ae0ab1ade1524a20cffc597c37f824abff6d6eb26307851e652c55f41270a59005093367197692b34bb650ec3')
validpgpkeys=(
  'B1B70BB1CD56047DEF31DE2EB62C3D10C54D5DA9'  # Vladislav Nepogodin <nepogodin.vlad@gmail.com>
)

#prepare() {
#  cd "${srcdir}/${pkgname}"
#
#  # patch for pacman 7.1.0
#  patch -Np1 -i ../0001-deps-use-latest-alpm-crate-commit.patch
#}

build() {
  cd "${srcdir}/${pkgname}"

  # Compile with clang
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"

  meson setup --buildtype=release --prefix=/usr build
  meson compile -C build
}

package() {
  cd "${srcdir}/${pkgname}"/build

  # Compile with clang
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"
  DESTDIR="${pkgdir}" meson install

  install -Dvm644 ../$pkgname.desktop \
    "$pkgdir/etc/skel/.config/autostart/$pkgname.desktop"
}

# vim:set sw=2 sts=2 et:
