# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=egl-wayland2
pkgver=1.0.0
pkgrel=2
pkgdesc="new EGLStream-based Wayland external platform"
url="https://github.com/NVIDIA/egl-wayland2"
arch=(x86_64)
license=(MIT)
depends=(
  eglexternalplatform
  glibc
  libdrm
  wayland
)
makedepends=(
  git
  'libglvnd>=1.3.4'
  meson
  wayland-protocols
)
provides=(libnvidia-egl-wayland2.so)
source=("git+$url#commit=3e5b643542708cbb9c67d18fe19b5fa4151d7d8f")
b2sums=('5ce301220b520a93880a3cb7e5c185840de49b9f47ae951dac464c43a9195810c3e5c6297e6f80b3ac797c15acca03a1fa8b596e151eea7eebc1192e73c1dfd4')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
#  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set sw=2 sts=-1 et:
