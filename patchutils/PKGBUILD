# Contributor: Aurelien Foret <orelien@chez.com>
# Maintainer: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=patchutils
pkgver=0.4.4
pkgrel=1.2
pkgdesc="A small collection of programs that operate on patch files"
license=('GPL')
url="http://cyberelk.net/tim/patchutils/"
depends=('python3' 'pcre2')
makedepends=('xmlto')
arch=('x86_64')
source=("https://github.com/twaugh/patchutils/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('2008241ee5d4f87ada3f743c719cc010b0ba929126e255f23c3fb48f614c7e32'
            'SKIP')
validpgpkeys=('4629AFE960EC20BEC12E3104B7C20D079491EA63') # Tim Waugh <tim@cyberelk.net>

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}"/usr/share/bash-completion/completions/interdiff
}
