# Maintainer: Peter Jung <ptr1337@cachyos.org>

pkgname=occt
pkgver=14.0.9
pkgrel=1
pkgdesc="OverClock Checking Tool - CPU/GPU stress testing and monitoring (binary)"
arch=('x86_64')
url="https://www.ocbase.com/"
license=('LicenseRef-Proprietary')
depends=(
  glibc
  gcc-libs
  zlib
)
options=(!strip !debug)
source=(
  "occt-${pkgver}::https://www.ocbase.com/download/edition:Personal/os:Linux/version:$pkgver"
  "occt.desktop"
  "occt.svg"
  "eula.txt"
)
b2sums=('10687edc103149626f82deee3510f384e1244377842f6eaa5ba190408ae36173ac8b5468a1a1209d1e48803a15a770e54a7def422bc6b92cbeef29108641b0d0'
        '20027853572302a9c4ac95b81c52364ffdbe925515d69caa8d08063e7872dc54449e6ca2938b68a41a6047475637ea0a2b1cb6743d03738b97b38d4caa617b37'
        'e12687498e34530906bbc8e67ac41d9a7c00cf8c08936b596fcb2d2486b7c294aeb0ee73d6d618d3eafe012377c2caff15741f123a47c26abb1e9aca99d1d348'
        'c27bfa10944aa8f945f890a351de0e0bd2ceb17f84494ddcb99be94ed173bd48388c537c8908b3b5ea8606a2778aab6c075b7575ab4ef389cf68cb1a7c74f3b0')

prepare() {
  # Make the downloaded binary executable
  chmod +x "occt-${pkgver}"
}

package() {
  cd "$srcdir"

  install -Dm755 "occt-${pkgver}" "$pkgdir/opt/$pkgname/$pkgname"

  # Disable automatic updates in future release
  touch "$pkgdir/opt/$pkgname/disable_update"
  # Use configs in home dir
  touch "$pkgdir/opt/occt/use_home_config"

  # Add symlink for easier execution and detection
  install -d "$pkgdir/usr/bin"
  ln -s /opt/$pkgname/$pkgname "$pkgdir/usr/bin/$pkgname" 
  
  install -Dm644 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"

  # EULA
  install -Dm644 "eula.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: set ts=2 sw=2 et:
