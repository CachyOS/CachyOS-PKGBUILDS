# Maintainer: loathingkernel <loathingkernel _a_ gmail _d_ com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=wine-cachyos-opt
_srctag=9.0-20241123
pkgver=${_srctag//-/.}
pkgrel=1
epoch=2

_pkgbasever=${pkgver/rc/-rc}
_winever=$_pkgbasever
#_winever=${_pkgbasever%.*}

source=(wine-cachyos::git+https://github.com/CachyOS/wine-cachyos.git#tag=cachyos-${_srctag}-wine
        30-win32-aliases.conf
        wine-binfmt.conf)
source+=(
)
validpgpkeys=(5AC1A08B03BD7A313E0A955AF5E6E9EEB9461DD7
              DA23579A74D4AD9AF9D3F945CEFAC8EAAF17519D)

pkgdesc="A compatibility layer for running Windows programs, with extra CachyOS flavor (installs under /opt)"
url="https://github.com/CachyOS/wine-cachyos"
arch=(x86_64 x86_64_v3)
options=(!staticlibs !lto !debug)
license=(LGPL-2.1-or-later)

depends=(
  attr             lib32-attr
  fontconfig       lib32-fontconfig
  libxcursor       lib32-libxcursor
  libxrandr        lib32-libxrandr
  libxi            lib32-libxi
  gettext          lib32-gettext
  freetype2        lib32-freetype2
  llvm-libs        lib32-llvm-libs
  libpcap          lib32-libpcap
  desktop-file-utils
)
depends+=(
  libxkbcommon     lib32-libxkbcommon
  wayland          lib32-wayland
)

makedepends=(autoconf bison perl flex clang lld
  git
  python
  llvm                  lib32-llvm
  giflib                lib32-giflib
  gnutls                lib32-gnutls
  libxinerama           lib32-libxinerama
  libxcomposite         lib32-libxcomposite
  libxxf86vm            lib32-libxxf86vm
  v4l-utils             lib32-v4l-utils
  alsa-lib              lib32-alsa-lib
  libxcomposite         lib32-libxcomposite
  mesa                  lib32-mesa
  mesa-libgl            lib32-mesa-libgl
  opencl-icd-loader     lib32-opencl-icd-loader
  libpulse              lib32-libpulse
  libva                 lib32-libva
  gtk3                  lib32-gtk3
  gst-plugins-base-libs lib32-gst-plugins-base-libs
  gst-plugins-good      lib32-gst-plugins-good
  vulkan-icd-loader     lib32-vulkan-icd-loader
  sdl2                  lib32-sdl2
  libcups               lib32-libcups
  sane
  libgphoto2
  ffmpeg
  samba
  opencl-headers
  git
)

optdepends=(
  giflib                lib32-giflib
  gnutls                lib32-gnutls
  v4l-utils             lib32-v4l-utils
  libpulse              lib32-libpulse
  alsa-plugins          lib32-alsa-plugins
  alsa-lib              lib32-alsa-lib
  libxcomposite         lib32-libxcomposite
  libxinerama           lib32-libxinerama
  opencl-icd-loader     lib32-opencl-icd-loader
  libva                 lib32-libva
  gtk3                  lib32-gtk3
  gst-plugins-base-libs lib32-gst-plugins-base-libs
  gst-plugins-good      lib32-gst-plugins-good
  vulkan-icd-loader     lib32-vulkan-icd-loader
  sdl2                  lib32-sdl2
  sane
  libgphoto2
  ffmpeg
  cups
  samba           dosbox
)
optdepends+=(
  wine
)

install=wine.install

prepare() {
  # Get rid of old build dirs
  rm -rf ${pkgname//-opt}-{32,64}-build
  mkdir ${pkgname//-opt}-{32,64}-build

  pushd ${pkgname//-opt}
      git config user.email "wine@cachyos.org"
      git config user.name "wine cachyos"
      git tag wine-9.0 --annotate -m "$pkgver" --force
      ./dlls/winevulkan/make_vulkan -x vk.xml
      ./tools/make_specfiles
      ./tools/make_requests
      autoreconf -fiv
  popd
}

build() {
  export CC="clang"
  export CXX="clang++"

  local -a split=($CFLAGS)
  local -A flags
  for opt in "${split[@]}"; do flags["${opt%%=*}"]="${opt##*=}"; done
  local march="${flags["-march"]:-nocona}"
  local mtune="${flags["-mtune"]:-core-avx2}"

  # From Proton
  OPTIMIZE_FLAGS="-O3 -march=$march -mtune=$mtune -mfpmath=sse -pipe -fno-semantic-interposition"
  SANITY_FLAGS="-fwrapv -fno-strict-aliasing"
  WARNING_FLAGS="-Wno-incompatible-pointer-types"
  #STRIP_FLAGS="-s"
  COMMON_FLAGS="$OPTIMIZE_FLAGS $SANITY_FLAGS $WARNING_FLAGS $STRIP_FLAGS"

  COMMON_LDFLAGS="-Wl,-O1,--sort-common,--as-needed"
  LTO_CFLAGS="-flto=thin -D__LLD_LTO__"
  LTO_LDFLAGS="-flto=thin -fuse-ld=lld"

  # Per component CFLAGS and LDFlAGS (requires makedep patch)
  export preloader_CFLAGS=" -fno-lto -Wl,--no-relax"
  export wine64_preloader_LDFLAGS=" -fno-lto -Wl,--no-relax"
  export wine_preloader_LDFLAGS=" -fno-lto -Wl,--no-relax"

  # Disable assertions
  #export CPPFLAGS="-U_FORTIFY_SOURCE -D_FORTIFY_SOURCE=0 -DNDEBUG -D_NDEBUG"

  export LDFLAGS="$COMMON_LDFLAGS $LTO_LDFLAGS"
  export CROSSLDFLAGS="-Wl,/FILEALIGN:4096,/OPT:REF,/OPT:ICF"

  cd "$srcdir"

  msg2 "Building Wine-64..."

  export CFLAGS="$COMMON_FLAGS -mcmodel=small $LTO_CFLAGS"
  export CXXFLAGS="$COMMON_FLAGS -mcmodel=small -std=c++17 $LTO_CFLAGS"
  export CROSSCFLAGS="$COMMON_FLAGS -mcmodel=small"
  export CROSSCXXFLAGS="$COMMON_FLAGS -mcmodel=small -std=c++17"
  export PKG_CONFIG_PATH="/usr/lib/pkgconfig:/usr/share/pkgconfig"
  cd "$srcdir/${pkgname//-opt}-64-build"
  ../${pkgname//-opt}/configure \
    --prefix=/opt/"${pkgname//-opt}" \
    --libdir=/opt/"${pkgname//-opt}"/lib \
    --with-x \
    --with-wayland \
    --with-gstreamer \
    --with-mingw=clang \
    --with-alsa \
    --without-oss \
    --disable-winemenubuilder \
    --disable-tests \
    --enable-win64 \
    --with-xattr

  make

  msg2 "Building Wine-32..."

  # Disable AVX instead of using 02, for 32bit
  export CFLAGS="$COMMON_FLAGS -mstackrealign $LTO_CFLAGS"
  export CXXFLAGS="$COMMON_FLAGS -mstackrealign -std=c++17 $LTO_CFLAGS"
  export CROSSCFLAGS="$COMMON_FLAGS -mstackrealign"
  export CROSSCXXFLAGS="$COMMON_FLAGS -mstackrealign -std=c++17"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig:/usr/share/pkgconfig"
  cd "$srcdir/${pkgname//-opt}-32-build"
  ../${pkgname//-opt}/configure \
    --prefix=/opt/"${pkgname//-opt}" \
    --with-x \
    --with-wayland \
    --with-gstreamer \
    --with-mingw=clang \
    --with-alsa \
    --without-oss \
    --disable-winemenubuilder \
    --disable-tests \
    --with-xattr \
    --libdir=/opt/"${pkgname//-opt}"/lib32 \
    --with-wine64="$srcdir/${pkgname//-opt}-64-build"

  make
}

package() {
  msg2 "Packaging Wine-32..."
  cd "$srcdir/${pkgname//-opt}-32-build"

  make prefix="$pkgdir/opt/${pkgname//-opt}" \
    libdir="$pkgdir/opt/${pkgname//-opt}/lib32" \
    dlldir="$pkgdir/opt/${pkgname//-opt}/lib32/wine" install-lib

  msg2 "Packaging Wine-64..."
  cd "$srcdir/${pkgname//-opt}-64-build"
  make prefix="$pkgdir/opt/${pkgname//-opt}" \
    libdir="$pkgdir/opt/${pkgname//-opt}/lib" \
    dlldir="$pkgdir/opt/${pkgname//-opt}/lib/wine" install-lib


  llvm-strip --strip-unneeded "$pkgdir"/opt/"${pkgname//-opt}"/lib32/wine/i386-windows/*.{dll,exe}
  llvm-strip --strip-unneeded "$pkgdir"/opt/"${pkgname//-opt}"/lib/wine/x86_64-windows/*.{dll,exe}

  find "$pkgdir"/opt/"${pkgname//-opt}"/lib{,32}/wine -iname "*.a" -delete
  find "$pkgdir"/opt/"${pkgname//-opt}"/lib{,32}/wine -iname "*.def" -delete
}

# vim:set ts=8 sts=2 sw=2 et:
b2sums=('464e34f57778e2fda38fe0b6717732ee06717bf8d87d4890f85d5ddd6feeda6ac73c944585fcfedcda1169303ae586b080e8fe27eac459f1ef441ea6fae29988'
        '45db34fb35a679dc191b4119603eba37b8008326bd4f7d6bd422fbbb2a74b675bdbc9f0cc6995ed0c564cf088b7ecd9fbe2d06d42ff8a4464828f3c4f188075b'
        'e9de76a32493c601ab32bde28a2c8f8aded12978057159dd9bf35eefbf82f2389a4d5e30170218956101331cf3e7452ae82ad0db6aad623651b0cc2174a61588')
