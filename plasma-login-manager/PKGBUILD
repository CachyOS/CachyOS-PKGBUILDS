# Maintainer: Peter Jung <ptr1337@cachyos.org>
# Maintainer: VolRen <VolRencs@proton.me>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-login-manager
pkgver=6.6.1
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=3
arch=(x86_64)
pkgdesc='Plasma Login Manager'
url='https://kde.org/plasma-desktop/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kauth
         kcmutils
         kconfig
         kcoreaddons
         kdbusaddons
         ki18n
         kio
         kirigami
         kpackage
         kservice
         kwindowsystem
         layer-shell-qt
         libplasma
         libxau
         pam
         plasma-workspace
         qt6-5compat
         qt6-base
         qt6-declarative
         sh
         systemd-libs)
makedepends=(extra-cmake-modules
             git
             qt6-tools)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig}
       https://invent.kde.org/plasma/plasma-login-manager/-/merge_requests/112.patch
       https://invent.kde.org/plasma/plasma-login-manager/-/merge_requests/117.patch)
sha256sums=('af7f911139a8ae4cd5e32800facb412f3ddda6dbd9d0fa27a3f4c6a6f98d6f39'
            'SKIP'
            '128b2f76a8ddb0b58a9acf32a667e10a28d5f3db3864058d2cd42f3e17103034'
            '123278d455ef386494b91997209406bd3bcd16c5acd14329281822d0f17f2afc')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>
prepare() {
  cd $pkgname-$pkgver
  patch -Np1 < ../112.patch
  # Fix for saving default with KCM
  patch -Np1 < ../117.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DDBUS_CONFIG_FILENAME=plasma_org.freedesktop.DisplayManager.conf
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
