# Maintainer: Konstantin Rannev <konstantin.rannev@gmail.com>
# Contributor: Ash <xash at riseup d0t net>

pkgname=lsfg-vk
pkgver=r143.cebe5e2
pkgrel=1
pkgdesc="Lossless Scaling Frame Generation on Linux via DXVK/Vulkan"
arch=('x86_64')
url="https://github.com/PancakeTAS/lsfg-vk"
license=('MIT')
depends=(
  vulkan-icd-loader
)
makedepends=(
  clang
  llvm
  vulkan-headers
  cmake
  meson
  ninja
  git
  sed
  sdl2
  glslang
  spirv-headers
)
source=('git+https://github.com/PancakeTAS/lsfg-vk#commit=cebe5e2e7a5bc671183791e880a5e70301676106'
		'git+https://github.com/PancakeTAS/dxbc.git#commit=04ca5e9ae5fef6c0c65ea72bbaa7375327f11454'
		'git+https://github.com/trailofbits/pe-parse#commit=31ac5966503689d5693cd9fb520bd525a8710e17')
sha256sums=('8781e41b5ed773366d4ccba65b31d58412e3be89403ba74c4ce552fb563b463d'
            'afdd34616c8ed06c01966e04f245232346a1ea8dd5adaa7167b7225eedcf083e'
            '0cafae771cb547b6f7e48121896dafa44990d2e7fb1fc50fcde1bba5438cd254')
install=lsfg-vk.install

pkgver() {
  cd "$srcdir/$pkgname"

  # Git, no tags available
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$pkgname"

  git submodule init
  
  git config submodule.dxbc.url "$srcdir/dxbc"
  git config submodule.pe-parse.url "$srcdir/pe-parse"
  
  git -c protocol.file.allow=always submodule update
}

build() {
  cd "$srcdir/$pkgname"

  cmake -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
    -DCMAKE_CXX_CLANG_TIDY="" \
    -DCMAKE_SHARED_LINKER_FLAGS="-Wl,-z,lazy" # fixes makepkg's default "-z,now" flag which strips out the necessary symbols
  cmake --build build
}

package() {
  cd "$srcdir/$pkgname"

  install -Dm644 VkLayer_LS_frame_generation.json "$pkgdir/etc/vulkan/implicit_layer.d/VkLayer_LS_frame_generation.json"
  install -Dm644 build/liblsfg-vk.so "$pkgdir/usr/lib/liblsfg-vk.so"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
