
pkgname=sudo-rs-compat
pkgver=0.2.8
pkgrel=1
pkgdesc='A memory-safe implementation of sudo (as drop-in)'
arch=(any)
url=https://github.com/trifectatechfoundation/sudo-rs
license=('Apache-2.0 AND MIT')
depends=(sudo-rs)
provides=(sudo)
conflicts=(sudo)
backup=(etc/{pam.d/sudo,sudoers-rs})
source=(sudoers-rs # simple example is introduced at $url
"https://gitlab.archlinux.org/archlinux/packaging/packages/sudo/-/raw/1.9.17.p2-1/sudo.pam")
sha256sums=('5ba40c3186bf554c7af801c265f7123b68c6b4d931cc5afeccc81bd9a9e83dbb'
            'd1738818070684a5d2c9b26224906aad69a4fea77aabd960fc2675aee2df1fa2')
package (){
  install -Dm644 sudo.pam "$pkgdir"/etc/pam.d/sudo
  ln -sf /etc/pam.d/sudo "$pkgdir"/etc/pam.d/sudo-i
  install -Dm0440 sudoers-rs "$pkgdir"/etc/sudoers-rs
  install -d "$pkgdir"/etc/sudoers.d # remove some warning from devtools
  options+=(emptydirs)
  install -d "$pkgdir"/usr/{bin,share/man/man{5,8}}
  for _b in sudo sudoedit visudo; do
    ln -sf /usr/bin/${_b}-rs "$pkgdir"/usr/bin/$_b
    ln -sf /usr/share/man/man8/${_b}-rs.8.gz "$pkgdir"/usr/share/man/man8/${_b}.8.gz
  done
  ln -sf /usr/share/man/man5/sudoers-rs.5.gz "$pkgdir"/usr/share/man/man5/sudoers.5.gz
}
