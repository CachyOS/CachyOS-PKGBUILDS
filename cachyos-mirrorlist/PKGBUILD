# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Vladislav Nepogodin (vnepogodin) <vnepogodin@cachyos.org>

pkgbase=cachyos-mirrorlist
pkgname=('cachyos-mirrorlist' 'cachyos-v3-mirrorlist' 'cachyos-v4-mirrorlist')
pkgver=24
pkgrel=1
pkgdesc="CachyOS repository mirrorlist"
arch=('any')
license=(GPL-3.0-or-later)
groups=(cachyos)
source=(cachyos-mirrorlist)
sha512sums=('ebec86cc847076fefb45b245a06169749dbc6ef9695cc0ac60e7631d5c12a00e524b5adbf1eade93af372a58ac50df0606b5f39d22de86b8edf2a42586fa8f6a')

package_cachyos-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
}

package_cachyos-v3-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
 sed -i 's|/$arch/|/$arch_v3/|g' "$pkgdir/etc/pacman.d/$pkgname"
}

package_cachyos-v4-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
 sed -i 's|/$arch/|/$arch_v4/|g' "$pkgdir/etc/pacman.d/$pkgname"
}
