# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Vladislav Nepogodin (vnepogodin) <vnepogodin@cachyos.org>

pkgbase=cachyos-mirrorlist
pkgname=('cachyos-mirrorlist' 'cachyos-v3-mirrorlist' 'cachyos-v4-mirrorlist')
pkgver=26
pkgrel=1
pkgdesc="CachyOS repository mirrorlist"
url='https://github.com/CachyOS/CachyOS-PKGBUILDS'
arch=('any')
license=(GPL-3.0-or-later)
groups=(cachyos)
source=(cachyos-mirrorlist)
sha512sums=('a9dfa521f3f6bed844119cc5aa9f34d7fed70f42ac0b030060b37322428a7d04ca5de7a1d6f2aef0302ab2ac10a1f775a6c21df9abec03f59e9f9191355b005f')

package_cachyos-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
}

package_cachyos-v3-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
 sed -i 's|/$arch/|/$arch_v3/|g' "$pkgdir/etc/pacman.d/$pkgname"
}

package_cachyos-v4-mirrorlist() {
 backup=("etc/pacman.d/$pkgname")  # to keep possibly ranked mirrorlist
 install -Dm644 "$srcdir/$pkgbase" "$pkgdir/etc/pacman.d/$pkgname"
 sed -i 's|/$arch/|/$arch_v4/|g' "$pkgdir/etc/pacman.d/$pkgname"
}
