# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=nvidia-open-dkms
pkgver=560.35.03
pkgrel=12
pkgdesc="NVIDIA open kernel modules"
arch=('x86_64')
url="https://github.com/NVIDIA/open-gpu-kernel-modules"
depends=("nvidia-utils" 'libglvnd')
license=('MIT AND GPL-2.0-only')
source=("$pkgname-$pkgver.tar.gz::https://github.com/NVIDIA/open-gpu-kernel-modules/archive/refs/tags/${pkgver}.tar.gz"
        0001-Make-modeset-and-fbdev-default-enabled.patch
        0002-Do-not-error-on-unkown-CPU-Type-and-add-Zen5-support.patch
        0003-Add-IBT-Support.patch
        0004-6.11-Add-fix-for-fbdev.patch
        0005-6.12-drm_outpull_pill-changed-check.patch
        0006-Fix-for-6.12.0-rc1-drm_mode_config_funcs.output_poll.patch
        0007-Replace-PageSwapCache-for-6.12-kernel.patch
        0008-silence-event-assert-until-570.patch)
sha512sums=('a0cbe05fc8acbb4769fa5320d6bfe2033fd31775036e984278cdf7e67ebd801bd8991d4d1626884a4ff729d3900c969f385caea7ae049e3d918a1ea60e45890a'
            '77695f4140b9beaa754c2358805e97bc79c7d0dbb0e45235f5718fea3a8c17036c4f955be4e1cd8ae36234a87086ba086adbdbd3831cd79926feed41ae1f7215'
            '629af798aec732381db0d0a05eac350b65abc9e3d5be70dec638f1deff5e35f506cae6235dccdb80b42614d2cf6d307d0dc548bb3049ff271c79e8e91b6e4c69'
            'e45b1605183ae16af2f52464264833c8854f79f9b3cc202d4a0534b9340a37f08dbd75ec6f664bb2d6b2358bb8d4619c647d64d793b58ffb77267c105df5e8b2'
            '3b6357627ad4672d864b985088908d43f7718dc428e8901c9d27101fd2e9bc109c84e2b414688bbcaa9a05c1e7e55434fff06bf1df5ed33175f1e8bac95dc557'
            '6e0b62f776ded40ab0ac8a2a5616d84723d98ef1ee55ec732b9303207909d5712e3d36273be97a6a0a13f1ff6d798ba9f1b62fc08d75f4e05a606083d725e093'
            'cb4c55f7d9aeac38cf583521bc8781bcffe6026658f9981a7bb6e2749a7668c620b7fdd3cdd32b50e27da0924572ffe36870070d82bb5c620d04adfc6e3900a3'
            '4c6774a1664391104dba2ee1d21a0a6afaa61357b6afedb2def093feb0c893d6ecdfd943cac3f9610e014ed5797f871a873ad600befcb9cbd5d6f5d05f4f8d8d'
            '8f0d0a4881588e10681060d6006a6c65108a753c3106a1a710cf90f8dba8e52e6d6c10633f8ad19b763a2ab119ef98fddc6db4481262daf644c0206ac2ecd2d9')

prepare() {
  cd open-gpu-kernel-modules-${pkgver}

  patch -Np1 < "$srcdir"/0001-Make-modeset-and-fbdev-default-enabled.patch
  patch -Np1 < "$srcdir"/0002-Do-not-error-on-unkown-CPU-Type-and-add-Zen5-support.patch
  patch -Np1 < "$srcdir"/0003-Add-IBT-Support.patch
  patch -Np1 < "$srcdir"/0004-6.11-Add-fix-for-fbdev.patch
  patch -Np1 < "$srcdir"/0006-Fix-for-6.12.0-rc1-drm_mode_config_funcs.output_poll.patch
  patch -Np1 < "$srcdir"/0007-Replace-PageSwapCache-for-6.12-kernel.patch
  patch -Np1 < "$srcdir"/0008-silence-event-assert-until-570.patch

  # Attempt to make this reproducible
  sed -i "s/^HOSTNAME.*/HOSTNAME = echo cachyos"/ utils.mk
  sed -i "s/^WHOAMI.*/WHOAMI = echo cachybuilder"/ utils.mk
  sed -i "s/^DATE.*/DATE = date -r version.mk"/ utils.mk

  sed -i "s/__VERSION_STRING/${pkgver}/" kernel-open/dkms.conf
  sed -i 's/__JOBS/`nproc`/' kernel-open/dkms.conf
  sed -i 's/__EXCLUDE_MODULES//' kernel-open/dkms.conf
  sed -i 's/__DKMS_MODULES//' kernel-open/dkms.conf
  sed -i '$i\
BUILT_MODULE_NAME[0]="nvidia"\
BUILT_MODULE_LOCATION[0]="kernel-open"\
DEST_MODULE_LOCATION[0]="/kernel/drivers/video"\
BUILT_MODULE_NAME[1]="nvidia-uvm"\
BUILT_MODULE_LOCATION[1]="kernel-open"\
DEST_MODULE_LOCATION[1]="/kernel/drivers/video"\
BUILT_MODULE_NAME[2]="nvidia-modeset"\
BUILT_MODULE_LOCATION[2]="kernel-open"\
DEST_MODULE_LOCATION[2]="/kernel/drivers/video"\
BUILT_MODULE_NAME[3]="nvidia-drm"\
BUILT_MODULE_LOCATION[3]="kernel-open"\
DEST_MODULE_LOCATION[3]="/kernel/drivers/video"\
BUILT_MODULE_NAME[4]="nvidia-peermem"\
BUILT_MODULE_LOCATION[4]="kernel-open"\
DEST_MODULE_LOCATION[4]="/kernel/drivers/video"' kernel-open/dkms.conf

  # Clean version for later copying for DKMS
  cp -r ../open-gpu-kernel-modules-${pkgver} "$srcdir"/open-gpu-kernel-modules-dkms
}

package_nvidia-open-dkms() {
  depends+=('dkms')
  conflicts=('nvidia-open' 'NVIDIA-MODULE')
  provides=('nvidia-open' 'NVIDIA-MODULE')

  install -dm 755 "${pkgdir}"/usr/src
  # cp -dr --no-preserve='ownership' kernel-open "${pkgdir}/usr/src/nvidia-$pkgver"
  cp -dr --no-preserve='ownership' open-gpu-kernel-modules-dkms "${pkgdir}/usr/src/nvidia-$pkgver"
  mv "${pkgdir}/usr/src/nvidia-$pkgver/kernel-open/dkms.conf" "${pkgdir}/usr/src/nvidia-$pkgver/dkms.conf"

  install -Dm644 open-gpu-kernel-modules-${pkgver}/COPYING "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set sw=2 et:
