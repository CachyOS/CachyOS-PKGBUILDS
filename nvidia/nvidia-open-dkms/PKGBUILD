# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=nvidia-open-dkms
pkgver=555.52.04
pkgrel=3
pkgdesc="NVIDIA open kernel modules"
arch=('x86_64')
url="https://github.com/NVIDIA/open-gpu-kernel-modules"
depends=("nvidia-utils=${pkgver}" 'libglvnd')
makedepends=('linux-cachyos-headers')
license=('GPL')
source=("$pkgname-$pkgver.tar.gz::https://github.com/NVIDIA/open-gpu-kernel-modules/archive/refs/tags/${pkgver}.tar.gz"
        nvidia-open-gcc-ibt-sls.patch
        make-modeset-fbdev-default.patch
        gsp-fix-stutter.patch)
sha512sums=('cedb61548abe81e60114cb7eefc8a3a334b5a034408840b188c62126fc3c683e764db8887d9ccde3bc0bc012f4c77ef17b57c5b1971acd8be515dc5d7b652e18'
            '263c4c5e75ef8cb8ca2641c022dfaf8bd9222fadf68ed15120b0ae7dd9cc901a04ce2e55625d513a0995759c9d82dfbdc4c33d4751159124915d7404b1400e34'
            'f19a7d52c6e7f5bd6818658c2b11adaaf4ad05016ec0acc73a42417748ed4bf81b8394c077a6f5aba9dfcf6ab01a578692ed1e9d3c2ab583fdaa56166bba05f2'
            '8caa8e237139fcafab7d33aeabad592091a01b113e9afea6375e8486ec5fd706bf3e9ec98db98944450910bec704d573faf8201dbe8fed639d920cf38d81562b')

prepare() {
  cd open-gpu-kernel-modules-${pkgver}
  msg2 "Put modeset and fbdev=1 as default"
  patch -Np1 < "$srcdir"/make-modeset-fbdev-default.patch
  msg2 "Patch NVIDIA module for IBT"
  patch -Np1 < "$srcdir"/nvidia-open-gcc-ibt-sls.patch
  msg2 "Fix Stutters, when GSP Firmware is used"
  patch -Np1 < ../gsp-fix-stutter.patch

  # Attempt to make this reproducible
  sed -i "s/^HOSTNAME.*/HOSTNAME = echo cachyos"/ utils.mk
  sed -i "s/^WHOAMI.*/WHOAMI = echo cachybuilder"/ utils.mk
  sed -i "s/^DATE.*/DATE = date -r version.mk"/ utils.mk

  sed -i "s/__VERSION_STRING/${pkgver}/" kernel-open/dkms.conf
  sed -i 's/__JOBS/`nproc`/' kernel-open/dkms.conf
  sed -i 's/__EXCLUDE_MODULES//' kernel-open/dkms.conf
  sed -i 's/__DKMS_MODULES//' kernel-open/dkms.conf
  sed -i '$i\
BUILT_MODULE_NAME[0]="nvidia"\
BUILT_MODULE_LOCATION[0]="kernel-open"\
DEST_MODULE_LOCATION[0]="/kernel/drivers/video"\
BUILT_MODULE_NAME[1]="nvidia-uvm"\
BUILT_MODULE_LOCATION[1]="kernel-open"\
DEST_MODULE_LOCATION[1]="/kernel/drivers/video"\
BUILT_MODULE_NAME[2]="nvidia-modeset"\
BUILT_MODULE_LOCATION[2]="kernel-open"\
DEST_MODULE_LOCATION[2]="/kernel/drivers/video"\
BUILT_MODULE_NAME[3]="nvidia-drm"\
BUILT_MODULE_LOCATION[3]="kernel-open"\
DEST_MODULE_LOCATION[3]="/kernel/drivers/video"\
BUILT_MODULE_NAME[4]="nvidia-peermem"\
BUILT_MODULE_LOCATION[4]="kernel-open"\
DEST_MODULE_LOCATION[4]="/kernel/drivers/video"' kernel-open/dkms.conf

  # Clean version for later copying for DKMS
  cp -r ../open-gpu-kernel-modules-${pkgver} "$srcdir"/open-gpu-kernel-modules-dkms
}

package_nvidia-open-dkms() {
  depends+=('dkms')
  conflicts=('nvidia-open' 'NVIDIA-MODULE')
  provides=('nvidia-open' 'NVIDIA-MODULE')

  install -dm 755 "${pkgdir}"/usr/src
  # cp -dr --no-preserve='ownership' kernel-open "${pkgdir}/usr/src/nvidia-$pkgver"
  cp -dr --no-preserve='ownership' open-gpu-kernel-modules-dkms "${pkgdir}/usr/src/nvidia-$pkgver"
  mv "${pkgdir}/usr/src/nvidia-$pkgver/kernel-open/dkms.conf" "${pkgdir}/usr/src/nvidia-$pkgver/dkms.conf"

  # Force module to load even on unsupported GPUs
  mkdir -p "$pkgdir"/usr/lib/modprobe.d
  echo "options nvidia NVreg_OpenRmEnableUnsupportedGpus=1" > "$pkgdir"/usr/lib/modprobe.d/nvidia-open.conf

  install -Dm644 open-gpu-kernel-modules-${pkgver}/COPYING "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set sw=2 et:
