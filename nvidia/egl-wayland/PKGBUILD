# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=egl-wayland
pkgver=1.1.15
pkgrel=3
epoch=4
pkgdesc="EGLStream-based Wayland external platform"
url="https://github.com/NVIDIA/egl-wayland"
arch=(x86_64)
license=(MIT)
depends=(
  eglexternalplatform
  glibc
  libdrm
  wayland
)
makedepends=(
  git
  'libglvnd>=1.3.4'
  meson
  wayland-protocols
)
provides=(libnvidia-egl-wayland.so)
source=(
  "git+$url#commit=5a5b17e9f0f18709e08a6ee08adfd27b56f2e601"
  10_nvidia_wayland.json
  "fix-qt6webengine.patch::$url/commit/f30cb0e4c9a215e933dc8250f5dad4e96d4f2136.patch"
)
b2sums=('0ced3f4b32516f315d7d39894efc1416dbd703537d8722dbe261c8274beab86c2f029534be769d65f2ab5ed2d9df0da7bc8e1e8bfb360074c834ad1e68667a67'
        'b10206c742e8966d1192b9b0604137e6b296d2be74a437841c63844c0716343578b11565a34fb4c534d5908c0b5775305581b68039a6ff9ed7421c9d385a2b7a'
        '85cddfcf9e75f3c805cfe78dafd8341484beb222873caec9eaa2dd3fec85552562eaff5867102d1c54f1e0e0326630e08eb10534de4d1e38c491587a6e6337e4')

prepare() {
  cd $pkgname
  patch -Np1 < ../fix-qt6webengine.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/egl/egl_external_platform.d" -m644 10_nvidia_wayland.json
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set sw=2 sts=-1 et:
