# Maintainer: Haden Collins <collinshaden@gmail.com>

pkgname='wlogout'
pkgver=1.2.1
pkgrel=1
pkgdesc='Logout menu for wayland'
arch=('x86_64')
license=('MIT')
url="https://github.com/ArtsyMacaw/wlogout"
depends=('gtk3' 'gobject-introspection' 'gtk-layer-shell')
makedepends=('meson' 'git' 'scdoc')
optdepends=("swaylock: default buttons")
source=("$pkgname-$pkgver.tar.gz::https://github.com/ArtsyMacaw/$pkgname/releases/download/$pkgver/$pkgname.tar.gz"
        "$pkgname-$pkgver.tar.gz.sig::https://github.com/ArtsyMacaw/$pkgname/releases/download/$pkgver/$pkgname.tar.gz.sig"
)
validpgpkeys=("F4FDB18A9937358364B276E9E25D679AF73C6D2F")
sha256sums=('5745f6258354aa7646969bf85271ea0fd20579358040a5cd64cbcbc691b606a1'
            'SKIP')

build() {
  cd "${srcdir}"

  arch-meson build
  ninja -C build
}

package() {
  cd "${srcdir}/build"
  DESTDIR="${pkgdir}" meson install

  cd "${srcdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
