# Maintainer: Adrian Perez de Castro <aperez@igalia.com>

pkgname=wcm
pkgdesc='Wayfire Configuration Manager'
pkgver=0.7.0
pkgrel=1
arch=(x86_64)
url=https://wayfire.org
license=(custom:MIT)
depends=('wayfire>=0.7' 'wf-shell>=0.7' gtk3)
makedepends=(wayland-protocols meson ninja glm)
source=("https://github.com/WayfireWM/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=(924a9682dc2f632bad8b6914a8927ad5dcee4b6698f75e89251f1647ad0beaa7)

build () {
  cd "${srcdir}/$pkgname-$pkgver"
  _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

  arch-meson -Dwf_shell=enabled build
  meson compile -C build --jobs $_cpuCount
}

check () {
  cd "${srcdir}/$pkgname-$pkgver"
  meson test -C build
}

package () {
  cd "${srcdir}/$pkgname-$pkgver/build"
  DESTDIR="${pkgdir}" meson install

  cd "${srcdir}/$pkgname-$pkgver"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
