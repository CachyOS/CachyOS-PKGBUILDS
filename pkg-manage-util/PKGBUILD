# Maintainer: Vladislav Nepogodin (vnepogodin) <vnepogodin@cachyos.org>

pkgname=pkg-manage-util
pkgver=0.1.0
pkgrel=1
pkgdesc="A command-line utility for managing pacman package builds"
arch=(x86_64)
url="https://github.com/cachyos/repo-manage-util"
license=(GPL-3.0-or-later)
depends=('gcc-libs' 'pacman' 'devtools')
makedepends=('git' 'cargo' 'lld' 'clang' 'llvm')
source=("$pkgname::git+$url.git#tag=v$pkgver?signed")
sha256sums=('090945cbd57385470c33539c54d6bc7f2cd0f09dbd2f17a02534f36a1572318a')
validpgpkeys=(
  'B1B70BB1CD56047DEF31DE2EB62C3D10C54D5DA9'  # Vladislav Nepogodin <nepogodin.vlad@gmail.com>
)

prepare() {
  cd "$srcdir/$pkgname"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$srcdir/$pkgname"

  # Compile with clang
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"

  cargo build --release --frozen
}

package() {
  cd "$srcdir/$pkgname"

  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
}

# vim:set sw=2 sts=2 et:
