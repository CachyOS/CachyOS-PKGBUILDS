# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tinywrkb <tinywrkb@gmail.com>

pkgname=power-profiles-daemon
pkgver=0.23
pkgrel=3
pkgdesc='Makes power profiles handling available over D-Bus'
url='https://gitlab.freedesktop.org/upower/power-profiles-daemon'
license=(GPL-3.0-only)
arch=(x86_64)
depends=(gcc-libs
         glib2
         glibc
         libgudev
         polkit
         upower)
optdepends=('python-gobject: for powerprofilesctl')
makedepends=(bash-completion
             git
             glib2-devel
             python-argparse-manpage
             python-shtab
             meson)
checkdepends=(python-dbusmock
              python-isort
              python-mccabe
              umockdev)
source=(git+https://gitlab.freedesktop.org/upower/$pkgname#tag=$pkgver)
sha256sums=('ffa0951d6c008239539c2a51150ad454b283c4b4b0e1910920b6afed3d143a3f')

build() {
  meson $pkgname build \
    --prefix /usr \
    --libexec lib \
    --sysconfdir /usr/share \
    -Dzshcomp=/usr/share/zsh/site-functions \
    -Dbashcomp=enabled \
    -Dmanpage=enabled
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
