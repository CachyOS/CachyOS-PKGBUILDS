### AUR PKGBUILD Maintainer:
# Maintainer: Sebastian 'gonX' Jensen <gonx@gonx.dk>
# Contributor: LavaDesu <me@lava.moe>
### CachyOS PKGBUILD Maintainers:
# Maintainer: Eric Naim <dnaim@cachyos.org>

pkgname=opentabletdriver
_pkgname=OpenTabletDriver
pkgver=0.6.4.0
pkgrel=8
pkgdesc="A cross-platform open source tablet driver"
arch=('x86_64')
url="https://opentabletdriver.net"

# license FIXME tracked upstream:
# https://github.com/OpenTabletDriver/OpenTabletDriver/issues/3571
license=('LGPL3') # FIXME: use SPDX format
depends=('dotnet-runtime-6.0' 'gtk3' 'libevdev')
optdepends=('libxrandr: x11 display querying support' 'libx11')
makedepends=('dotnet-sdk>=6.0' 'jq' 'git')
conflicts=(
  'digimend-kernel-drivers-dkms-git'
  'digimend-drivers-git-dkms'
  'digimend-kernel-drivers-dkms'
  'digimend-kernel-drivers'
)
install="notes.install"

# unified binary dotnet releases break when stripped
# see https://github.com/dotnet/runtime/issues/54947
#
# disabling debug is necessary for the time being
# see https://gitlab.archlinux.org/archlinux/packaging/packages/pacman/-/issues/19
options=('!strip')
source=("OpenTabletDriver-$pkgver.tar.gz::https://github.com/OpenTabletDriver/OpenTabletDriver/archive/v$pkgver.tar.gz"
        "use-libc-timerfd-in-linuxtimer.patch::https://github.com/OpenTabletDriver/OpenTabletDriver/commit/c2a67e811fe0c47e705c4474840117e00837b47f.patch"
        "fix-linuxtimer.patch::https://github.com/OpenTabletDriver/OpenTabletDriver/commit/e583b9d42cb323d40412f2f7c33e451a8c9eacd8.patch"
        )

sha256sums=('1ad04f4a32b54b9b62bd944b0196abb6613873b19c269abcc9f9e94c1dc3027f'
            'f6603d40b5c1d50001b6213730973bc324d0c29dfda71cc83cf07d3495a9088f'
            '2ef247de1e2a0e0367ad1f46402990d58fb9501aa1e889ad5db4f29cdd4c482a')

_srcdir="OpenTabletDriver-$pkgver"

prepare() {
    cd $_srcdir
    patch -Np1 < ../use-libc-timerfd-in-linuxtimer.patch
    patch -Np1 < ../fix-linuxtimer.patch
}

build() {
    export DOTNET_CLI_TELEMETRY_OPTOUT=1
    export DOTNET_SKIP_FIRST_TIME_EXPERIENCE=true

    cd "$srcdir/$_srcdir"

    if check_option "strip" y; then
        EXTRA_OPTIONS="/p:DebugType=None /p:DebugSymbols=false"
    fi

    export OTD_CONFIGURATIONS="${PWD}/OpenTabletDriver.Configurations/Configurations"
    ./eng/linux/package.sh --package Generic -c Release -- $EXTRA_OPTIONS
}

package() {
    cd "$srcdir/$_srcdir"

    cp -r ./dist/files/* "${pkgdir}/"

    # moving LICENSE to the suggested Arch folder is also tracked upstream:
    # https://github.com/OpenTabletDriver/OpenTabletDriver/issues/3572
    mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
    mv "$pkgdir"/usr/share/doc/opentabletdriver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
