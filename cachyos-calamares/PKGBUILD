# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>
# Contributor: Philip MÃ¼ller <philm[at]manjaro[dog]org>

_bootloader=${_bootloader-'grub'}

pkgname=cachyos-calamares-next-${_bootloader}
pkgver=3.3.0
pkgrel=2
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64' 'x86_64_v3')
license=('GPL3')
url="https://github.com/cachyos/cachyos-calamares"
conflicts=('calamares')
provides=('calamares')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'kpmcore' 'boost' 'python-jsonschema' 'python-pyaml' 'python-unidecode' 'ninja')
depends=( 'qt5-svg' 'qt5-webengine' 'yaml-cpp' 'networkmanager' 'upower' 'kcoreaddons' 'kconfig' 'ki18n' 'kservice' \
          'kwidgetsaddons' 'kpmcore' 'squashfs-tools' 'rsync' 'cryptsetup' 'qt5-xmlpatterns' 'doxygen' 'dmidecode' 'qt5-quickcontrols2' 'qt5-quickcontrols' \
          'gptfdisk' 'hwinfo' 'kparts' 'polkit-qt5' 'python' 'solid' 'qt5-tools' 'boost-libs' 'libpwquality' 'ckbcomp' 'mkinitcpio-openswap')
source=(https://github.com/calamares/calamares/commit/79db04dc2eb00b354044f73c054a94fe2b9f9aae.patch)
sha256sums=('3457ab03e46dcbb4e5e42c078c3cf0349e1eace31ca4ae6ee6f030810f57c6b8')
# Bootloader
## systemd-boot
if [ "$_bootloader" = "systemd" ]; then
source+=(calamares::git+https://github.com/cachyos/cachyos-calamares.git#branch=cachyos-systemd)
sha256sums+=('SKIP')
fi
## grub
if [ "$_bootloader" = "grub" ]; then
source+=(calamares::git+https://github.com/cachyos/cachyos-calamares.git#branch=cachyos-grub)
sha256sums+=('SKIP')
fi
options=('lto' '!strip' '!emptydirs')

prepare() {
    cd ${srcdir}/calamares
    # patches here
	# https://github.com/calamares/calamares/issues/2008#issuecomment-1172947659
	patch -Rp1 -i ${srcdir}/79db04dc2eb00b354044f73c054a94fe2b9f9aae.patch

    # change version
    _ver="$(cat CMakeLists.txt | grep -m3 -e "  VERSION" | grep -o "[[:digit:]]*" | xargs | sed s'/ /./g')"
    _ver="$pkgver"
    printf 'Version: %s-%s\n' "${_ver}" "${pkgrel}"
    sed -i -e "s|\${CALAMARES_VERSION_MAJOR}.\${CALAMARES_VERSION_MINOR}.\${CALAMARES_VERSION_PATCH}|${_ver}-${pkgrel}|g" CMakeLists.txt
    sed -i -e "s|CALAMARES_VERSION_RC 1|CALAMARES_VERSION_RC 0|g" CMakeLists.txt
}



build() {
    cd ${srcdir}/calamares

    _cpuCount=$(grep -c -w ^processor /proc/cpuinfo)

    cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DWITH_APPSTREAM=OFF \
        -DSKIP_MODULES="dracut dracutlukscfg \
        dummycpp dummyprocess dummypython dummypythonqt \
        finishedq keyboardq license localeq notesqml oemid \
        openrcdmcryptcfg packagechooserq \
        plymouthcfg plasmalnf services-openrc \
        summaryq tracking usersq webview welcomeq"

    cmake --build build --parallel $_cpuCount
}

package() {
    cd ${srcdir}/calamares/build
    DESTDIR="${pkgdir}" cmake --build . --target install

    cp ${srcdir}/calamares/settings_offline.conf "$pkgdir/usr/share/calamares/settings_offline.conf"
    cp ${srcdir}/calamares/settings_online.conf "$pkgdir/usr/share/calamares/settings_online.conf"
	cp ${srcdir}/calamares/settings_online.conf "$pkgdir/usr/share/calamares/settings.conf"
    local _destdir=etc/calamares
    install -dm755 $pkgdir/$_destdir
    install -dm755 $pkgdir/$_destdir/modules
    cp -rf ${srcdir}/calamares/src/modules/*/*.conf "$pkgdir/etc/calamares/modules"
    cp -rf ${srcdir}/calamares/src/scripts "$pkgdir/etc/calamares"
    cp -rf ${srcdir}/calamares/src/images "$pkgdir/etc/calamares"
}
