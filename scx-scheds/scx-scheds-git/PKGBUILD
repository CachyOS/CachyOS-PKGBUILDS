# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.5.r162.g730052a
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
backup=('etc/default/scx')
license=('GPL-2.0-only')
depends=(
  bpf
  jq
  libbpf
  libelf
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  meson
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")

_backports=(
112a5d480b00a183b77184527ef2c6eb7c22446e # scx_bpfland: rework lowlatency mode to adjust tasks priority
19939ee8a0969fbd92daed208055479560338384 # scx_bpfland: tune default settings
2e40f34beb2b6cf92ac5af2e9b5fb84bcc678198 # scheds: introduce scx_fair
7a788bed05da3c91d350b6819d8ee638e5e4c64d # scx_fair: introduce lowlatency mode
6026d8af7f15bab9d5a6c58ff3b9eba10faaa54a # scx_fair: use cast_mask() from common.bpf.h
05587afd1a95bd03842e0c29794e5a254f385102 # scx_fair: always give tasks a chance to run on an idle CPU
6c201b3628732d42a6346ed6ede7246b19b9142b # scx_fair: introduce per-CPU DSQs
3321ca638c016ea6c5d8bd7488578534e58560d5 # scx_fair: apply minimum vruntime when task is dispatched
fafa51571cf15f2cbeb9433fa3900631810d9d41 # scx_fair: do not scale the assigned time slice based on task priority
d88679954488da0921acbce425f889faf8fb2169 # scx_fair: make lowlatency default and drop it as an option
c403356e1e81d4f9b7c8361f37e5ae7b64c7416c # scx_fair: prevent per-CPU DSQ stall with per-CPU kthreads
53dc5feae738e87df7c1a63e8adc7599303c9726 # scx_fair: code cleanup
cff8f0f9494916b34c2dad03e5e3890bae6dd999 # scx_fair: custom voluntary context switch metric
f8eccedcc41f10243c98415502893dc284e69076 # scx_fair: always charge at least slice_min to vruntime
efd1de6df465577d6646bb2588fb77a0c07e7ca7 # scx_fair: prevent DSQ starvation
badd1de274a61f5df32a248c6cc8486be62bbab9 # WIP: scx-fair: drop per-CPU DSQs
60a073a4a6d68a39fa3f1cb052b2ed12626b0aed # WIP: scx_fair: drop BPF iterator
1bbae64dc7b4aac925305f6895c02c3872b2feaa # scx_rustland_core: update CPU idle selection logic
be681c731a4f925b58623ef4c863f4c93b118cfd # scx_rustland_core: pass nvcsw, slice and dsq_vtime to user-space
4432e64d85a9de26b871686d2b2166d51878ad74 # scx_rustland_core: allow user-space scheduler to run indefinitely
abfb4c53f5310c06e4113fcf54cbde7983a8f6bf # scx_rustland_core: restart scheduler on hotplug events
0a05f1f1930e15cce8adb0213e15428c770dddb9 # scx_rustland_core: keep CPUs alive with pending tasks
67ec1af5cf5f96fca6702b28c376915c865b1f28 # scx_rustland_core: kick an idle CPU after global dispatch
704fe95f51803e215eb5e037ae7581e8a693fe12 # scx_rustland_core: get rid of the SCX_ENQ_WAKEUP logic
97629178e2a186b1d226f3bc9440d59ad2ba84b4 # scx_rustland_core: bump up version to 2.2.2
0b2de2c10c93b6a50399cef00f9ffc27b6fbeb7f # scx_rustland: use built-in nvcsw metrics
c4b6408e925e182e155f5bb405e6a6f5f58a8149 # scx_rustland: smooth vruntime update
b07de1d7d54824fd7c7c07b39250160b92042c4d # scx_rustland: clarify EDF scheduling
763da6ab559c64e087a1a3b707d82ba17e5449f8 # scx_rlfifo: operate in a more work-conserving way
)

_reverts=(
a23f3566e3d2c4b9119d3eeed656a02e693b12d3 # scx_mitosis: Fix static assertion of scx_bpf_task_cgroup failing __weak check
70670925553ee1fe0e92428c2bef7c88a7c0fdce # Add vmlinux.h for multiple arch
f7cdf087541eaccffdc5042d45f633995479c600 # Use per-arch vmlinux.h
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled -D libbpf_a=disabled -D bpftool=disabled
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
