# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.5.r90.gc23efb1
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
backup=('etc/default/scx')
license=('GPL-2.0-only')
depends=(
  bpf
  jq
  libbpf
  libelf
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  meson
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")

_backports=(
9ea4b56ad679efa6ef813384d93d418dabc106e3 # scheds: introduce scx_fair
540c81b2f39044979a5f765c8cce09c77a54c7f5 # scx_fair: introduce lowlatency mode
1d146b20e61282eea165edc1d31e4c913f92ccb2 # scx_fair: use cast_mask() from common.bpf.h
2eee42b9cc8ec3047fa8a5292a4ec0ebb3c80f3d # scx_fair: always give tasks a chance to run on an idle CPU
cf582851a645fbfba19c2431d099ffe46694280f # scx_fair: introduce per-CPU DSQs
77a92c8bc42b1ad1527a756560d00d184532fe67 # scx_fair: apply minimum vruntime when task is dispatched
dc1da218567be96c81a9fcef1bc2b177c2880120 # scx_fair: do not scale the assigned time slice based on task priority
f3a0435a09153a87ba7d427f131ac690033f2d71 # scx_fair: make lowlatency default and drop it as an option
dfba810c3245a3f0f8bf7f4d9d60bad00e8e3baa # scx_fair: prevent per-CPU DSQ stall with per-CPU kthreads
77c15d72ed92f8f3d55d777fc278c679d93a4203 # WIP: scx_fair: handle user-space futex critical sections
bf4b90215125f84cdca1a6bff914e28cd98e21d4 # scx_bpfland: pick a random idle CPU when prev_cpu is not valid
c862f7548e1bf70a03450c300919f07ddc705234 # scx_bpfland: return more meaningful error codes from pick_idle_cpu()
08d2e5e77801e47a2b8905574b5f92287b506a1b # scx_bpfland: always refill task timeslice in ops.dispatch()
9bec7e2dd2d5a42cdfa70db801e1c8164be0a157 # scx_bpfland: prioritize only per-CPU kthreads with --local-kthreads
2c43337fc76294e492e248ca14c9f5d66e38f025 # scx_bpfland: auto lowlatency mode
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled -D libbpf_a=disabled -D bpftool=disabled
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
