# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.5.r62.geb73005
pkgrel=7
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
backup=('etc/default/scx')
license=('GPL-2.0-only')
depends=(
  bpf
  jq
  libbpf
  libelf
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  meson
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")

_backports=(
4b1082dc37a1ce7896f46a2923b9f3108b965daf # scx_bpfland: prevent per-CPU DSQ stall with per-CPU kthreads
c2f2bb30fdb85cb02bb5b27ad238d99564f505d9 # scheds: introduce scx_fair
147931668beb9d908ab35ec5f83159efe17e6e32 # scx_fair: introduce lowlatency mode
534913e28c1129f3735c25b16fa58ca5543500e1 # scx_fair: use cast_mask() from common.bpf.h
2b44c4fd30a10e7ad397bd4f0f58964f5b13dff2 # scx_fair: always give tasks a chance to run on an idle CPU
bb2f9637d91850fb8a1af7e164540121267e8ffb # scx_fair: introduce per-CPU DSQs
a9a4b505fb86d49361e08259b9b771879192b76a # scx_fair: apply minimum vruntime when task is dispatched
551275c598cf6c072c2e89c70d5aa09e5b6d8421 # scx_fair: do not scale the assigned time slice based on task priority
cf9fef70bc96822b1c89de5c173bd3a48c5c02c5 # scx_fair: make lowlatency default and drop it as an option
406fcf3edeac41c4e6d7d13ae872d3e44fe5f83d # scx_fair: prevent per-CPU DSQ stall with per-CPU kthreads
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled -D libbpf_a=disabled -D bpftool=disabled
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
