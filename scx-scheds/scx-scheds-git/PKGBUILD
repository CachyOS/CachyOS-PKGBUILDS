# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.5.r144.g03f078a
pkgrel=5
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
backup=('etc/default/scx')
license=('GPL-2.0-only')
depends=(
  bpf
  jq
  libbpf
  libelf
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  meson
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")

_backports=(
112a5d480b00a183b77184527ef2c6eb7c22446e # scx_bpfland: rework lowlatency mode to adjust tasks priority
19939ee8a0969fbd92daed208055479560338384 # scx_bpfland: tune default settings
2e40f34beb2b6cf92ac5af2e9b5fb84bcc678198 # scheds: introduce scx_fair
7a788bed05da3c91d350b6819d8ee638e5e4c64d # scx_fair: introduce lowlatency mode
6026d8af7f15bab9d5a6c58ff3b9eba10faaa54a # scx_fair: use cast_mask() from common.bpf.h
05587afd1a95bd03842e0c29794e5a254f385102 # scx_fair: always give tasks a chance to run on an idle CPU
6c201b3628732d42a6346ed6ede7246b19b9142b # scx_fair: introduce per-CPU DSQs
3321ca638c016ea6c5d8bd7488578534e58560d5 # scx_fair: apply minimum vruntime when task is dispatched
fafa51571cf15f2cbeb9433fa3900631810d9d41 # scx_fair: do not scale the assigned time slice based on task priority
d88679954488da0921acbce425f889faf8fb2169 # scx_fair: make lowlatency default and drop it as an option
c403356e1e81d4f9b7c8361f37e5ae7b64c7416c # scx_fair: prevent per-CPU DSQ stall with per-CPU kthreads
53dc5feae738e87df7c1a63e8adc7599303c9726 # scx_fair: code cleanup
cff8f0f9494916b34c2dad03e5e3890bae6dd999 # scx_fair: custom voluntary context switch metric
f8eccedcc41f10243c98415502893dc284e69076 # scx_fair: always charge at least slice_min to vruntime
efd1de6df465577d6646bb2588fb77a0c07e7ca7 # scx_fair: prevent DSQ starvation
badd1de274a61f5df32a248c6cc8486be62bbab9 # WIP: scx-fair: drop per-CPU DSQs
60a073a4a6d68a39fa3f1cb052b2ed12626b0aed # WIP: scx_fair: drop BPF iterator
3de396232e621df664710215395a7120f61f3e45 # scx_rustland_core: update CPU idle selection logic
f3a1299db0d7fb0f209d3173f68573a3e897d286 # scx_rustland_core: pass nvcsw, slice and dsq_vtime to user-space
6db59a94cb62e893e12b98eee914f6a1e420845c # scx_rustland: use built-in nvcsw metrics
7bf60b2332b7f4b40445ed8842d105de993d8e9e # scx_rustland: smooth vruntime update
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
 cd $_gitname

 local _c _l
  for _c in "${_backports[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git cherry-pick --mainline 1 --no-commit "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
    git log --oneline "${_l}" "${_c}"
    git revert --mainline 1 --no-commit "${_c}"
  done

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_gitname
  arch-meson . build -D openrc=disabled -D libbpf_a=disabled -D bpftool=disabled
  meson compile -C build
}

package() {
  cd $_gitname
  meson install -C build --destdir "${pkgdir}"
}
