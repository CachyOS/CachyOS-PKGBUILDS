# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=xdg-desktop-portal-cosmic
pkgver=1.0.0.alpha.2
pkgrel=1
pkgdesc='A backend implementation for xdg-desktop-portal for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/xdg-desktop-portal-cosmic
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libpipewire
  libxkbcommon
  mesa
  wayland
  xdg-desktop-portal
)
makedepends=(
  cargo
  clang
  git
  just
  lld
)
_tag=dcbdd9974b570633ae3b47cb1d427771e4b8e276
source=(git+https://github.com/pop-os/xdg-desktop-portal-cosmic.git#tag=${_tag})
b2sums=('9913f89855be0c446a3b795200c207c75103dc799d0ecbe5361a2befb0d48e6a96dd6bf15fad578d4a065f49dec79864d071b31244d32a0c853486792d185d8c')

prepare() {
  cd xdg-desktop-portal-cosmic
  cargo fetch --locked
}

pkgver() {
  cd xdg-desktop-portal-cosmic
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd xdg-desktop-portal-cosmic
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd xdg-desktop-portal-cosmic
  make prefix='/usr' libexecdir='/usr/lib' DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
