# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-greeter
pkgver=1.0.0.alpha.2
pkgrel=2
pkgdesc='COSMIC greeter for greetd'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-greeter
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-comp
  gcc-libs
  glibc
  greetd
  libxkbcommon
  pam
  wayland
)
makedepends=(
  cargo
  clang
  git
  git-lfs
  just
  lld
)
_tag=4ddb320daeea5124bf67500d73d6ead620f3d09b
source=(
  git+https://github.com/pop-os/cosmic-greeter.git#tag=${_tag}
  cosmic-greeter-display-manager.patch
)
b2sums=('2bae195d537b84f6a9e21b5bf224c7cd44bdfad11fbb952a018959ee716f0aa10e49f051e65b2211c7798c36258753b92eeb4f6f68d0848c4f1da58b67d9fa91'
        '8b5c32a991e31cf102b1b07e06d37e979f19106b82e8cab7dc8df81789ab6b24587605aa3387254057732a268368c4074f92461f6f5125bc1cba4e62e18cef27')

prepare() {
  cd cosmic-greeter
  git lfs install --local
  git remote add network-origin ${url}
  git lfs fetch network-origin
  git lfs checkout
  cargo fetch --locked
  patch -Np1 -i ../cosmic-greeter-display-manager.patch
}

pkgver() {
  cd cosmic-greeter
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-greeter
  export CC=clang
  export CXX=clang++
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-greeter
  just rootdir="${pkgdir}" install
  install -Dm 644 cosmic-greeter.toml -t "${pkgdir}"/etc/greetd/
  install -Dm 644 debian/cosmic-greeter{.service,-daemon.service} -t "${pkgdir}"/usr/lib/systemd/system/
}

# vim: ts=2 sw=2 et:
