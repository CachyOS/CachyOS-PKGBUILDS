# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-store
pkgver=1.0.0.alpha.2
pkgrel=1
pkgdesc='Cosmic App Store'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-store
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  archlinux-appstream-data
  cosmic-icon-theme
  flatpak
  gcc-libs
  glib2
  glibc
  libxkbcommon
  openssl
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
  packagekit
)
optdepends=('packagekit: Native packages support')
_tag=da8c64229f43164c6fa6613087f2284d3caf1721
source=(git+https://github.com/pop-os/cosmic-store.git#tag=${_tag})
b2sums=('b51474bb9f1dd7e8a56d06486a95c04a2ffcba35c699114c7f8e31a2e171a254decd9106e7aba14aee4c3deb6dfa71ec1267eb39527978e75bb2e40dce0ffe2f')

prepare() {
  cd cosmic-store
  cargo fetch --locked
}

pkgver() {
  cd cosmic-store
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-store
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-store
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
