# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kyuzial <kyuzial@protonmail.com>

pkgname=cosmic-terminal
pkgver=1.0.0.alpha.1
pkgrel=2
pkgdesc='Cosmic Terminal Emulator'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-term
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libxkbcommon
  wayland
  cosmic-icon-theme
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=be808b56cf24d03fc99cf44b0885078a81a16523
source=(git+https://github.com/pop-os/cosmic-term.git#tag=${_tag})
b2sums=('7b0575b0c1e995c12be5f73a909ce1cc4752df94ab8cab5bf0930ac8c5a125d309ac8754c302195db33fd90180d5daa196c9857ecb4e994848e28e9314f74040')

prepare() {
  cd cosmic-term
  cargo fetch --locked
}

pkgver() {
  cd cosmic-term
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-term
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-term
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
