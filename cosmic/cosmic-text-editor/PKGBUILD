# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>
# Contributor: soloturn <soloturn@gmail.com>

pkgname=cosmic-text-editor
pkgver=1.0.0.alpha.2
pkgrel=1
pkgdesc='Text editor for the COSMIC desktop'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-edit
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
  oniguruma
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=96b2a190d8a93955e93de08471477492aafd46c3
source=(git+https://github.com/pop-os/cosmic-edit.git#tag=${_tag})
b2sums=('36f58ea04f5d6ca5aac4d6c92ca238a5b0e053abf2808a8fb8d07203157de6771c4e72d4a77d325b4cf4dc1056e76d555e22dba014ef41b641c93742d0f2b4e7')

prepare() {
  cd cosmic-edit
  cargo fetch --locked
}

pkgver() {
  cd cosmic-edit
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-edit
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld -C link-arg=-lonig"
  just build-release --frozen
}

package() {
  cd cosmic-edit
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
