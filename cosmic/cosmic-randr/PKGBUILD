# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-randr
pkgver=1.0.0.alpha.1
pkgrel=3
pkgdesc='Library and utility for displaying and configuring Wayland outputs'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-randr
license=(MPL-2.0)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=71fabbb382fa8cf750f50fb77c4ba014bff80056
source=(git+https://github.com/pop-os/cosmic-randr.git#tag=${_tag})
b2sums=('30347b3a87561b9c979b9f645c5c638ec798b28fe1f36166d16efc370f3673281a42376884abb2a45e918e18f8180a224c4f52ec0a6bb98e805b2faf39d71c56')

prepare() {
  cd cosmic-randr
  cargo fetch --locked
}

pkgver() {
  cd cosmic-randr
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-randr
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-randr
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
