# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-icon-theme
pkgver=1.0.0.alpha.2
pkgrel=1
pkgdesc='System76 Cosmic icon theme'
arch=(any)
url=https://github.com/pop-os/cosmic-icons
license=(CC-BY-SA-4.0)
depends=(pop-icon-theme)
makedepends=(
  git
  just
)
options=(!strip)
_tag=341c84467fd863f0319cadf49b3d4bac1bf3029a
source=(git+https://github.com/pop-os/cosmic-icons.git#tag=${_tag})
b2sums=('df0dc0cd84abcca10f7af62486c9517f85ca94962320470723cca92d046690e55bb7eabdb3ecccc978662928a951419fa1401b8051c968606af4ca27a7e05727')

pkgver() {
  cd cosmic-icons
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

package() {
  cd cosmic-icons
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
