# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-settings
pkgver=1.0.0.alpha.2
pkgrel=1
pkgdesc='The settings application for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-settings
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  accountsservice
  cosmic-icon-theme
  cosmic-randr
  fontconfig
  gcc-libs
  glibc
  iso-codes
  libinput
  libxkbcommon
  otf-fira-mono
  otf-fira-sans
  systemd-libs
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
optdepends=('power-profiles-daemon: power profiles support')
_tag=5fc02288564e059f8d325c061f0787d5a86fb39a
source=(git+https://github.com/pop-os/cosmic-settings.git#tag=${_tag})
b2sums=('b4cb80d63496792d9a0f716fdeaacb2dcdd4644169b9ed8567351cb049eab0f3cc740fa2348132177c3530726e8aa1dc32688c0c7772d03afe79fc677a18da2c')

prepare() {
  cd cosmic-settings
  cargo fetch --locked
}

pkgver() {
  cd cosmic-settings
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-settings
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-settings
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
