# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-comp
pkgver=1.0.0.alpha.2
pkgrel=2
pkgdesc='Compositor for the COSMIC desktop environment'
arch=(x86_64)
url='https://github.com/pop-os/cosmic-comp'
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  fontconfig
  gcc-libs
  glibc
  libseat.so
  libinput
  libxcb
  libxkbcommon
  mesa
  pixman
  systemd-libs
  wayland
)
makedepends=(
  cargo
  clang
  git
  lld
)
_tag=afdb65677857946b403043f04ca083810639e4e5
source=(git+https://github.com/pop-os/cosmic-comp.git#tag=${_tag})
b2sums=('7c7135a28c081e606ee033393ff5a068e60fea42893d642008008a206ce46589e9fb94544a9e27d34a6ef9d5e3937b94512ec527c2caf802dc280683ad87fea0')

prepare() {
  cd cosmic-comp
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/' -i Cargo.toml
}

pkgver() {
  cd cosmic-comp
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-comp
  export CC=clang
  export CXX=clang++
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd cosmic-comp
  make prefix='/usr' libexecdir='/usr/lib' DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
