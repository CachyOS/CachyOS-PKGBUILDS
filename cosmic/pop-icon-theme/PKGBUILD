# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Christopher Reimer <mail@c-reimer.de>

pkgname=pop-icon-theme
pkgver=3.5.0
pkgrel=1
pkgdesc='System76 Pop Icon Theme'
arch=(any)
url=https://github.com/pop-os/icon-theme
license=(CCPL)
depends=(adwaita-icon-theme)
makedepends=(
  git
  meson
)
_tag=3126c6a3f605923c648e74e7276b9de59d4ab67a
source=(pop-icon-theme::git+https://github.com/pop-os/icon-theme.git#tag=${_tag})
b2sums=('963f155a2835328d7c385e88b3a321735470bf07e660c81610b81de2610720a2c537fd88dc62bea7e6aea770bde9c7d88513e0629fb52540890c47b0ddbfd043')

pkgver() {
  cd pop-icon-theme
  git describe --tags | sed 's/^v//;s/-/.r/;s/-g/./'
}

build() {
  arch-meson pop-icon-theme build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
