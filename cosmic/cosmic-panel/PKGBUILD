# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-panel
pkgver=1.0.0.alpha.1
pkgrel=4
pkgdesc='XDG Shell Wrapper Panel for Cosmic'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-panel
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glibc
  libxkbcommon
  wayland
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=47c672066adac6ff77b680b9461c8e1c2667b758
source=(git+https://github.com/pop-os/cosmic-panel.git#tag=${_tag})
b2sums=('f861548b817cb16b2db6d21bc7ae45bdc0747f363364a585712c0ebc10a8c7d995a3bc5ad2d1ef643d076463aa0e6268a30095cc980124ecc61e41cf6510a282')

prepare() {
  cd cosmic-panel
  cargo fetch --locked
}

pkgver() {
  cd cosmic-panel
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-panel
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-panel
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
