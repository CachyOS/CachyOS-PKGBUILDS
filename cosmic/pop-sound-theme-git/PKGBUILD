# Maintainer: Mark Wagie <mark dot wagie at proton dot me>
pkgname=pop-sound-theme
pkgver=5.5.1.r7.g25ea85d
pkgrel=1
pkgdesc="System76 Pop sound theme"
arch=('any')
url="https://github.com/pop-os/gtk-theme"
license=('GPL-3.0-or-later')
makedepends=(
  git
  setconf
)
conflicts=('pop-gtk-theme')
_commit=25ea85d97126992024b03bfb4e4c3b0711c749ed
source=(git+https://github.com/pop-os/gtk-theme.git#commit=${_commit})
sha256sums=('43e2044d5c51413e1038068f1a178c6412db1841d52aa304978790782b1b9bbc')

pkgver() {
  cd gtk-theme
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd gtk-theme/sounds/src
  setconf index.theme.in Name "Pop"
  setconf index.theme.in Comment "The Pop Sound Theme"
}

package() {
  cd gtk-theme
  install -Dm644 sounds/src/index.theme.in "$pkgdir/usr/share/sounds/Pop/index.theme"
  cp -r sounds/src/stereo "$pkgdir/usr/share/sounds/Pop"
}
