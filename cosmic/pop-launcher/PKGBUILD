# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=pop-launcher
pkgver=1.2.1.r65.c994240
pkgrel=2
pkgdesc='Modular IPC-based desktop launcher service'
arch=(x86_64)
url=https://github.com/pop-os/launcher
license=(MPL-2.0)
depends=(
  dbus
  fd
  gcc-libs
  glibc
  libegl
  libqalculate
  libxkbcommon
  pop-icon-theme
  sh
  xdg-utils
)
makedepends=(
  cargo
  git
  just
  lld
)
options=(!lto)
_commit=c994240676e8fe20aaf631f657545e456725d924
source=(
  git+https://github.com/pop-os/launcher.git#commit=${_commit}
  pop-launcher-symlinks.patch
)
b2sums=('fa8cf60eeca70fa574737d7c0d1e7a5e93dce2ec8702c946ebc0c5c9de02e47e3137962fea7ca3edca849bedb21a14ea21f22b1a09d388787a1cf7f07ecfd69d'
        'a4d39a4116df864556a960faeaf6f9c1a9847fdc4a9c32b051bc3e8dfbfb6114b9d6e47666900df17f4bbdd23434069afddd52556e68c206c9590f08d013536d')

prepare() {
  cd launcher
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/g' -i Cargo.toml
  patch -Np1 -i ../pop-launcher-symlinks.patch
}

pkgver() {
  cd launcher
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  cd launcher
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

check() {
  cd launcher
  just check
}

package() {
  cd launcher
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
