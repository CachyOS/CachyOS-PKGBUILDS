# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-files
pkgver=1.0.0.alpha.1
pkgrel=3
pkgdesc='File manager for the COSMIC desktop environment'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-files
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  gcc-libs
  glib2
  glibc
  cosmic-icon-theme
  libxkbcommon
  xdg-utils
)
makedepends=(
  cargo
  git
  just
  lld
)
_tag=190029aa27d08a26877e73db2dda789de3e6595f
source=(git+https://github.com/pop-os/cosmic-files.git#tag=${_tag})
b2sums=('294e63292397c88961704bfade498c66eb374238c7e183d321607bb3b79845c146cbc5b26ef4bd4a975a72467bd274c4a31fc467de185a8f06c5233d8ae6a824')

prepare() {
  cd cosmic-files
  cargo fetch --locked
}

pkgver() {
  cd cosmic-files
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-files
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-files
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
