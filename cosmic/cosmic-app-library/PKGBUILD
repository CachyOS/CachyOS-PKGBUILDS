# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-app-library
pkgver=1.0.0.alpha.2
pkgrel=1
pkgdesc='Cosmic App Library'
arch=(x86_64)
url=https://github.com/pop-os/cosmic-applibrary
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  cosmic-icon-theme
  gcc-libs
  glibc
  libxkbcommon
)
makedepends=(
  cargo
  git
  just
  lld
)
provides=(cosmic-applibrary)
conflicts=(cosmic-applibrary)
_tag=de55dc5861bc9a9ca49fd7bf9a16372d1dc2a3be
source=(git+https://github.com/pop-os/cosmic-applibrary.git#tag=${_tag})
b2sums=('e0746a1567bb0590f23de072e53af73a8b19229fb1a54677480aab8548befe05275ed999e2420a2d662ff52e1e78ed290257b6b9ec0cd5c9904420b4d4798006')

prepare() {
  cd cosmic-applibrary
  cargo fetch --locked
}

pkgver() {
  cd cosmic-applibrary
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-applibrary
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  just build-release --frozen
}

package() {
  cd cosmic-applibrary
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
