# Maintainer: Vladislav Nepogodin (vnepogodin) <vnepogodin@cachyos.org>
# Contributor: Nikita Almakov <nikita.almakov@gmail.com>

pkgname=rate-mirrors
pkgver=0.25.0
pkgrel=2
pkgdesc="Everyday-use client-side map-aware mirror ranking tool"
arch=(x86_64)
url="https://github.com/westandskif/rate-mirrors"
license=('CC-BY-NC-SA-3.0')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'cargo')
options=(!lto)
source=(
    "${pkgname}::git+$url.git#tag=v${pkgver}"
    https://github.com/CachyOS/rate-mirrors/commit/eb640518f354b2ea1da6e81e4550dfc61dfc9fc9.patch
    https://github.com/CachyOS/rate-mirrors/commit/1fcc102934bf5e8570f406c4bb87937f3e4d3dd0.patch
)
sha256sums=('12d16b8bc9ca55c1d0d1acee22dd8e802c65695ea2fff5857e41983b4756f0aa'
            '5afe2c4afaa12c4dd55e170c9289fbb5efd30a344bbfe9e9fa392ec96400b866'
            '7d142c842a31faaa3b2fed4a20961c014e983fdc97caee3b2194c3c50a4e42e7')

prepare() {
  cd "$pkgname"

  # archlinux: use cachyos proxy mirrorlist
  patch -Np1 -i ../eb640518f354b2ea1da6e81e4550dfc61dfc9fc9.patch

  # cachyos: add proxy mirrorlist as primary
  patch -Np1 -i ../1fcc102934bf5e8570f406c4bb87937f3e4d3dd0.patch

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"
  cargo build --release --frozen
}

package() {
  cd "$pkgname"

  install -Dm755 "target/release/rate_mirrors" "$pkgdir/usr/bin/${pkgname}"
}

# vim:set sw=2 sts=2 et:
