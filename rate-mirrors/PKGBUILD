# Maintainer: Vladislav Nepogodin (vnepogodin) <vnepogodin@cachyos.org>
# Contributor: Nikita Almakov <nikita.almakov@gmail.com>

pkgname=rate-mirrors
pkgver=0.25.0
pkgrel=2
pkgdesc="Everyday-use client-side map-aware mirror ranking tool"
arch=(x86_64)
url="https://github.com/westandskif/rate-mirrors"
license=('CC-BY-NC-SA-3.0')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'cargo')
options=(!lto)
source=(
    "${pkgname}::git+$url.git#tag=v${pkgver}"
    https://github.com/CachyOS/rate-mirrors/commit/573da0780d009d5a9a996c1e0d214fc6603c3512.patch
    https://github.com/CachyOS/rate-mirrors/commit/b549c72c8c55f7eecb9413e344699f7d392409da.patch
)
sha256sums=('12d16b8bc9ca55c1d0d1acee22dd8e802c65695ea2fff5857e41983b4756f0aa'
            '25541ab94d7fa38b505caa90a861f3cff28a37563d699c4ef1f1289177669dd2'
            '6ae345e1998c6870ae6bbc77703dc42349aeaf4f9941efa18de5d58ec0be1142')

prepare() {
  cd "$pkgname"

  # archlinux: use cachyos proxy mirrorlist
  patch -Np1 -i ../eb640518f354b2ea1da6e81e4550dfc61dfc9fc9.patch

  # cachyos: add proxy mirrorlist as primary
  patch -Np1 -i ../1fcc102934bf5e8570f406c4bb87937f3e4d3dd0.patch

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"
  cargo build --release --frozen
}

package() {
  cd "$pkgname"

  install -Dm755 "target/release/rate_mirrors" "$pkgdir/usr/bin/${pkgname}"
}

# vim:set sw=2 sts=2 et:
