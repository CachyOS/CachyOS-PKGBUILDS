# Maintainer: Vladislav Nepogodin (vnepogodin) <vnepogodin@cachyos.org>
# Contributor: Nikita Almakov <nikita.almakov@gmail.com>

pkgname=rate-mirrors
pkgver=0.28.1
pkgrel=2
pkgdesc="Everyday-use client-side map-aware mirror ranking tool"
arch=(x86_64)
url="https://github.com/westandskif/rate-mirrors"
license=('CC-BY-NC-SA-3.0')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'cargo')
options=(!lto)
source=(
    "${pkgname}::git+$url.git#tag=v${pkgver}"
    https://github.com/CachyOS/rate-mirrors/commit/29d2f3beb572187d0ece1e81e78b4e5612b563b2.patch
    https://github.com/CachyOS/rate-mirrors/commit/c221115b27bf98285c84fc07caa00b72146e2ce8.patch
)
sha256sums=('87812b06dd134632f1a262a85d1d90ad2b99fe2081ad13fa4afb6ece86fc8578'
            '10ffe2092e2f7bd19ad3e842d82734817276e3bca19344c778504b812c357b21'
            '416a4fe6d0e49dcea0189a9e965328e4402827eec16038b9647983768a001eca')

prepare() {
  cd "$pkgname"

  # archlinux: use cachyos proxy mirrorlist
  patch -Np1 -i ../29d2f3beb572187d0ece1e81e78b4e5612b563b2.patch

  # cachyos: add proxy mirrorlist as primary
  patch -Np1 -i ../c221115b27bf98285c84fc07caa00b72146e2ce8.patch

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"
  cargo build --release --frozen
}

package() {
  cd "$pkgname"

  install -Dm755 "target/release/rate_mirrors" "$pkgdir/usr/bin/${pkgname}"
}

# vim:set sw=2 sts=2 et:
