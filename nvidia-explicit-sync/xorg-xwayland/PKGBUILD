# Maintainer: Antonin DÃ©cimo <antonin dot decimo at gmail dot com>
# Contributor: AndyRTR <andyrtr@archlinux.org>

pkgname=xorg-xwayland-explicit-sync
pkgver=23.2.4
pkgrel=3
arch=('x86_64')
license=('custom')
groups=('xorg')
url="https://xorg.freedesktop.org"
pkgdesc="Run X clients under Wayland (git version)"
depends=('nettle' 'libepoxy' 'systemd-libs' 'libxfont2'
         'pixman' 'xorg-server-common' 'libxcvt' 'mesa'
         'libglvnd' 'libxau' 'wayland' 'libdrm' 'libtirpc'
         'libxshmfence' 'glibc')
makedepends=('meson' 'xorgproto' 'xtrans' 'libxkbfile' 'dbus'
             'xorg-font-util' 'wayland-protocols' 'mesa-libgl'
             'systemd' 'egl-wayland')
source=(https://xorg.freedesktop.org/archive/individual/xserver/xwayland-$pkgver.tar.xz{,.sig}
        "https://gitlab.freedesktop.org/xorg/xserver/-/merge_requests/967.patch"
        "xf86Module.h::https://gitlab.freedesktop.org/xorg/xserver/-/raw/24c5d8f17e2afd7d4e9626ba7cdcfbab53b2fdc0/hw/xfree86/common/xf86Module.h"
        "upstream.patch"
        "upstream2.patch")
sha256sums=('a99e159b6d0d33098b3b6ab22a88bfcece23c8b9d0ca72c535c55dcb0681b46b'
            'SKIP'
            '09f31b80c750ec0a9247b74f66f8f06f900301c00abe8ff26d53fe6d94197ae9'
            '01011be86f706aba20623191befc353ce9f730f9bcde7966b17c112007b5a11f'
            '2e112b742df7d524c202066685a1eca0ab67412cc6886b1bfe3a455ab79a171a'
            '714c62a5518149ed082ba99453677b22292d561fc3f0f8fec29a13267fc9d693')
provides=('xorg-server-xwayland')
conflicts=('xorg-server-xwayland')
replaces=('xorg-server-xwayland')

#validpgpkeys=('B09FAF35BE914521980951145A81AF8E6ADBB200') # "Michel Daenzer <michel@daenzer.net>"
validpgpkeys=('67DC86F2623FC5FD4BB5225D14706DBE1E4B4540') # "Olivier Fourdan <fourdan@xfce.org>"
validpgpkeys+=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>

prepare() {
  cd "xwayland-$pkgver"

  mkdir -p hw/xfree86/common
  cp ../xf86Module.h hw/xfree86/common/

  # Add upstream patches to not conflict with the newest explicit sync
  patch -Np1 < ../upstream.patch
  # Add upstream patches to not conflict with the newest explicit sync
  patch -Np1 < ../upstream2.patch
  # Add Explicit GPU Synchronization for DRI3, Present, and Xwayland
  # https://gitlab.freedesktop.org/xorg/xserver/-/merge_requests/967
  patch -Np1 < ../967.patch
}
build() {
  arch-meson "xwayland-$pkgver" build \
    -D ipv6=true \
    -D xvfb=false \
    -D xdmcp=false \
    -D xcsecurity=true \
    -D dri3=true \
    -D xwayland_eglstream=true \
    -D glamor=true \
    -D xkb_dir=/usr/share/X11/xkb \
    -D xkb_output_dir=/var/lib/xkb

  # Print config
  meson configure build
  ninja -C build
}

package() {

  DESTDIR="${pkgdir}" ninja -C build install
  # xorg-server-common file /usr/lib/xorg/protocol.txt
  rm "${pkgdir}"/usr/lib/xorg/protocol.txt
  rmdir "${pkgdir}"/usr/lib/xorg
  rm "${pkgdir}"/usr/share/man/man1/Xserver.1

  # license
  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" xwayland-$pkgver/COPYING
}
