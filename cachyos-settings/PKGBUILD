# Maintainer: Peter Jung <admin@ptr1337.dev>

_gitname=CachyOS-Settings
pkgname=cachyos-settings
pkgver=1.3.2
pkgrel=1
epoch=1
groups=(cachyos)
arch=('any')
license=(GPL-1.0-only)
makedepends=('coreutils' 'git')
pkgdesc='CachyOS - Settings'
url="https://github.com/CachyOS/$_gitname"
source=("git+$url.git?signed#tag=$pkgver")
install="$pkgname.install"
sha256sums=('a6fb6897cd92262fe2691431e25efe3b0906347fc212f55354e9ee2d456fbbf6')
validpgpkeys=(
  E8B9AA39F054E30E8290D492C3C4820857F654FE  # Peter Jung <admin@ptr1337.dev>
  B1B70BB1CD56047DEF31DE2EB62C3D10C54D5DA9  # Vladislav Nepogodin <nepogodin.vlad@gmail.com>
)
depends=(
  zram-generator
  ananicy-cpp
  cachyos-ananicy-rules
  inxi
  "systemd>=256"
  iw # for setting proper regulatory
  wireless-regdb
)
optdepends=('libluv: for topmem'
            'lua-luv: for topmem'
            'power-profiles-daemon: For game-performance')
provides=('ntsync-common')
prepare() {
  cd $_gitname
}
package() {
  install -d "$pkgdir/etc"
  cp -rf "${srcdir}/${_gitname}/etc" "$pkgdir"
  install -d "$pkgdir/usr"
  cp -rf "${srcdir}/${_gitname}/usr" "$pkgdir"
}
