# Maintainer: Peter Jung <admin@ptr1337.dev>

_gitname=CachyOS-Settings
pkgname=cachyos-settings
pkgver=1.1.1
pkgrel=2
epoch=1
groups=(cachyos)
arch=('any')
license=(GPL-1.0-only)
makedepends=('coreutils')
pkgdesc='CachyOS - Settings'
source=("git+https://github.com/cachyos/CachyOS-Settings?signed#tag=$pkgver")
install="$pkgname.install"
sha256sums=('bc1b6ff90d447dd647aac2ee9e765ac5c4220ae480be981351b8191045e9fa3f')
validpgpkeys=(
  E8B9AA39F054E30E8290D492C3C4820857F654FE  # Peter Jung <admin@ptr1337.dev>
)
depends=(
  zram-generator
  ananicy-cpp
  cachyos-ananicy-rules
  inxi
  "systemd>=256"
)
optdepends=('ruby: for tunecfs2'
            'libluv: for topmem'
            'lua-luv: for topmem'
            'irqbalance'
            'power-profiles-daemon: For game-performance')

prepare() {
  cd $_gitname
  msg2 "kerver: Check for new x86_64 level config"
  git cherry-pick -n 5ffc0525a09b745590a84d0bea084cf8b9527e08
}

package() {
  install -d "$pkgdir/etc"
  cp -rf "${srcdir}/${_gitname}/etc" "$pkgdir"
  install -d "$pkgdir/usr"
  cp -rf "${srcdir}/${_gitname}/usr" "$pkgdir"
}
