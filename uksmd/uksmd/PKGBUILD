# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Oleksandr Natalenko <oleksandr@natalenko.name>
# Contributor: Damian N. <nycko123 at gmail>

pkgname=uksmd
pkgver=1.2.6
pkgrel=1
epoch=1
pkgdesc="Userspace KSM helper daemon"
url="https://github.com/CachyOS/uksmd"
license=('GPL3')
arch=('x86_64')
depends=('libcap-ng' 'libprocps')
makedepends=('meson')
conflicts=('uksmd-git' 'uksmd-lts')
provides=("uksmd=${pkgver}")
source=("$url/archive/v$pkgver.tar.gz")
sha256sums=('146808e3b4d58c1ff6a71d397b85a422717ba458fdc205b07a3defca247e5849')

prepare() {
 cd $pkgname-$pkgver

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $pkgname-$pkgver
  arch-meson . build
  meson compile -C build
}

package() {
  depends+=(UKSMD-BUILTIN)

  cd $pkgname-$pkgver
  meson install -C build --destdir "${pkgdir}"
}
