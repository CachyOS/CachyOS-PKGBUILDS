# Maintainer: CachyOS <https://cachyos.org>
# Contributor: ptr1337 <ptr1337@cachyos.org>

pkgname=scx-cake-git
pkgver=r32.e9a4938
pkgrel=1
pkgdesc="A sched_ext scheduler applying CAKE bufferbloat concepts to CPU scheduling"
arch=('x86_64')
url="https://github.com/RitzDaCat/scx_cake"
license=('GPL-2.0-only')
depends=('glibc' 'gcc-libs')
makedepends=('cargo' 'git' 'clang' 'llvm')
options=(!lto !debug)
source=("git+https://github.com/RitzDaCat/scx_cake.git#commit=e9a4938a385c4bee6d3b352af292c351acc983e2")
sha256sums=('d59de9db0ae2fb5d2e359042c9e4e409a4fda9ca1d0dd81cb5ba8928365f3fcc')

pkgver() {
    cd "scx_cake"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "scx_cake"
    cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
    cd "scx_cake"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

package() {
    cd "scx_cake"
    install -Dm755 "target/release/scx_cake" "$pkgdir/usr/bin/scx_cake"
}
