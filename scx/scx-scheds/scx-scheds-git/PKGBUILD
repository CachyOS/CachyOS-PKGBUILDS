# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.19.r295.g42e6f153
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
adbbcd70733a86f83b1c5a2b8cb586a30ebb5f02 # Add scx_cake scheduler to workspace
02d6a093157fd9f7ecfb7f683b7e2db1cbc33b9c # Add scx_cake scheduler to workspace
2fa04983a687c12e1d5c30a49746385760696aa0 # scx_cake: Use local path dependencies instead of git
1c94d7728cf718ba5e8117dec99ad03815d2ffce # Update Cargo.lock for scx_cake
463b963107ac47b5db4d04b40e991ea73acbab45 # scx_cake: Run cargo fmt
cf529835c068df1393265e80dd3a9949e2642122 # scx_cake: add kernel 6.16+ compatibility for scx_bpf_cpu_curr
002986f7e10fea08045aefa5ba0907471bd995d2 # scx_cake: Implement Check & Skip Optimizations and SMT Fix
b67b529c27210087ba56bc72f9072359405ecdcc # perf sched map optimization
c87dd5fbc1f13975692ec91a6b758793fdf0503c # feat(scx_cake): implement state-based per-cpu direct dispatch
3e51925fb531bba5c971c251a2b82a862b6ad83e # Ghost Scheduler Optimizations: Store coalescing and bit testing
81bd9c06d89890673ca665d7f6afa6c9ed197489 # bug fixes, floor for runtime, healthy IPC
511fcf41bd9e5ace75c9c334d55c28a032784d28 # Great benchmarks - need to review jitter
13f047bb78854a28d44b815e707c280769732690 # scx_cake: Implement Zero-Math Locality Arbiter
af2865bac03e02adea5d53223c05aa7855a80652 # scx_cake: warning removed from build
77012f8e3bc407a64ecca989a18ee41984be35b9 # struct alignment 4 byte for clang 19+
89208feb6c4aed46300e04eada0a22d55a912b4b # BPF compat: Use semantic macros for 32-bit atomics to fix Clang 19 crash
4b0b803c72946523b0daa28ba4d2f8963d0f524c # BPF: Optimize MLP by migrating volatile loads to relaxed semantic macros
fddb38521ed93a54022ef27e6948e54b6e6ea422 # BPF: Bulletproof compat layer with inline ASM for Clang < 21
8e89fcee4d90de0e25ad5ffa1a95683404da27a9 # Checkpoint: Immediate Consumption pattern - baseline 53 spills restored
061e9534423513f0184301563fd019e708ac6868 # Absolute Zero Spill: Eliminate remaining BPF spills in cake_select_cpu (53 -> 0). Verified with llvm-objdump.
702db0f59b483e2c8b12782e138e6d768591c3a4 # Optimize: Implement more aggressive warmth seeking (Stubborn Warmth)
d1ea1d1ed8e327bc58215f8576f84aa9e766fd9b # perf(cake): remove volatile from 15 rodata declarations for JIT constant folding
ad841347f6510aa94954a174bf296640fb958c1f # feat(cake): v1.01 with clean ANSI startup splash
55e2f9bed2439274a30151f0dcb7c03ebe550e25 # chore(cake): bump version to 1.0.1
59bee189be1e51d4d2b71afbecb998d0a2a4bd4d # scx_cake: Remove LOCK prefixes from victim_mask ops (5 atomics → 2)
41a8b5e001949d3898cefa8d769183a2f35ef5d7 # scx_cake: Phase 2 tiered masks - 40-60% reduced atomic hit rate
71fd967d758369874de9e70f02a2fd20c0f76f23 # scx_cake: cake_update_idle zero-spill optimization
d20f2ff4076d111a41757375f4e227796efe2a3c # scx_cake: eliminate all BPF register spills via cold path optimization
956fc3a20078b8b9ebd83c821f67e37ff124aedd # scx_cake: add cache-line padding to cold_scratch
2538ba99de75fad2fd75c3794af684065eaa6477 # scx_cake: Empirical Topology Discovery (ETD) System Review
bbd0240c0e32ae42e113d00686269144139acad9 # spill fixes
dc8b23651a69086c5b36c79ee3af155705529396 # fix(cake): prioritize cache warmth over ETD to reduce jitter
06fc9ced6c04816f711e5fc9a86d08ab1b1ced44 # cold path optimizations
ccf92832236435659abb447201e279073986f8d8 # optimization leading to better fps, lower latency and better 50% frame distribution
2b705b4ccbc40ef75e0495ef9a1f4815e299ee6b # fuse optimization round 1 passed
52a5fb906e2e4f81fe27a8f15b94b24a9657f816 # fuse round 2 pass
9891f65986d0f5d6f06a42693686c0682ea14714 # MESI-friendly warm-tier updates: skip-if-unchanged optimization
7fc429b4fb9ac8164ae8f03ee361d27ed50a460e # skip-if-unchanged optimization for packed_info in cake_stopping
ba4dd7f69425a66ae43d91c3455814791fdc5fbb # cache line separation for tiered_idle_mask to eliminate false sharing
cbabf9f9dca834e641e62207b53c4f3300475f1e # branchless clamp in compute_sparse_score using nested ternary
c6dcb58fcdbb1af472dc6ef323b56f1f6eca1dc3 # scx_cake: fix build errors in calibrate test and improve signal handling consistency
0e845b16ee551e4755cd692c5f54903238ab9e84 # Refactor: Topology-Agnostic 'Physical-First' isolation and Zero-Math Arbiter implementation
ee957d493dae921a2c6329d2a28bf182b842b7a8 # Perf: BPF atomic optimizations (TTAS) and build.rs flag adjustments
bf20cdde24de8fe35e26e3d77e88295064c49c2b # scx_cake: resolved clippy warnings and applied cargo fmt
2b6a7b6929e078e763e8e78160a8e600177979a4 # spill fixes and jitter optimizations, new highscore benchmark arc raiders
2d5e2f637b9ce4638216c29300d60bd00e35924b # schbernch improvements round 1 passed
74afc037ece137db95368f20e9e56a738e90f295 # Phase 1: Memory load hoisting in cake_running
dd35c36cb4d7a7490bddcf5dd8c6d7eca1be02fd # Phase 2: ANDN bit-clearing patterns in cake.bpf.c
b3c826497f0b6d90fa59f52355de3c625b644af5 # Phase 3: BEXTR Bitfield Extraction in cake.bpf.c
b4cdd66b6cbbb9c0e6db30b4f2258f26f20508e5 # Phase 4: TZCNT Enforcement in cake.bpf.c
4f0193d6716ad930129dcfd01b0f06b6caa4b313 # Phase 5: CMOV Cascade in cake_select_cpu
bddc720b09eb4057cd67bcc3214a4f859e6df325 # Phase 6: Constant Hoisting & Snapshotting in cake_select_cpu
38fc2485fb49959cfaa60fe99f68db704fe4f916 # experimental optimization via assembly
d3c7e573d2f04359f2e5aadce5b4180e2545cae6 # scx_cake: Implement Phase 1-3 Zen 5 micro-architectural optimizations
ddfc1a9d0bce34b6e8f45fd06d35d706ebc3ae4c # checkpoint: BPF callback optimization (4 trampolines)
9822fd32540f50ed8536e5ef3a1f442a244e9be9 # scx_cake: Implement Symphony of 2 bypass and resolve build/runtime conflicts
51c6584a86fae6d20172595578c348be9e4f7caa # bpf trampoline reduction + dark arts
714936e7b38101ae7adfc035c3de2099c6f0c0cd # fix(scx_cake): isolate dsq_peek legacy fallback to avoid stack spills
6425ed46e2fb98e27772dc857445ac2dcba98c02 # feat: scx_cake - Absolute Zero v15.4 (Surgical Snatch, Rent's Rule, ALU-Shift)
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
