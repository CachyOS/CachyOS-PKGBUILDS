# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.19.r270.g6931d6b3
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
821348cd646d8f01238c13ef41756a31e5fa3493 # Add scx_cake scheduler to workspace
a75edff3f3be0ab10bb8402616783633d072ecc4 # Add scx_cake scheduler to workspace
f2c16eb43a46f1539747780d6d967a796f5d8481 # scx_cake: Use local path dependencies instead of git
578fd26cb2c346af51aa037a24758d02a9a48974 # Update Cargo.lock for scx_cake
fa899f4139d01ee5892eb4865b1dec77c29bcbfb # scx_cake: Run cargo fmt
e9c93fc722557095ce7dcca399dcee80336a721e # scx_cake: add kernel 6.16+ compatibility for scx_bpf_cpu_curr
811dfbc163d7e6631f968a17ad91dad63f0fb8cb # scx_cake: Implement Check & Skip Optimizations and SMT Fix
23acf038a1c50562e3cb0c81fa0eb85c957f1683 # perf sched map optimization
5b7951640f936040bbf3b856e8e28f7670817fbf # feat(scx_cake): implement state-based per-cpu direct dispatch
a82c328d8540984a29528400fa72ef16c0137d2f # Ghost Scheduler Optimizations: Store coalescing and bit testing
8ea6368cfb39778b1755a3e9d21a89079839442b # bug fixes, floor for runtime, healthy IPC
1022df0f4754931822a066d749231e64d4b47150 # Great benchmarks - need to review jitter
2d9979a53ab8ba2bf3d994178245e990899c8388 # scx_cake: Implement Zero-Math Locality Arbiter
a1d48bbe71c0bc1237b972dbff76cc090b48eb77 # scx_cake: warning removed from build
de5401d9fc24db79ade7288540369c2c72ece6dd # struct alignment 4 byte for clang 19+
2e6b81f4cb7dbef01e5be852a30437320decb83c # BPF compat: Use semantic macros for 32-bit atomics to fix Clang 19 crash
89ffed1b95fbf9005d4a0c6a13686fe038d4c7cf # BPF: Optimize MLP by migrating volatile loads to relaxed semantic macros
7c77fbe60021cb0be59ebddd5a1e77774b54ce51 # BPF: Bulletproof compat layer with inline ASM for Clang < 21
392c3ff524b4a49bb05d47a2f442b2f77a810edc # Checkpoint: Immediate Consumption pattern - baseline 53 spills restored
9984b9803ea34164e78f52a625c2ee4bd19f775a # Absolute Zero Spill: Eliminate remaining BPF spills in cake_select_cpu (53 -> 0). Verified with llvm-objdump.
090fb0669b1d3303a1463a4c300d84b309473019 # Optimize: Implement more aggressive warmth seeking (Stubborn Warmth)
68a350540c8311ae0749a47e8530136b65315af6 # perf(cake): remove volatile from 15 rodata declarations for JIT constant folding
6fde6816a5f1fad0ca99f46651c7fca003c89996 # feat(cake): v1.01 with clean ANSI startup splash
3c5907eca2087779f3809156672e943f31979ae6 # chore(cake): bump version to 1.0.1
df1d7c5cec0c90d9df8d7cbc9d0b19fbee5289ff # scx_cake: Remove LOCK prefixes from victim_mask ops (5 atomics → 2)
7e138a339f0b0aaa74b4564b7d3e12c9349c86b5 # scx_cake: Phase 2 tiered masks - 40-60% reduced atomic hit rate
162475d18b1d6ca4dde01843cc7d52262f5d3b6a # scx_cake: cake_update_idle zero-spill optimization
e3e927e7b8d047bc05cff6f5377a750e21aa44e9 # scx_cake: eliminate all BPF register spills via cold path optimization
18ee4d756c86f68e849ceabf7d2459222e89afa5 # scx_cake: add cache-line padding to cold_scratch
9cb9d86de02664d80b9a3e4d20bf1c3fab89616b # scx_cake: Empirical Topology Discovery (ETD) System Review
a6aff5d048342546d19d4b07a2dc57115ee0448e # spill fixes
4339f8800513c77cc72983bfe42606cfdf1ba577 # fix(cake): prioritize cache warmth over ETD to reduce jitter
01eb3cdc9fc9a8e74aaf03d4b6c86a33e6ac8773 # cold path optimizations
daf5e47f95ac5b131bdb45337f791c1f1281c6f6 # optimization leading to better fps, lower latency and better 50% frame distribution
9a73542ce7544820ed84ad1b774c62131bb20d43 # fuse optimization round 1 passed
8b8696ccb9ce1f661e062c4bcb6b9a7131b44325 # fuse round 2 pass
7c5d19b97767f06d7964595a34477c0f5fcff2f6 # MESI-friendly warm-tier updates: skip-if-unchanged optimization
dd5be072f15ce31438097b990cd441949e6878bd # skip-if-unchanged optimization for packed_info in cake_stopping
980c394a0fd277387616b4a7b5f056eb6407d1ac # cache line separation for tiered_idle_mask to eliminate false sharing
6a182d458b97d5b7f5855c28daaebcf944025035 # branchless clamp in compute_sparse_score using nested ternary
c83f0197eb2a4d658d6e9561b4ec797bb9f78323 # scx_cake: fix build errors in calibrate test and improve signal handling consistency
34253cf82acea4fd975411259ae84b5c99ab7d35 # Refactor: Topology-Agnostic 'Physical-First' isolation and Zero-Math Arbiter implementation
5db55099fd20d970fca309e879219b91e0023ec6 # Perf: BPF atomic optimizations (TTAS) and build.rs flag adjustments
8ce57e6da1c9c037376843300ff53afc24dd941a # scx_cake: resolved clippy warnings and applied cargo fmt
658ce42cea1b538a09594d320862db838854bf2d # spill fixes and jitter optimizations, new highscore benchmark arc raiders
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
