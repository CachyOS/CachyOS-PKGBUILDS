# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.19.r342.g48bcca8f
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
36de931a31ab0e297e765de28db4a0aed8699b4f # Add scx_cake scheduler to workspace
ed88548d6cbb46322d006f2f97b7f022f1c51913 # Add scx_cake scheduler to workspace
62c075127c939c079dbea94613a3ae456aa02886 # scx_cake: Use local path dependencies instead of git
b664743c4c29330f5466cf54d34b648a51a714e3 # Update Cargo.lock for scx_cake
1ca4034491918004ff524f7c63835beed2eabda4 # scx_cake: Run cargo fmt
069c5341bd4432301034d296142a866625b57526 # scx_cake: add kernel 6.16+ compatibility for scx_bpf_cpu_curr
5df9d9e3d4bfccd8ae009d177cd3ad97238abc07 # scx_cake: Implement Check & Skip Optimizations and SMT Fix
f1e15b034095602390cb1aceaf1438f72abeb7ca # perf sched map optimization
1272879eb9c27e8d10fcc8f2bb9a4d06cd61ed7a # feat(scx_cake): implement state-based per-cpu direct dispatch
7ecb39db8424d95e71715d213839097a19ce17f8 # Ghost Scheduler Optimizations: Store coalescing and bit testing
607c4dff59f4dcc8694a692918f686180233bac7 # bug fixes, floor for runtime, healthy IPC
f349c642b05cfdb46e8bb02ba8786cb190b72a70 # Great benchmarks - need to review jitter
732ee2ce440fe9a9165e2ce963f4da4414cc2ae3 # scx_cake: Implement Zero-Math Locality Arbiter
342b701c5272867fbb5cf79e0bd58654c5c64111 # scx_cake: warning removed from build
9f04b62c1b7f82d195cee8c37ffb74ec224a1ef4 # struct alignment 4 byte for clang 19+
623c586c23feda4735c181087d2937a2ac3690b5 # BPF compat: Use semantic macros for 32-bit atomics to fix Clang 19 crash
d508e5811e78813196727b116da8e16496790272 # BPF: Optimize MLP by migrating volatile loads to relaxed semantic macros
91eb58d4ec1447b6fa0a4b6aa30346742c86d896 # BPF: Bulletproof compat layer with inline ASM for Clang < 21
d883f83b394f95a434a05a778634dae65a10b668 # Checkpoint: Immediate Consumption pattern - baseline 53 spills restored
bd10e823d4f36e30ff07e5e7f68f013b02447b37 # Absolute Zero Spill: Eliminate remaining BPF spills in cake_select_cpu (53 -> 0). Verified with llvm-objdump.
57a65d270503dfab7f4c97177432cfa96b04c437 # Optimize: Implement more aggressive warmth seeking (Stubborn Warmth)
dc8ebf7de57b4ea6b63b3e4358e9d7ff2530ee7f # perf(cake): remove volatile from 15 rodata declarations for JIT constant folding
c12fe73fce16db809a9a517448b6594dcad3ed72 # feat(cake): v1.01 with clean ANSI startup splash
d9ac788b8e3adfa8294cabe2498dc21899b04034 # chore(cake): bump version to 1.0.1
79b1fc3824f2a07fa25996b92c1e2ff417cea052 # scx_cake: Remove LOCK prefixes from victim_mask ops (5 atomics → 2)
f39b1807105871e0ec9628d82bfe75fba4919ab9 # scx_cake: Phase 2 tiered masks - 40-60% reduced atomic hit rate
41aea9999e527c2de071050b6700fce39334ad20 # scx_cake: cake_update_idle zero-spill optimization
c25d6d3509c9901c9f941687d8037411f845b6ae # scx_cake: eliminate all BPF register spills via cold path optimization
0d686a8b02a97fbc27c39a375d33c526f439630e # scx_cake: add cache-line padding to cold_scratch
38b0efb853517686256d18002fd5763f42e56870 # scx_cake: Empirical Topology Discovery (ETD) System Review
cce37e645955d109bd5dc1bacd213bdedb05326f # spill fixes
e2187410ce96c05dc14720228a39ef486f3e99a4 # fix(cake): prioritize cache warmth over ETD to reduce jitter
f7e9fe08d214042cf5e57e96e6e51682b7554cec # cold path optimizations
5d83d268834c718b94291b3d24cb50f9800fd735 # optimization leading to better fps, lower latency and better 50% frame distribution
5da7233fc46d2af36eaac61508ed13d1983cb724 # fuse optimization round 1 passed
ddba5f3505d36bd304e60af3b6cd61512aa1c0f1 # fuse round 2 pass
73fe726ec96c2ea74bdd298a6405a95c5dc52259 # MESI-friendly warm-tier updates: skip-if-unchanged optimization
bec5c042009796024b248d54379b6627d372bd24 # skip-if-unchanged optimization for packed_info in cake_stopping
092d04715bf71304b4f41ebe21bbe1657cdc8b21 # cache line separation for tiered_idle_mask to eliminate false sharing
249ababc81b597f4763ed7a4b85895ef31e36769 # branchless clamp in compute_sparse_score using nested ternary
046600e9567d09f3b7cc4ef9d24d2fd138524cf3 # scx_cake: fix build errors in calibrate test and improve signal handling consistency
5b3c2a554f2553bbe9c69345d7a5ea5b6d27f19a # Refactor: Topology-Agnostic 'Physical-First' isolation and Zero-Math Arbiter implementation
2f213049516f250657a444a31f8ab913bd5d9475 # Perf: BPF atomic optimizations (TTAS) and build.rs flag adjustments
b5ed6355c706c60b8acb24f5aac03903d61607f0 # scx_cake: resolved clippy warnings and applied cargo fmt
e4abd5a851488467dd6103d179888a97cab5b3e1 # spill fixes and jitter optimizations, new highscore benchmark arc raiders
750021fcf8682c190a212edbbaa8999a27f8827a # schbernch improvements round 1 passed
17bb6131e6913edc5496109bc3e83d4e11471c73 # Phase 1: Memory load hoisting in cake_running
499ce147a9a6534328b0e5732960f5b91e7ddafa # Phase 2: ANDN bit-clearing patterns in cake.bpf.c
985b276a04271e5c8d3f954b1f359c0db7ebfffc # Phase 3: BEXTR Bitfield Extraction in cake.bpf.c
a48ddbfb805720bd67f500add578727714164f93 # Phase 4: TZCNT Enforcement in cake.bpf.c
ae3883f5592b901c21f0fee98e443578676f7459 # Phase 5: CMOV Cascade in cake_select_cpu
c715c33bb076371d0d968e7ee5cc0ab5268a0ab8 # Phase 6: Constant Hoisting & Snapshotting in cake_select_cpu
3815b979d0816a469fb3ee6c8ae46d20c7ecd62a # experimental optimization via assembly
ba927da5899ff7e53b9ce23496bae38607c03144 # scx_cake: Implement Phase 1-3 Zen 5 micro-architectural optimizations
ff44c01bd16104e695395a0df55c080f442b9335 # checkpoint: BPF callback optimization (4 trampolines)
e719c2853ccc2f694238e6f814db5f08314a79d0 # scx_cake: Implement Symphony of 2 bypass and resolve build/runtime conflicts
a45ed65ddfac99480a1a599081711458773391ed # bpf trampoline reduction + dark arts
7a78eb79bf1291a4beac1d548c183a12f8685a52 # fix(scx_cake): isolate dsq_peek legacy fallback to avoid stack spills
0b426d9a76ce3676333c542d889e2c804fe0346c # feat: scx_cake - Absolute Zero v15.4 (Surgical Snatch, Rent's Rule, ALU-Shift)
89cc76f68477b65166123e76a2812d4dc6d8c9b3 # refactor(cake): Unified DSQ architecture with async accounting - Replace 7 tier DSQs with single vtime-ordered UNIFIED_DSQ, encode tier in vtime high bits, simplify cake_dispatch to 2 DSQ probes, move accounting to tick, remove D2A signal mask and surgical producer staging. ~250 lines removed. Lowest wakeup latency in cake testing (p99: 2µs).
9461c5c181b1fcaad83a2394c9fb2331249434b2 # chore: code formatting and comment cleanup
fd3f12ef2f568341832edcf2dc07ddb1976a55dc # checkpoint: before userspace offload optimization
32f5aa62f009c6ece6147f160661a5a8ea28c9f7 # chore: comment cleanup
d8816ece1164b333d88a535ab7309ae9051c67bd # chore: divison cleanup
782d5d2f7ada8ae8d9ecb5402655c13fcd010a73 # checkpoint: before adaptive mailbox implementation - cake_stopping hook added, arbiter LUT ETD-tuned
f81bd6463bb255792047a55e36a19e425e0afc42 # feat(cake): DDR5 mega-mailbox - zero false sharing per-CPU state
8dadf58c6ba583400319d2893cc3bfd8b974e42e # perf(cake): Shrink mega-mailbox to 64B for optimal L1 + ALP efficiency
db452b11fed9b941d44b3753c74eb3cfbcc7bcd9 # perf(cake): Add kfunc caching - cached_now and cached_idle_mask in mailbox
93d3943c850256cc33e948a51ecd4add9de61250 # perf(cake): Add wakeup latency optimizations - cached masks, best_idle_cpu hint
f5bb64fb356a4f6f35b8fb0150545881d600ca4a # perf(scx_cake): tiered short-circuit select_cpu + DB_MAGIC RODATA
aa83b86608bcfaa944dcf496c6a864454fc86aea # chore(scx_cake): remove trailing whitespace from cake.bpf.c
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
