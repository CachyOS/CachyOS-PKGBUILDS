# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.20.r60.gb1f19b35
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
2a01ed97471f6a126aec129517eed5693283f221 # scx_cake: dead code cleanup + p->scx.slice locality promotion
c88146063d447dfc51bd2b220f9e6e6045fd4aa1 # scx_cake: eliminate bpf_task_storage_get from enqueue via dsq_vtime staging
5863e39337b1fcbb2377a8cf6d22ac434c2957ff # scx_cake: eliminate bpf_task_storage_get from running and tick via mailbox staging
1cc18cf1d54acfc99a38ba730b4f3aab4bb130b2 # scx_cake: confidence-gated stopping eliminates kfunc for stable tasks
ce85e257cccc0f29b4541522cc7dc11e882c6505 # scx_cake: benchmark-driven pattern optimizations for 9800X3D
fac1af316cfa7d1e1c037f482a2da314dab03595 # cake: branchless arithmetic optimizations (M6+M8+R1)
9b0628b8b2948b2d2e1a83461979700b3d2c6116 # fix(cake): eliminate Elden Ring tier oscillation + core migration
8922639da081069ab15b0e4b12a44f1692ab7134 # chore: add mangohud/ to gitignore
72f4e9e52395d7c51de209703dbd5be48bbef70b # cake: S2 CPU placement + A4 tick-assembled tier snapshot
795baaa42cc465b94dab63a9169867b4eb9ca020 # cake: fix CPU affinity crash in Gate 2 + optimize check
04dcee1919838739fe35bb0d0dee2471b2f37c4f # cake: upgrade to scx_bpf_locked_rq with __weak fallback
24e54881db3e3a3c1fc9447d87dacdd70e750337 # cake: HFT microarch optimizations (write coalescing + cold-path deferral)
6f23a6f06923e70510b1fb35a49da08a586f3951 # scx_cake: MONSTER optimization + select_cpu affinity fix
0cc0db3b45bbac96077dfba6d4fd519b5522ac5f # fix(cake): eliminate u16 counter overflow cascades
c9309805bd6b5a16fedd9594270a66201d4b6a30 # fix(cake): correct mega_mailbox struct alignment
41a2082075e598b644b935ae7189c7f0fea75e40 # OPT2+OPT6: eliminate kfuncs from cake_stopping
1ad6a2248b0db2989df9328350d8220b90cbfa6d # OPT2/OPT6: inline cold classification + slice-delta runtime
ffddc9a20156125a7fc79ce5d309dd8736c4261c # cake: Gate 4 lazy preempt — T0/T1 dispatch to worst-tier CPU via LOCAL_ON
762256cc1fa1e371bf879ad694be8fec84e732d4 # cake: R2 3-slot cache, R4 WSC hint, C1 interleaved u16, C2 deferred promotion
5878f9813e3801881b7391925e568d6f80280f54 # cake: Gate 1c migration cooldown (NEAR_PREF) + DUAL_PHASE bimodal slice boost
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
