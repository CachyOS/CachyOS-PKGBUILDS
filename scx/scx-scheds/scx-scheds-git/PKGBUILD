# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.20.r100.g005e4d7f
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
f5e5f5916a15aa823fa2a6fea88c0982de3d9e57 # scx_cake: dead code cleanup + p->scx.slice locality promotion
4b595823a7b021d620f40a4c8183c4314ac67b29 # scx_cake: eliminate bpf_task_storage_get from enqueue via dsq_vtime staging
47d9ee46dfd6c9b7392ef68ca04f1f4618cb6fa7 # scx_cake: eliminate bpf_task_storage_get from running and tick via mailbox staging
693a5099c4531af57ad551109b1453daaa0a6fe2 # scx_cake: confidence-gated stopping eliminates kfunc for stable tasks
e5b80306d74eab1d3ed873053918c2b33dc58578 # scx_cake: benchmark-driven pattern optimizations for 9800X3D
3a3b293ccc7462dc4064d85e7132fc701b2782ad # cake: branchless arithmetic optimizations (M6+M8+R1)
2ba1c75245f8da57fa08dc302696de81eeef27fd # fix(cake): eliminate Elden Ring tier oscillation + core migration
644499edda37021a77513fa97c56d36aafd636c2 # chore: add mangohud/ to gitignore
1efb9d0fa459a339231d4851cb8aead5d8c4161e # cake: S2 CPU placement + A4 tick-assembled tier snapshot
04b40c0f29e32dc299e0e084ca7a016ced088494 # cake: fix CPU affinity crash in Gate 2 + optimize check
997d692aa93c1a7da84a20f0d877cadf61d3ee9f # cake: upgrade to scx_bpf_locked_rq with __weak fallback
7e3e7b93130ddd89537c425e4df78714604131d5 # cake: HFT microarch optimizations (write coalescing + cold-path deferral)
ae82c347fb4ba070809e56e832fee4257b34d2a9 # scx_cake: MONSTER optimization + select_cpu affinity fix
24345582d094cea2418d64c3d380648d78916f4e # fix(cake): eliminate u16 counter overflow cascades
931e2c65f2cf0add00f0a07eb544eff556b56407 # fix(cake): correct mega_mailbox struct alignment
4df0fd76860ec48c2d7868afbd53a26a8cccf6c4 # OPT2+OPT6: eliminate kfuncs from cake_stopping
981b62d8506a6d3783fbfc3f47ad13a70f120dff # OPT2/OPT6: inline cold classification + slice-delta runtime
0998c6167bc2ab63024ca278e99514845ca66923 # cake: Gate 4 lazy preempt — T0/T1 dispatch to worst-tier CPU via LOCAL_ON
8961fb5ed50ac502fce02eb5294bdc2fcb41db9c # cake: R2 3-slot cache, R4 WSC hint, C1 interleaved u16, C2 deferred promotion
351d2d49ee792288140a36f6b112d205b7dc2aae # cake: Gate 1c migration cooldown (NEAR_PREF) + DUAL_PHASE bimodal slice boost
c0a7c9b5d82465f3c9e4daaadbffbf883ae173d8 # scx_cake: detect headless terminal and skip TUI/splash
718dcb84968ed1ddea5b9c7ba4af77f16de407d8 # scx_cake: eliminate 4 sources of false sharing
c039190143b50f8fe26de24c2e6bc650a5dc8a59 # perf(cake): trampoline latency hiding — defer kfunc + pre-load + MLP
7cd99c048e2b399151fa08136afdb82531055509 # perf(cake): revert spilled pre-loads per codegen audit
73adac41e7c8f41866dc0a96afca6bd995fe6654 # cake: remove dead code and add jitter guards
69edf7414b4e1522cc1f0380e71c5aa8075e5b6a # arena: migrate per-task storage from bpf_task_storage_get to sdt_task arena
93b10ebc27cce57b2735488747d822cb07123c72 # arena: migrate mega_mailbox + global_scratch to arena per-CPU arrays
9e60b48cbe750245186a65a3e99ce322af4758bf # arena: tier-occupancy bitmask for dispatch skip optimization
5ddde52c1bb29d815277a643e4b0cecffe352753 # arena: optimize ARENA_ASSOC to inline asm (2 insns vs 3)
c804860fb12bad9a91763f7b92c76575032fd409 # scx_cake: arena audit — fix tier_occupied race, stale preempt scratch
d96498c1527d494de821a6ef27ebd84eb6f35f58 # cake/bpf: C1 spatial consolidation — merge arena_mailbox + arena_scratch into unified per_cpu block
45e4718363bacfe1b781e9f928c7063b7eed9482 # scx_cake: bump scx_arena version to 0.1.4
1036375f591609d575721dbdef6e32b85d500e6e # scx_cake: fix CI exit code 143, proper UEI shutdown, skip ETD headless, fix heatmap layout
9ee8b511e3e18eb5b46a63aebadf797500b37ef7 # scx_cake: Final Jitter, Latency, and Topology Audit (Phase 3 & 4)
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
