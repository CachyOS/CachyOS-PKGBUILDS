# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.19.r274.gdb76dafa
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
6b111fd62e414ed988e88af3a3608cf21c3317c0 # Add scx_cake scheduler to workspace
89c441f14b17fc7a6f902805add581aeabb86445 # Add scx_cake scheduler to workspace
fc5261725bc73827950b9315f6e2f9169c234cd1 # scx_cake: Use local path dependencies instead of git
9f94dfa461c49b8569ba88646dbc32fb2b39ab16 # Update Cargo.lock for scx_cake
b7094aeb1f8513ae7fb702693ac4176796e4d94c # scx_cake: Run cargo fmt
ed72c333f790b6d21ec8290823f4ddad3a9f0a67 # scx_cake: add kernel 6.16+ compatibility for scx_bpf_cpu_curr
08b45ad320abe30f0088bea2a103643101678e4f # scx_cake: Implement Check & Skip Optimizations and SMT Fix
79b8fa0372592a942b071c5310f9859426825d74 # perf sched map optimization
a0a32b5ab4700ac68ed2be79ea39ccda8ee1f31c # feat(scx_cake): implement state-based per-cpu direct dispatch
f0d32bcf80f21f3196adf82b2c19007d9c6caace # Ghost Scheduler Optimizations: Store coalescing and bit testing
56a9fa5b81f82e4fcf908f6fdf99a9f0d88f2a89 # bug fixes, floor for runtime, healthy IPC
b3155f333e1f6114c2104472b531691703ee28e3 # Great benchmarks - need to review jitter
0d2870dc2b05bf580b2511f3ab41db8bdf6005cb # scx_cake: Implement Zero-Math Locality Arbiter
e86dde8daa3c21d59645dcf1b9d0c30f536efe0a # scx_cake: warning removed from build
427d329a380e743a72092c2640add439728e53e7 # struct alignment 4 byte for clang 19+
40d79c5f0ad4aaac81a7f0b14db9cff1c07f2d98 # BPF compat: Use semantic macros for 32-bit atomics to fix Clang 19 crash
49890775a345995f8829c5d183ed50cfbbb4bed8 # BPF: Optimize MLP by migrating volatile loads to relaxed semantic macros
254a3ca6555834862d22854d04d85fb237c1ed99 # BPF: Bulletproof compat layer with inline ASM for Clang < 21
4a8b414748f74c415aaf98b75528b52090be6b1a # Checkpoint: Immediate Consumption pattern - baseline 53 spills restored
2e83dcfdaac04d7eeb48ed593a804e4bd4c76e80 # Absolute Zero Spill: Eliminate remaining BPF spills in cake_select_cpu (53 -> 0). Verified with llvm-objdump.
c1b1dd068ed2a4013067dce57ccb41b7aa95bae7 # Optimize: Implement more aggressive warmth seeking (Stubborn Warmth)
93024a497220485a525a231bb4aa2ada0bf6bbc1 # perf(cake): remove volatile from 15 rodata declarations for JIT constant folding
00e717afe088f59c48f4a0ac7ee8aff0db38925c # feat(cake): v1.01 with clean ANSI startup splash
32ac24a69abb709417caec48b5b275ee3cc48556 # chore(cake): bump version to 1.0.1
f119d6ceb89f1aef12ef2b8fe956d28a6aa4f477 # scx_cake: Remove LOCK prefixes from victim_mask ops (5 atomics → 2)
f4b4c035251085de2e7e2307672d2b3054418a8d # scx_cake: Phase 2 tiered masks - 40-60% reduced atomic hit rate
15d346bae0e4ff0098485889f0e54d18f09f1317 # scx_cake: cake_update_idle zero-spill optimization
70c5d7c7e3df8f9dd0cb0ab5bf37e5552ca120b4 # scx_cake: eliminate all BPF register spills via cold path optimization
a588b1221fe72c95092a646de75499c424672704 # scx_cake: add cache-line padding to cold_scratch
afcd99d0d2d70f5e708bffed85b0b4228a32bd5f # scx_cake: Empirical Topology Discovery (ETD) System Review
66d23ee8a47251c77ea0143c4e90054762d58427 # spill fixes
1c46bf324d9db36e5444a4c4ac63a37d0b371146 # fix(cake): prioritize cache warmth over ETD to reduce jitter
760219082ed66979742809649889fa3990f1a58f # cold path optimizations
1beb7651301a4533674afdb77638a9c66670bfac # optimization leading to better fps, lower latency and better 50% frame distribution
8a37eb000b658b6cdcbaeb8fa39dad75626aeac0 # fuse optimization round 1 passed
829b57c6a4d7543cdf7acc28366603779f299d56 # fuse round 2 pass
f3f466ff1acd42810cfe795dd7694063f4629e45 # MESI-friendly warm-tier updates: skip-if-unchanged optimization
e378ba48cbd6b8cd2683f5d813b4225c616eb533 # skip-if-unchanged optimization for packed_info in cake_stopping
9b1431f16a02bb4f7ee4228a48202a6d4ec6fc90 # cache line separation for tiered_idle_mask to eliminate false sharing
bca9b2d2dbaacbf7dc934d3176cc59dfd45aee02 # branchless clamp in compute_sparse_score using nested ternary
f538e61a1fdb27f2eaf12c676dbdf6da21d2c344 # scx_cake: fix build errors in calibrate test and improve signal handling consistency
6fd57e6910d878ba3e86af9a5795fe9d356e6384 # Refactor: Topology-Agnostic 'Physical-First' isolation and Zero-Math Arbiter implementation
9548b2b87b4b15221262cae96eb897b4efdc33f2 # Perf: BPF atomic optimizations (TTAS) and build.rs flag adjustments
35a0bb162cfd1e2b2735695f8605706b789e7b17 # scx_cake: resolved clippy warnings and applied cargo fmt
80bb944d1dbed7f83dc8a1cffaa3843fcd9b664e # spill fixes and jitter optimizations, new highscore benchmark arc raiders
b0fa7fc30aafa740522116fdaf8189fded518120 # schbernch improvements round 1 passed
585c8d20cad5eb00e973f0e58fa93515cc2c42ed # scx_cake: make clippy happy
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
