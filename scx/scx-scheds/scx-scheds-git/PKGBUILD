# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.19.r330.gc6de1782
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
043a70fd2a098307d2834a09f8918a7c00656601 # Add scx_cake scheduler to workspace
6b08d086fb6c34b8f511f883bc54ce7e5253ada0 # Add scx_cake scheduler to workspace
1a9b18e6bdb6173164d3398bec4170fb9e4fb072 # scx_cake: Use local path dependencies instead of git
56414717b8658b3cf3e08cf3ae2be764ed8c861a # Update Cargo.lock for scx_cake
a8babb4dbc8933d2b7f8c182acc9225a765b40ac # scx_cake: Run cargo fmt
d2d86d564f00eb1623c3c49d03f442e546c63835 # scx_cake: add kernel 6.16+ compatibility for scx_bpf_cpu_curr
d147e0804d08144ad7f20ce0023c0e3168716745 # scx_cake: Implement Check & Skip Optimizations and SMT Fix
3103210833e2182440fa581b9b25b7da51598ca6 # perf sched map optimization
e23313869d2abd832e7480d18d20e723b073c803 # feat(scx_cake): implement state-based per-cpu direct dispatch
43989253afa64e887a743cb2c65cac19a1fef1f9 # Ghost Scheduler Optimizations: Store coalescing and bit testing
7e91a89b2e2bdd32372012d4af6caf1aa3ab5004 # bug fixes, floor for runtime, healthy IPC
ba8cb9cfc17409439c85ae229fc0b55ba6f24fcf # Great benchmarks - need to review jitter
88fbb8624e57319f68405538383031fde8e7781e # scx_cake: Implement Zero-Math Locality Arbiter
31f8b5c0f4e4ad475a673caaf1fc309b7839a3f6 # scx_cake: warning removed from build
89d70b293141aef73e5c4d660a4876fb4eb63615 # struct alignment 4 byte for clang 19+
5ce15ea9e15806d2726a620d5dfe03b8e33ec605 # BPF compat: Use semantic macros for 32-bit atomics to fix Clang 19 crash
7c756b8afeef6b47faaf263792f756b619120ef2 # BPF: Optimize MLP by migrating volatile loads to relaxed semantic macros
f9d1a55cebd74e868c8818e9082ee0ff6bcdd929 # BPF: Bulletproof compat layer with inline ASM for Clang < 21
8f538145a41e5f86c93a27a71d4d883dc8a7c17e # Checkpoint: Immediate Consumption pattern - baseline 53 spills restored
c4fc6cc0fb1f71b6f166700a106c551aaa15c37d # Absolute Zero Spill: Eliminate remaining BPF spills in cake_select_cpu (53 -> 0). Verified with llvm-objdump.
d6aeb60da7240ea54aa9f048ac1057da48204ea7 # Optimize: Implement more aggressive warmth seeking (Stubborn Warmth)
23a0b516f57f22d6b035f41db651395f3da35b31 # perf(cake): remove volatile from 15 rodata declarations for JIT constant folding
9b4137e33d4918d99b9b7b9d0366d539307ed8bf # feat(cake): v1.01 with clean ANSI startup splash
69f7282d0dff722e258f6347fd7537238fd3585d # chore(cake): bump version to 1.0.1
7612fab4d21edc534312d312e63eeb306465678f # scx_cake: Remove LOCK prefixes from victim_mask ops (5 atomics → 2)
3687868671b8bd84887be10c6f0d21fb2a249771 # scx_cake: Phase 2 tiered masks - 40-60% reduced atomic hit rate
9dc93877c120125cd001a035a2506c67d99206cf # scx_cake: cake_update_idle zero-spill optimization
eb807e2ab2736e150907a676b4f8c6a012991aca # scx_cake: eliminate all BPF register spills via cold path optimization
7ede58abdff1bc615322a08f57c877406a075728 # scx_cake: add cache-line padding to cold_scratch
e965afb8607fcd1758e6182d6a9c148c72c3f044 # scx_cake: Empirical Topology Discovery (ETD) System Review
7edda879c7774d9b8ac44197c683fcb45a36593a # spill fixes
01f912fe978e2a18e6a54c9c6a4a4506aacc5f2a # fix(cake): prioritize cache warmth over ETD to reduce jitter
e9af9acd64a952f529344ed44fb7544d37cb9947 # cold path optimizations
224c836221c76415bbde71605f34dba7f331df97 # optimization leading to better fps, lower latency and better 50% frame distribution
e33a163b58d71f8cc40bcde0f8db90c576810011 # fuse optimization round 1 passed
add51424e7464f5dc4f0260ac923d75ce413f270 # fuse round 2 pass
5c2d6910b4e41af93047fe203c168fcf224b32d1 # MESI-friendly warm-tier updates: skip-if-unchanged optimization
f3788a140786271a3808d440b11078c9cc2ad735 # skip-if-unchanged optimization for packed_info in cake_stopping
fd17acaecac4f4b0a3f4f6585e523b2ccc20ba4e # cache line separation for tiered_idle_mask to eliminate false sharing
08eafab0c096835ab0b2fdd7e33eed6eb9610494 # branchless clamp in compute_sparse_score using nested ternary
9d469e4a875412ccc909a7dd233e8876122ee697 # scx_cake: fix build errors in calibrate test and improve signal handling consistency
95f65404d019775e2bff303063d6ec83c52aa261 # Refactor: Topology-Agnostic 'Physical-First' isolation and Zero-Math Arbiter implementation
55b65d062871df26cc6aa904d9328cda0d25a9f4 # Perf: BPF atomic optimizations (TTAS) and build.rs flag adjustments
16509155d98d04906c659a88bc2e2007965b6e72 # scx_cake: resolved clippy warnings and applied cargo fmt
a50ee3b168fd0f89f3d0e84097a2843da9ab97e2 # spill fixes and jitter optimizations, new highscore benchmark arc raiders
171e9f63a3e8fded905d5a4b1e72c5b13f15e065 # schbernch improvements round 1 passed
cb82a0ec3d8e18b1c24274ff5e45e15c92660e1f # Phase 1: Memory load hoisting in cake_running
fce83e757537a3ff98b985aae6b4fc73b4dadce8 # Phase 2: ANDN bit-clearing patterns in cake.bpf.c
793e735d453e022a67e47c1c4e401093cdb80b7d # Phase 3: BEXTR Bitfield Extraction in cake.bpf.c
26542b92fea0c283903277ade5add0f0c4b1996f # Phase 4: TZCNT Enforcement in cake.bpf.c
9f6892b38eafcc960090c9383ef3dc426d26ba1a # Phase 5: CMOV Cascade in cake_select_cpu
97c960d32139ae0bda38ae84f55feb1f72f6a763 # Phase 6: Constant Hoisting & Snapshotting in cake_select_cpu
6d59dc82260c25532e869cca8241a9f5f42c44d6 # experimental optimization via assembly
c0c3f529159fefdbdfa6bab5a7590901c810ecfc # scx_cake: Implement Phase 1-3 Zen 5 micro-architectural optimizations
93a8c0b151afd2562c065264014f4352763c4bb8 # checkpoint: BPF callback optimization (4 trampolines)
6689ed238e148874d50f872b12b5170b7318a2f8 # scx_cake: Implement Symphony of 2 bypass and resolve build/runtime conflicts
edc45109dcedf951e3a1d9bf5a2e08acdf36f21f # bpf trampoline reduction + dark arts
ef7a44f0aa64037543e1d24ff3c86d9bb046a05a # fix(scx_cake): isolate dsq_peek legacy fallback to avoid stack spills
71b7ce8258a7dda75cdb6b222f06809c48ba221b # feat: scx_cake - Absolute Zero v15.4 (Surgical Snatch, Rent's Rule, ALU-Shift)
8bd57eb19053cadba1dc3e782cfc1678e8a12149 # refactor(cake): Unified DSQ architecture with async accounting - Replace 7 tier DSQs with single vtime-ordered UNIFIED_DSQ, encode tier in vtime high bits, simplify cake_dispatch to 2 DSQ probes, move accounting to tick, remove D2A signal mask and surgical producer staging. ~250 lines removed. Lowest wakeup latency in cake testing (p99: 2µs).
b98a64c5097f53e328fa3c8b05b2913065c4a736 # chore: code formatting and comment cleanup
4007d881454bcd096168ced1075e31b5b3c397ad # checkpoint: before userspace offload optimization
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
