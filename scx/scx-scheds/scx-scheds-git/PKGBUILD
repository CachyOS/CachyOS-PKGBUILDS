# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.20.r127.g59b9392c
pkgrel=2
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
76d18d6a3952017878262ddaabc6483c1f1e3866 # scx_cake: dead code cleanup + p->scx.slice locality promotion
b20a8f1cdeb3369b3ac91d25cdaa2548d954c086 # scx_cake: eliminate bpf_task_storage_get from enqueue via dsq_vtime staging
063c3dc86735ae154c1d3385d079538f4fc0a254 # scx_cake: eliminate bpf_task_storage_get from running and tick via mailbox staging
d69db4dac70c70e3d43cfcd4e46c58ccb701d140 # scx_cake: confidence-gated stopping eliminates kfunc for stable tasks
c997499f552deffb947e855f1c6abd932fbf5e03 # scx_cake: benchmark-driven pattern optimizations for 9800X3D
674597c9738543142d59fe7e8dd3f1e9030997c6 # cake: branchless arithmetic optimizations (M6+M8+R1)
7f169529545db9c03d0433102aab9c78819e24fb # fix(cake): eliminate Elden Ring tier oscillation + core migration
6b55967b80d2ba4a1526afbf1ac4c61f24d48259 # chore: add mangohud/ to gitignore
403506bff502edda35376d6c3cf5c73a4c82a577 # cake: S2 CPU placement + A4 tick-assembled tier snapshot
fe9896e53f2297798562c46b9d627ebfb33aa96a # cake: fix CPU affinity crash in Gate 2 + optimize check
87f62381a5545a44fc18e17f4724c5ab1ce4fe0f # cake: upgrade to scx_bpf_locked_rq with __weak fallback
2800db9d629607efe4130a1542147baffab2e890 # cake: HFT microarch optimizations (write coalescing + cold-path deferral)
9f76b7230a616bb80d87ad9774724265672ca06b # scx_cake: MONSTER optimization + select_cpu affinity fix
f7b6e68f83fce47023ef0b5df2098a13888a4c5a # fix(cake): eliminate u16 counter overflow cascades
1cb9919fa9ac613f945d80516030efecdaa434e0 # fix(cake): correct mega_mailbox struct alignment
370066b12b7c7948200b90332715fc576ff2db2e # OPT2+OPT6: eliminate kfuncs from cake_stopping
0efff922326b4e107e04d95dacbcf9caa8586acf # OPT2/OPT6: inline cold classification + slice-delta runtime
cbbe7ec9d7c09e3d811aedfab9d3a581934eb42c # cake: Gate 4 lazy preempt — T0/T1 dispatch to worst-tier CPU via LOCAL_ON
1f56a76d3f7cdd4632fd20ff65971091d9fb570d # cake: R2 3-slot cache, R4 WSC hint, C1 interleaved u16, C2 deferred promotion
b25bafb8f11f67f9c194ed20feeddbb39f692ece # cake: Gate 1c migration cooldown (NEAR_PREF) + DUAL_PHASE bimodal slice boost
30451161ea897a60be621fffabaaddecb14c39db # scx_cake: detect headless terminal and skip TUI/splash
a9bb74468e600800006ca158b12a736267e7d3de # scx_cake: eliminate 4 sources of false sharing
96d6f047376ec94c51e482a6cc49863a613917d3 # perf(cake): trampoline latency hiding — defer kfunc + pre-load + MLP
ee19ad7c1938b0349f06b0769e5210fbce92d8a0 # perf(cake): revert spilled pre-loads per codegen audit
9d602cfc3070f20e16d78332301737fa36cae8f6 # cake: remove dead code and add jitter guards
689cbbeb4f53b6d9faf86667ebf299bd0108e61d # arena: migrate per-task storage from bpf_task_storage_get to sdt_task arena
2d063a4454b4c0556aef78e4db458900c918f99f # arena: migrate mega_mailbox + global_scratch to arena per-CPU arrays
b4f6dbad92eac1fc60010c6966fb6b9f4c624614 # arena: tier-occupancy bitmask for dispatch skip optimization
b5a1b11fc420b01745c284afcf0f2883e5cf666e # arena: optimize ARENA_ASSOC to inline asm (2 insns vs 3)
1611300fa6d2357fea6fba2d9c98cddec14db11e # scx_cake: arena audit — fix tier_occupied race, stale preempt scratch
f2450ae03a2cdd068580e670e65343902c8e574b # cake/bpf: C1 spatial consolidation — merge arena_mailbox + arena_scratch into unified per_cpu block
894207a905582b0897dc69cbe3f150e6afe6be63 # scx_cake: bump scx_arena version to 0.1.4
dd91894360ab6fa74bdb54d76f59dfd18235abb2 # scx_cake: fix CI exit code 143, proper UEI shutdown, skip ETD headless, fix heatmap layout
5d680bfa58b6107c60c40bdc449fef8ab0765a23 # scx_cake: Final Jitter, Latency, and Topology Audit (Phase 3 & 4)
9e05c5ec6a516473e2ee2a509e7d598555f6e5be # Phase 5: Yielder Privilege System
8a2c97f95f7afccacd2775b37fe92a13e7c6e5e1 # scx_cake: waker-aware CPU placement (Gate 1W) & self-scaling yield priority
ea65bd33ce1638eb0b8c2a2ecb7a75b0ceb626d6 # BenchLab 19-entry + Disruptor handoff: psychic cache removed, zero-arena cake_stopping
b091752ff4d14628c08a48c17e384f5ca9d295c8 # Cargo.lock: update crates for scx_cake
b0cb6ea49c2353d8f49e3e91c101fc968ec92bf7 # scx_cake: try sysinfo 0.35.2
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
