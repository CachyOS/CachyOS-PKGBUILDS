# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.20.r7.gfd056ee6
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
63b6fe399f7b832b634a376f4ffcd420da3cdadb # Add scx_cake scheduler to workspace
640e104fea543497ab59a47179c887b68c1abdd5 # Add scx_cake scheduler to workspace
468aa7248fae06808b3a03a895153287757e93fd # scx_cake: Use local path dependencies instead of git
7840d9ebe34ffb75dde92b7317dbfce018bc0ee6 # Update Cargo.lock for scx_cake
5fd42624d218c933a293265705701c603d907c05 # scx_cake: Run cargo fmt
5873de8908b77ec5dcdaf295974e54309eebb057 # scx_cake: add kernel 6.16+ compatibility for scx_bpf_cpu_curr
ffbf3736c3114a3e65824255f234ebbefd4a15b0 # scx_cake: Implement Check & Skip Optimizations and SMT Fix
217a26cb118a5d5edd0dc2bb4dc1bdbc996491b3 # perf sched map optimization
51c9fdb8d7e1bcb5c27ce37912de5b1abae87898 # feat(scx_cake): implement state-based per-cpu direct dispatch
fd96a16c5d8442e4c28aee95b5d01d5d43337c4d # Ghost Scheduler Optimizations: Store coalescing and bit testing
13c36d6f0763a999b012416791fa5c5067e025b4 # bug fixes, floor for runtime, healthy IPC
0f2b58d6d2a624a6d79f6f858efa4a96af0281c3 # Great benchmarks - need to review jitter
52fc4782e1bd2fb4ba9118c82dd484ca70653df0 # scx_cake: Implement Zero-Math Locality Arbiter
3172a39ca4e00cb21c5033e43f6e46f5c4d3e327 # scx_cake: warning removed from build
1db81b47fa66971d0a543a22894bfadc340181d6 # struct alignment 4 byte for clang 19+
a18c72a8a7fb22680a7cd759c05ecaddd88e5db7 # BPF compat: Use semantic macros for 32-bit atomics to fix Clang 19 crash
9fba61bb923d73b20ce17e38c3a0c71202d54670 # BPF: Optimize MLP by migrating volatile loads to relaxed semantic macros
4e44a61d3514189c56feb7d8d5083a9c31f7c21a # BPF: Bulletproof compat layer with inline ASM for Clang < 21
3bda4e9dc0732682f83fea5164c6257a440fd8d9 # Checkpoint: Immediate Consumption pattern - baseline 53 spills restored
36488220b98ef00db64cd7f3e423be9b4958b3e6 # Absolute Zero Spill: Eliminate remaining BPF spills in cake_select_cpu (53 -> 0). Verified with llvm-objdump.
12d10e92525803c8ab365ccbd7057e9d2ef0804d # Optimize: Implement more aggressive warmth seeking (Stubborn Warmth)
654c79d01cdfbbc6e6b9b4ac8d3eabfab4284853 # perf(cake): remove volatile from 15 rodata declarations for JIT constant folding
d9ebc8659709f04089bdfeea628db5b400372501 # feat(cake): v1.01 with clean ANSI startup splash
62972ada04ae44523d63b83044ba0054220fae4b # chore(cake): bump version to 1.0.1
eaf96a905609a23fa1c76542ce80819ff85efb5e # scx_cake: Remove LOCK prefixes from victim_mask ops (5 atomics → 2)
971a676df1cfacddbb7aa7b1653682faf9e58953 # scx_cake: Phase 2 tiered masks - 40-60% reduced atomic hit rate
eebd7f80b7a54a90e4253abb14650eb750a99d8c # scx_cake: cake_update_idle zero-spill optimization
ace4b64c732a1742d03871e9ae0fea2533b648be # scx_cake: eliminate all BPF register spills via cold path optimization
03316ffb00d831ca1080792ef61ef58ea4edbbf1 # scx_cake: add cache-line padding to cold_scratch
45b1b9843dde47fcdd89ee46d6e031450b03a90d # scx_cake: Empirical Topology Discovery (ETD) System Review
05a8a862e9d6140fcfba8ff47cf28c20ed799215 # spill fixes
4c00f6442d5f3cad2c06466ba13bf4f22276d384 # fix(cake): prioritize cache warmth over ETD to reduce jitter
dd01f10e794f17b980516920988a6de257df3d4e # cold path optimizations
c10969982f7b48e0f245e8d37eefca4d58494cbe # optimization leading to better fps, lower latency and better 50% frame distribution
074ca035b7a7fdad133e37041c41acb6d29e9782 # fuse optimization round 1 passed
8144b5fa302a7bbfda7ed0ed8fcb1d7c1c7fddde # fuse round 2 pass
4818325f41261e6d74a959e16c34d8a21393d702 # MESI-friendly warm-tier updates: skip-if-unchanged optimization
02d077996545051d4b6e9c0a7efc5ee91f4fccd8 # skip-if-unchanged optimization for packed_info in cake_stopping
02ada39f3cd16520614851a55191fd44698deabd # cache line separation for tiered_idle_mask to eliminate false sharing
43170853078d024c11c447825dbaf3aaf889ab22 # branchless clamp in compute_sparse_score using nested ternary
20494a7e525f6e71885dd2bb68692e2060d2e051 # scx_cake: fix build errors in calibrate test and improve signal handling consistency
007906e63be51366de3071be39beb6ca280dfad5 # Refactor: Topology-Agnostic 'Physical-First' isolation and Zero-Math Arbiter implementation
da84cc2b83e6788695e815513150475f597a1b96 # Perf: BPF atomic optimizations (TTAS) and build.rs flag adjustments
d6d77eed7408fc162de45c4e4f224a46fff1958f # scx_cake: resolved clippy warnings and applied cargo fmt
eb1430aea3f098e12d600a5427aa0ab744c41930 # spill fixes and jitter optimizations, new highscore benchmark arc raiders
9f382cb071af0ce49651692101897f9d6a9452cc # schbernch improvements round 1 passed
e39b2eca95925d9d2fd1fa3ae66d1ff8d2e71480 # Phase 1: Memory load hoisting in cake_running
8ffdec172f54886713356f8fecb015a419c054d3 # Phase 2: ANDN bit-clearing patterns in cake.bpf.c
b0586831882e0b315b2a3862534f416d3421b3d6 # Phase 3: BEXTR Bitfield Extraction in cake.bpf.c
f48d5dbc700a9e663719c4d723b1bf5e931a08b2 # Phase 4: TZCNT Enforcement in cake.bpf.c
24a15f7567082498468c14941c0145edacd1c4f3 # Phase 5: CMOV Cascade in cake_select_cpu
d1ffe6249f2a4c10367596a014beaf839ee2dff9 # Phase 6: Constant Hoisting & Snapshotting in cake_select_cpu
4a73edcd278720fffe97e0951b612a944ef6021a # experimental optimization via assembly
3f974e58bdb767579a96cf1c0c50ebaba2f086c0 # scx_cake: Implement Phase 1-3 Zen 5 micro-architectural optimizations
d61c34646621d74a4dd9cce599857e631447a70d # checkpoint: BPF callback optimization (4 trampolines)
da9637302fa4fe415fff71fbab0fd3485eaea3fd # scx_cake: Implement Symphony of 2 bypass and resolve build/runtime conflicts
f71c23606fea5828434e04188953401507cbdada # bpf trampoline reduction + dark arts
8568e134890b5d5d6440858adb72a63c86b2349c # fix(scx_cake): isolate dsq_peek legacy fallback to avoid stack spills
c8139d4ffe9e6ccc9758dbe4a2d4bba2b9469cf3 # feat: scx_cake - Absolute Zero v15.4 (Surgical Snatch, Rent's Rule, ALU-Shift)
e5f2c701d21033b91befadcaea18be5ecbb0f746 # refactor(cake): Unified DSQ architecture with async accounting - Replace 7 tier DSQs with single vtime-ordered UNIFIED_DSQ, encode tier in vtime high bits, simplify cake_dispatch to 2 DSQ probes, move accounting to tick, remove D2A signal mask and surgical producer staging. ~250 lines removed. Lowest wakeup latency in cake testing (p99: 2µs).
267d05ca99ffebe463743abfe187d76f79e4e7c6 # chore: code formatting and comment cleanup
f4a3019b99a4e83101e3923f133b48b4c0b0dd98 # checkpoint: before userspace offload optimization
e7aebb4baacb4b8bc6d9efaed8934cd43ea704c8 # chore: comment cleanup
609da0470d26df7a241c1793996da99803d18ae7 # chore: divison cleanup
bc17b576bc88fb046f59c8328db61a19e1ee936a # checkpoint: before adaptive mailbox implementation - cake_stopping hook added, arbiter LUT ETD-tuned
20ba1b983ff0282b6085d381056596eb2cfc66b7 # feat(cake): DDR5 mega-mailbox - zero false sharing per-CPU state
4fa5b11e92b8843942676fe901c406ef0f466801 # perf(cake): Shrink mega-mailbox to 64B for optimal L1 + ALP efficiency
658a8c34424ee3ceae7506e9b7c4375b55b21f05 # perf(cake): Add kfunc caching - cached_now and cached_idle_mask in mailbox
b798f950105fae882096e340e058af492504b85b # perf(cake): Add wakeup latency optimizations - cached masks, best_idle_cpu hint
5c2bdd1419e9f3dcd829109ba19d92ecdf7ee6c1 # perf(scx_cake): tiered short-circuit select_cpu + DB_MAGIC RODATA
45948d6a392e4a2be1e9cccae605a71b5a999df4 # chore(scx_cake): remove trailing whitespace from cake.bpf.c
d8bf622ad53da2ab4dc65fae95eb70c7cab9a323 # scx_cake: fix cache coherency bugs under high-refresh gaming load
51103a09d97762ca92804810a0096434a3bb99ac # chore: add .agent to gitignore
f4a06697c00287811c548409677238094b3a8426 # perf(scx_cake): reduce 99.9% max wakeup latency and jitter
c95bdff5ad0e899ee4924443409a3adb6734a33b # select_cpu: zero hot-path spills via topology self_cpu
04ed7e6279ac8960dc77436fad1d8e3c57900eef # cake_disable: remove redundant bpf_task_storage_delete
fa05da96c3056d081c9bb5d7315abec8356729eb # select_cpu: remove redundant arbiter kick
8f589ff8391e9ec626b9171937b2ee9f893d4833 # select_cpu: remove redundant T0 victim kick
1712b67de7425acd95767d6f9762e684dc9134b8 # scx_cake v1.02: enqueue-time kick, rule 39, dead unroll removal
312101f492a6a0b48d04fb74707f445dfba053f2 # v1.03: T3-only kick + cpumask fix
0e4856ddc9a39b6d81755066f66512b27f303efa # confidence systems + overhead reduction
f751896492cc416efd52183fbd991327a6b95067 # v1.02: TUI startup cleanup — unified color palette, consistent borders, tidy panel titles
03ea2bcc423d4c6e7805fec46cd04a535355f220 # chore(scx_cake): fix Cargo.lock + cargo fmt
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
