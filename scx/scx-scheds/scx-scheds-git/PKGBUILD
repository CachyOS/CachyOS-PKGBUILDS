# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.20.r87.g81ddb563
pkgrel=2
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
628f394aa71554758f0e29f6357930704c2d4b42 # scx_cake: bump scx_cargo to 1.0.27
cb43edbe40c9fd6b6b250537e0fc80c3d2300e4e # scx_cake: bump scx_utils to 1.0.26
8e10060b7594e7c924e259c592a69195328b7c44 # scx_cake: dead code cleanup + p->scx.slice locality promotion
bd875466c290b1eb689b357f70973bce18c1769f # scx_cake: eliminate bpf_task_storage_get from enqueue via dsq_vtime staging
9f3d47bf983a1e10fe480474b25d40e1b57cb4d8 # scx_cake: eliminate bpf_task_storage_get from running and tick via mailbox staging
faf84293a645d79eb3b978956547cd78d1b32d9b # scx_cake: confidence-gated stopping eliminates kfunc for stable tasks
bb567b39b1681bd99a5aa4048217ceaab455588d # scx_cake: benchmark-driven pattern optimizations for 9800X3D
8d45ffe50e82e12088f2fdaa7e2c025d16a96001 # cake: branchless arithmetic optimizations (M6+M8+R1)
9a69052831a256e6dfe1159cdc47b9b4c2d1a69a # fix(cake): eliminate Elden Ring tier oscillation + core migration
48d592a5d6a91ff13d1f61e526456c62a48f09f1 # chore: add mangohud/ to gitignore
0f0a63a1fbc4f054687427d1be8e85e8c227c702 # cake: S2 CPU placement + A4 tick-assembled tier snapshot
0104262f0273261ee3b19701f077ff6b1b21a095 # cake: fix CPU affinity crash in Gate 2 + optimize check
24f05b5b88e26c723472022357c0049b707b52aa # cake: upgrade to scx_bpf_locked_rq with __weak fallback
8d742cdc85bc0c4f9a997488c196acb9f874c4e7 # cake: HFT microarch optimizations (write coalescing + cold-path deferral)
7101a79298914d8f793ddc492c546fcf65d33cc2 # scx_cake: MONSTER optimization + select_cpu affinity fix
e3e377ae7183ce17d4f3778836c875c1873c34af # fix(cake): eliminate u16 counter overflow cascades
a9b297990eb9b9352d4d388ad5d416492d330b98 # fix(cake): correct mega_mailbox struct alignment
45dc5138ee038cd2b205efbc4b4444ea37917ebe # OPT2+OPT6: eliminate kfuncs from cake_stopping
4f522decddcb1427a1ff705e05a07e1776da84d7 # OPT2/OPT6: inline cold classification + slice-delta runtime
172f7956cdbb60daf4b99025930522cc24e9ad7a # cake: Gate 4 lazy preempt — T0/T1 dispatch to worst-tier CPU via LOCAL_ON
d323849b804b913c17f74660d1cf023f9f7a75ad # cake: R2 3-slot cache, R4 WSC hint, C1 interleaved u16, C2 deferred promotion
25e65c3e4cf54eb264705c4dff806d46386d33e9 # cake: Gate 1c migration cooldown (NEAR_PREF) + DUAL_PHASE bimodal slice boost
c3ee565fe19b975a0bff5a0513d5cfbb716e56c7 # scx_cake: detect headless terminal and skip TUI/splash
072160e932ffc13d0f456b93fb30336dd93f8df3 # scx_cake: eliminate 4 sources of false sharing
4478cb9e835babcec6cfa70a34cf45ccba7266e5 # perf(cake): trampoline latency hiding — defer kfunc + pre-load + MLP
9c420b42b8e771d1277adfbef966eeee6ce31d51 # perf(cake): revert spilled pre-loads per codegen audit
ed2577f20aa15f267c0601af67c8c26747fb4acd # cake: remove dead code and add jitter guards
fc5e396248bb8fcdb56991240874effa96e48a43 # arena: migrate per-task storage from bpf_task_storage_get to sdt_task arena
4ca3bdedfacc334b97e83d018acd66bbdb24bb13 # arena: migrate mega_mailbox + global_scratch to arena per-CPU arrays
ff4ee95882d4c03fab359a47c12f2ea351260fa7 # arena: tier-occupancy bitmask for dispatch skip optimization
46f8f44009a7dd0d1f3a0625c8139ad7e6cd105c # arena: optimize ARENA_ASSOC to inline asm (2 insns vs 3)
afb62bd0823e6bab3f76c820187d035f37189b25 # scx_cake: arena audit — fix tier_occupied race, stale preempt scratch
9d5243f11a84927bae364904192a6de0b519193e # cake/bpf: C1 spatial consolidation — merge arena_mailbox + arena_scratch into unified per_cpu block
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
