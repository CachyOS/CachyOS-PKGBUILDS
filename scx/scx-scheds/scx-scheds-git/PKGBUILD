# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.20.r92.gc61e9370
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
8210d92f4e94ab3ce687e45682377cd61aeeea57 # scx_cake: dead code cleanup + p->scx.slice locality promotion
55ed922a24a511b7d32046a156f4be689b0d0edf # scx_cake: eliminate bpf_task_storage_get from enqueue via dsq_vtime staging
8ba123602dbb1d2b2ad96b22f071a699ceae4518 # scx_cake: eliminate bpf_task_storage_get from running and tick via mailbox staging
2e8e8bbf9aa2ac41079aeb9fbcadb120f0f2ce4d # scx_cake: confidence-gated stopping eliminates kfunc for stable tasks
b7c4af3d9269468f29c65cf516b77f2954e2fa66 # scx_cake: benchmark-driven pattern optimizations for 9800X3D
2a06ffafbfaa150e2cdc33783366cbee4f917a83 # cake: branchless arithmetic optimizations (M6+M8+R1)
499d23ffbfff283bb5307e233dc93ef187ab3c28 # fix(cake): eliminate Elden Ring tier oscillation + core migration
136b16b9e17c0a416cff73f10bb84edb45b8ee66 # chore: add mangohud/ to gitignore
1c458cd48eaf922662e8236a25df56f61b36085b # cake: S2 CPU placement + A4 tick-assembled tier snapshot
a7c236b525f10cc533763f8e05279c128ac4afe8 # cake: fix CPU affinity crash in Gate 2 + optimize check
5758744a7de45d67e5cba9cacf7289370487637d # cake: upgrade to scx_bpf_locked_rq with __weak fallback
a8862a25c9dee785b3015211f5efb2c9646d477c # cake: HFT microarch optimizations (write coalescing + cold-path deferral)
8eba5492d701572fb1bf0fb7b577f271e517e6b1 # scx_cake: MONSTER optimization + select_cpu affinity fix
01e7ce4a1853852e64f40c51e207144cdec7b4a8 # fix(cake): eliminate u16 counter overflow cascades
e4a7c88b90aad7db2c7718128c8c070510fb1b93 # fix(cake): correct mega_mailbox struct alignment
fda098be417eb786a181bafef73ffa5eeda7b7d7 # OPT2+OPT6: eliminate kfuncs from cake_stopping
6082c31c6a694d757855098baf340fde4f582f99 # OPT2/OPT6: inline cold classification + slice-delta runtime
e886e649f90e1905432bcc2dbb06dbe12712f294 # cake: Gate 4 lazy preempt — T0/T1 dispatch to worst-tier CPU via LOCAL_ON
c626d3bb62bf4b308b41b90fe6422191a60e750b # cake: R2 3-slot cache, R4 WSC hint, C1 interleaved u16, C2 deferred promotion
1ff9585b0b289f17fd497401ec38503cd57d0dc6 # cake: Gate 1c migration cooldown (NEAR_PREF) + DUAL_PHASE bimodal slice boost
7b7fede9b0c9d36a6018fffa92f3a2f5c35b838d # scx_cake: detect headless terminal and skip TUI/splash
2c808a8f5759e5b5f5fd5b44d7602e3c214141f0 # scx_cake: eliminate 4 sources of false sharing
a8ec6105af044463a65a5c0d5391bdba55d7b726 # perf(cake): trampoline latency hiding — defer kfunc + pre-load + MLP
cf3c563afebc9d486ef732480998aecc4d508543 # perf(cake): revert spilled pre-loads per codegen audit
800f49e517333d8d822117d4af73925ad5dbc74c # cake: remove dead code and add jitter guards
74dc5e558e2138816e9d8ac106aa0755961c45ca # arena: migrate per-task storage from bpf_task_storage_get to sdt_task arena
28c5b7d4cd8fe1e47e107040f45cb47702c09de4 # arena: migrate mega_mailbox + global_scratch to arena per-CPU arrays
2648a35a47059a267921112bce0edf1f8c06e90c # arena: tier-occupancy bitmask for dispatch skip optimization
809f9b5c986d912131b7f9093b502dfb6e283db7 # arena: optimize ARENA_ASSOC to inline asm (2 insns vs 3)
5cf784d5ef50f420c3fb5a2eac4e1a1b90df1b7d # scx_cake: arena audit — fix tier_occupied race, stale preempt scratch
7569ba65cb00b42d72276b25ad42f65088c8c55d # cake/bpf: C1 spatial consolidation — merge arena_mailbox + arena_scratch into unified per_cpu block
e4ca51ea9a42b51c8bad10b987d6cff3632adba5 # scx_cake: bump scx_arena version to 0.1.4
661f572c2f954a83f07f553993700cdfc62e44fb # scx_cake: fix CI exit code 143, proper UEI shutdown, skip ETD headless, fix heatmap layout
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
