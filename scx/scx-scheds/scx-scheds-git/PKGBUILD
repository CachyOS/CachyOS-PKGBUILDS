# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.19.r288.g7a190025
pkgrel=2
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
6c8b9fd2b9436b66cf85abcbc7d0c8f9e6c8ab8a # Add scx_cake scheduler to workspace
a61f4a9931da52fe797744f8764ea1f46aa24d8a # Add scx_cake scheduler to workspace
34d73e6fd1dc96243e9d9c2082dcaf66a2c7bcf7 # scx_cake: Use local path dependencies instead of git
1f8acdb8c30e76ba81a6600f47c02224a33e3569 # Update Cargo.lock for scx_cake
93ef844ead99a0e10cc97979edd9f143042829eb # scx_cake: Run cargo fmt
d870ee6955baaa5f07130ff7f52b70eeeee60d3a # scx_cake: add kernel 6.16+ compatibility for scx_bpf_cpu_curr
dc1c50e04f129e10d5f827b416a139473726090f # scx_cake: Implement Check & Skip Optimizations and SMT Fix
0d3b906982496247e8878573df3e3a10ad00d97b # perf sched map optimization
086cfa034f225d135afbd3e29052fbf69dff9870 # feat(scx_cake): implement state-based per-cpu direct dispatch
84a51fbdcbf738b305db0b15255224b420ddfe77 # Ghost Scheduler Optimizations: Store coalescing and bit testing
8bb1a7422f4e6b17711dc67f8548d843ceb396b8 # bug fixes, floor for runtime, healthy IPC
6f68814227e98d24b2096c24316a39fbd5016ed0 # Great benchmarks - need to review jitter
696b4b51b7d2a8a531e908e99a668d39fa84b5d5 # scx_cake: Implement Zero-Math Locality Arbiter
64fca7ac7ed2c250cc2629cda60dfd77debb1239 # scx_cake: warning removed from build
9f9ef1fb7588943949b7c0ab635046ef4267e30a # struct alignment 4 byte for clang 19+
08728a7fed72b995b798ece9b475206a52a3a687 # BPF compat: Use semantic macros for 32-bit atomics to fix Clang 19 crash
c26b2468300715f5dd5844098c5f28e341bbfa60 # BPF: Optimize MLP by migrating volatile loads to relaxed semantic macros
2da5a1acb3a1ea3a6645991f5bc2d1ed92d09d27 # BPF: Bulletproof compat layer with inline ASM for Clang < 21
294f723b042da18f930b690fad4a38945fb88c58 # Checkpoint: Immediate Consumption pattern - baseline 53 spills restored
f35b46db691c376babe77c50c75b372f56c7fa31 # Absolute Zero Spill: Eliminate remaining BPF spills in cake_select_cpu (53 -> 0). Verified with llvm-objdump.
8cece32d005948d23cb912f5b00e1da1582c0e30 # Optimize: Implement more aggressive warmth seeking (Stubborn Warmth)
e619f758d43113d05dff206b6e77dfdf1b3b56e9 # perf(cake): remove volatile from 15 rodata declarations for JIT constant folding
372872233bdd3133fdd3d7df4272364629abd157 # feat(cake): v1.01 with clean ANSI startup splash
7230094a97fb90bad5de1f68b8015c0d5b028014 # chore(cake): bump version to 1.0.1
c617aca4d478dec1ebaf70aecb01119834e74992 # scx_cake: Remove LOCK prefixes from victim_mask ops (5 atomics → 2)
97411729661d81fc3e062ee55d7bff23a565c487 # scx_cake: Phase 2 tiered masks - 40-60% reduced atomic hit rate
87678b2ef0b7b970f1ab11e5f36d92aa20984484 # scx_cake: cake_update_idle zero-spill optimization
63005c94cc389981eada95034c7497d27d849916 # scx_cake: eliminate all BPF register spills via cold path optimization
03c22aa80106646b1d7aff70a34cebab3c35d7d3 # scx_cake: add cache-line padding to cold_scratch
faac2f59095e3d23d2718e96034796eb6d3d4581 # scx_cake: Empirical Topology Discovery (ETD) System Review
bc242f75cc0b1bc3bbfb74f81065944acb0f5257 # spill fixes
474b8c95d340c60423171e2d090b29c4c327dfeb # fix(cake): prioritize cache warmth over ETD to reduce jitter
ebf72230c8001bc071235d98412eb1db91a79b33 # cold path optimizations
ef70aa4285e9e80f54e589cccabb3ea7cd3c965f # optimization leading to better fps, lower latency and better 50% frame distribution
0c37bef75dbcb1bfeb687ccffeed7ece55871bff # fuse optimization round 1 passed
eb11e2c5a9dada28ef137b9426439e151499ebd5 # fuse round 2 pass
1e2519a0dcde36ed061ea67eac8dbcb3ec41ddf4 # MESI-friendly warm-tier updates: skip-if-unchanged optimization
3aef7fa778923c10a7240f0211f7ecf8fd480903 # skip-if-unchanged optimization for packed_info in cake_stopping
d898e70c1e4959e3f8ec8225d6b2b2a7a6bf4de2 # cache line separation for tiered_idle_mask to eliminate false sharing
ca19eda8cb467f763b1e487ba87870828d65bcf5 # branchless clamp in compute_sparse_score using nested ternary
fd8303e5f93dbd05d720bf6b53cc3d86bf6518dd # scx_cake: fix build errors in calibrate test and improve signal handling consistency
37ebf7bf7a3af58155e16a683d56af93c70971a4 # Refactor: Topology-Agnostic 'Physical-First' isolation and Zero-Math Arbiter implementation
f036b1ae68bc13419dbda3c6504911caedc3cfd3 # Perf: BPF atomic optimizations (TTAS) and build.rs flag adjustments
1007963f00b9c06f4d14f675ca83a766a6b00412 # scx_cake: resolved clippy warnings and applied cargo fmt
cab15f4e78ded627bd003ecf6aab41ef930f4712 # spill fixes and jitter optimizations, new highscore benchmark arc raiders
ceac3a0c19be791afc8a240cf877d1a5a8c1c9c4 # schbernch improvements round 1 passed
b7f53a59c1263771e11e2ab44a4a6897b644f4e1 # Phase 1: Memory load hoisting in cake_running
51c371f63afacc499845963c722fda345f540afc # Phase 2: ANDN bit-clearing patterns in cake.bpf.c
09efa5827c54edda3b932ff1b66213b443486eab # Phase 3: BEXTR Bitfield Extraction in cake.bpf.c
b2ff797064d2e77cfbaec805bc012bb1a4b222da # Phase 4: TZCNT Enforcement in cake.bpf.c
ecaf36275b8fb4005ceab9c665eb6d2c796f0766 # Phase 5: CMOV Cascade in cake_select_cpu
805f9a6a1ff2bbe49b073ac6b18b9f5f3dbc214e # Phase 6: Constant Hoisting & Snapshotting in cake_select_cpu
012c15c0216a42802a15c3936314e48006828d3c # experimental optimization via assembly
d0a4eb044bbbdf68a03787d77d5efb727c51e5f5 # scx_cake: Implement Phase 1-3 Zen 5 micro-architectural optimizations
b053bdbd3555cb10c39eb686528711359daf9f1b # checkpoint: BPF callback optimization (4 trampolines)
75f95913f5a1d72c491ea8ca9d2bf59c920d7d44 # scx_cake: Implement Symphony of 2 bypass and resolve build/runtime conflicts
9c69fcf9734654f9b16db85e92280ce548784bff # bpf trampoline reduction + dark arts
a586774f955260ced116bfc37b133a989eda2501 # fix(scx_cake): isolate dsq_peek legacy fallback to avoid stack spills
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
