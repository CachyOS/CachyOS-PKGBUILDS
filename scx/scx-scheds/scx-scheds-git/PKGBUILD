# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.20.r79.gf82cefc3
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
3f38a6b5080d2af5413dceb59ad4e870664cc0fa # scx_cake: dead code cleanup + p->scx.slice locality promotion
daa4467ab12a5a92cd4711aa467d86de79d0aff2 # scx_cake: eliminate bpf_task_storage_get from enqueue via dsq_vtime staging
62626b9b9ff435e9f70c1e63b72f505059cf25ef # scx_cake: eliminate bpf_task_storage_get from running and tick via mailbox staging
ff00f64dac2da063364b1e9bf4cfd469531d93c5 # scx_cake: confidence-gated stopping eliminates kfunc for stable tasks
b82ad16129bc38103a3d53f45a0da9fe9ba07c40 # scx_cake: benchmark-driven pattern optimizations for 9800X3D
deb3e3b45fcc32604d06ccaed6f72f3795e9f221 # cake: branchless arithmetic optimizations (M6+M8+R1)
ad895c1f2109a9356b0fade31c47310622aeff52 # fix(cake): eliminate Elden Ring tier oscillation + core migration
cd8133682608c917c57aedb7d05a42118368296e # chore: add mangohud/ to gitignore
a74a37f57eb98bf36072ed431ed9527fd1dac2d7 # cake: S2 CPU placement + A4 tick-assembled tier snapshot
abad120527976bd56074b0599c53b31b43e7c49e # cake: fix CPU affinity crash in Gate 2 + optimize check
f736f1cc3f817db3ca9bc8ab9253f79945a335bf # cake: upgrade to scx_bpf_locked_rq with __weak fallback
ea405d3c8ed57d05baf3c023884d57b160faf47d # cake: HFT microarch optimizations (write coalescing + cold-path deferral)
21fce551265cb8d80b64fefc524e2df81cd071e6 # scx_cake: MONSTER optimization + select_cpu affinity fix
ecbc0b0f439b830e222954ad70e88424d6c9f5c6 # fix(cake): eliminate u16 counter overflow cascades
c0ff7f4136f44f25674a52191182bf4cf6326c36 # fix(cake): correct mega_mailbox struct alignment
9e7c0b65bf0ec111ff3b212927074ed19eedeaa2 # OPT2+OPT6: eliminate kfuncs from cake_stopping
4c93ae054a5bd45b2de227ae7222bac292ead67a # OPT2/OPT6: inline cold classification + slice-delta runtime
e78967f6b207eac389f4fa69f2573c61f8f0b367 # cake: Gate 4 lazy preempt — T0/T1 dispatch to worst-tier CPU via LOCAL_ON
2f9c1bbbd4c32575eef0d7b1b5fc2444e4ed2702 # cake: R2 3-slot cache, R4 WSC hint, C1 interleaved u16, C2 deferred promotion
c20c49048e96733ca051b2beaf0eacd1f2a16db1 # cake: Gate 1c migration cooldown (NEAR_PREF) + DUAL_PHASE bimodal slice boost
b5559504520336036c84114fd944018851c647a3 # scx_cake: detect headless terminal and skip TUI/splash
43bfedaba54f7caca9729206ba2d71d658936f85 # scx_cake: eliminate 4 sources of false sharing
b6ee8d3ad9564e4c07ce2598e720874e60dbf7a0 # perf(cake): trampoline latency hiding — defer kfunc + pre-load + MLP
07cca695e2e39e56b992963b407ce779d89d3f1c # perf(cake): revert spilled pre-loads per cod
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
