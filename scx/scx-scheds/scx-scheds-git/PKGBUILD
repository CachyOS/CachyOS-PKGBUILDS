# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.20.r87.g81ddb563
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
c6b8333071b83475ffd9be60986cc42497099b8f # scx_cake: dead code cleanup + p->scx.slice locality promotion
a17b07353e9b3472625bf50cf6d526d489539409 # scx_cake: eliminate bpf_task_storage_get from enqueue via dsq_vtime staging
025ca5a6b2001f522e2a743c0f2390b543c7dd65 # scx_cake: eliminate bpf_task_storage_get from running and tick via mailbox staging
3618ea9b84283437c3f9f979ad3793563ba58d10 # scx_cake: confidence-gated stopping eliminates kfunc for stable tasks
a86726ca2a11af1b2c97b9cbaffa2654bf888792 # scx_cake: benchmark-driven pattern optimizations for 9800X3D
58fab08ae65288f98d83577c03b123a115333719 # cake: branchless arithmetic optimizations (M6+M8+R1)
0c46492008797d6ead82fb091eaa75f2e526b824 # fix(cake): eliminate Elden Ring tier oscillation + core migration
f66736abfa249867540c7260577a515a641f3517 # chore: add mangohud/ to gitignore
86d594c1d865c01cfb69954eb8ce1a9f49480631 # cake: S2 CPU placement + A4 tick-assembled tier snapshot
e3f058ada6f9a9845fdf51ba1800b60e16e3eb22 # cake: fix CPU affinity crash in Gate 2 + optimize check
58948fac033a51afe38fa563cfd21860d0c0c328 # cake: upgrade to scx_bpf_locked_rq with __weak fallback
b6c6b329550a8ede58a9c0f3f933db75a72f684e # cake: HFT microarch optimizations (write coalescing + cold-path deferral)
b1c52b682c239fa89b67d7084a77446a3ca63db9 # scx_cake: MONSTER optimization + select_cpu affinity fix
17bb68cca4fc100d0ad71977935ac730cc7eabe6 # fix(cake): eliminate u16 counter overflow cascades
b89593be303d19130e94ee3dbf6ba97c677b1720 # fix(cake): correct mega_mailbox struct alignment
135f1b4e5cdf22e110abc09033b2e1194ad5dfde # OPT2+OPT6: eliminate kfuncs from cake_stopping
bb994bc70af5f90035746f02936fbcfeec26a49d # OPT2/OPT6: inline cold classification + slice-delta runtime
00dfec54e772ea26bf07f57f43d5762f5771e2f4 # cake: Gate 4 lazy preempt — T0/T1 dispatch to worst-tier CPU via LOCAL_ON
ff2f227e0618eb4b27645cf9641ef39cba7e52a3 # cake: R2 3-slot cache, R4 WSC hint, C1 interleaved u16, C2 deferred promotion
fd955d02bdc205162d2cc17e3a603ce0312c2a93 # cake: Gate 1c migration cooldown (NEAR_PREF) + DUAL_PHASE bimodal slice boost
41b5f75825b319940204aa1628a9611b8bbebd94 # scx_cake: detect headless terminal and skip TUI/splash
52760185f5570340ce8214b5beab506328830329 # scx_cake: eliminate 4 sources of false sharing
bda6b47cab35f465783169460a02d4476ebffd25 # perf(cake): trampoline latency hiding — defer kfunc + pre-load + MLP
9dcf317aa04df2983bf51da82dad70817bae38a4 # perf(cake): revert spilled pre-loads per codegen audit
f7d3c2c433c342a389f839d3507e8ceddc43852a # cake: remove dead code and add jitter guards
8a809999ca568ac9413e205029c0de0a3f582f01 # arena: migrate per-task storage from bpf_task_storage_get to sdt_task arena
314e562620c94f4426d60cefd8662765a41ddc68 # arena: migrate mega_mailbox + global_scratch to arena per-CPU arrays
f9d0de153b879ea3239bcd461cfccc9456006ec5 # arena: tier-occupancy bitmask for dispatch skip optimization
8a7d40b5fc40c7ce5c5ecc1694f7f7a7ed7f3542 # arena: optimize ARENA_ASSOC to inline asm (2 insns vs 3)
c8bba45dc320d44963f2124d2472f0205d13521e # scx_cake: arena audit — fix tier_occupied race, stale preempt scratch
0bd18a906675055e8997f0a888b5bd651d27b279 # cake/bpf: C1 spatial consolidation — merge arena_mailbox + arena_scratch into unified per_cpu block
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
