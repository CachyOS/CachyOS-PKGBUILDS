# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.20.r40.g1fc73cdb
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
860d78ef86337707ce6b23c8b3c17eb3e769ad6f # scx_cake: dead code cleanup + p->scx.slice locality promotion
5829323dc21279c8dc417b83354ed80e1ae644ae # scx_cake: eliminate bpf_task_storage_get from enqueue via dsq_vtime staging
37b428f32ec283b23bb4b543e83b66ca9f9adedf # scx_cake: eliminate bpf_task_storage_get from running and tick via mailbox staging
029d2a2b0a2c42299b65c650b357c422ac56e3ce # scx_cake: confidence-gated stopping eliminates kfunc for stable tasks
6c9cb878dd2505eeacf438c6ee9bc71e91acf138 # scx_cake: benchmark-driven pattern optimizations for 9800X3D
49fadce2d1a4b2eaf2f5226c1de4c4e641709dee # cake: branchless arithmetic optimizations (M6+M8+R1)
7202b5981313efdbac891d34623ef1b4cb882e8e # fix(cake): eliminate Elden Ring tier oscillation + core migration
5b8e399e2b441ee46ec1fb9eb6a5be94e5f7cc25 # chore: add mangohud/ to gitignore
eac79794597b8db9ec9c929047b0760786daae68 # cake: S2 CPU placement + A4 tick-assembled tier snapshot
e479adc702005e59a013c2d9fd1b32195dcfec9c # cake: fix CPU affinity crash in Gate 2 + optimize check
8d2275111d3fb9ae873d113988f87fb3c44dc4b9 # cake: upgrade to scx_bpf_locked_rq with __weak fallback
09100f420dc59553783ccd36c52973c8d1b729e3 # cake: HFT microarch optimizations (write coalescing + cold-path deferral)
b1accc79ba29232969edec2d471cd3cdb721e1d2 # scx_cake: MONSTER optimization + select_cpu affinity fix
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
