# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr Górski <lucjan.lucjanov@gmail.com>

pkgname=scx-scheds-git
_gitname=scx
pkgver=1.0.19.r334.g0fe8a0f4
pkgrel=1
pkgdesc='sched_ext schedulers and tools'
url='https://github.com/sched-ext/scx'
arch=('x86_64' 'aarch64')
license=('GPL-2.0-only')
depends=(
  bash
  bpf
  gcc-libs
  glibc
  jq
  libseccomp
  libbpf
  libelf
  protobuf
  scx-tools
  zlib
)
makedepends=(
  clang
  git
  llvm
  llvm-libs
  python
  rust
)
source=("git+https://github.com/sched-ext/scx")
sha256sums=('SKIP')
options=(!lto)
provides=("scx-scheds=$pkgver")
conflicts=("scx-scheds")
replaces=('scx-cake')

_backports=(
d6949ac1cf2142368238fc5c1d01b1b7a7cd6648 # Add scx_cake scheduler to workspace
4731c2edc7255c6fa4765d7b74017af33ad05274 # Add scx_cake scheduler to workspace
36a1a1cf967a6b40bfca8328c613e0837966ca5b # scx_cake: Use local path dependencies instead of git
0a223b53ea2d5f12345cea494ef6fc1963e1b97f # Update Cargo.lock for scx_cake
6d035aef2300f074f875ea4b48220d4101768756 # scx_cake: Run cargo fmt
ab6055ecfe3a04e202a6dd8ba90595ce73cadcc4 # scx_cake: add kernel 6.16+ compatibility for scx_bpf_cpu_curr
a357c713e7cbf850dcab34ffeb5831ae53b4913b # scx_cake: Implement Check & Skip Optimizations and SMT Fix
b54c517d8a6c20aa0a1d0c82245744b073942c73 # perf sched map optimization
0550f32d66219a5db94aef2cd0355cd8602b4f87 # feat(scx_cake): implement state-based per-cpu direct dispatch
3444d0eef96f5de65d1662e749beba23b5699a90 # Ghost Scheduler Optimizations: Store coalescing and bit testing
e1074b3b56013d71922e0f5ff73077a13500042c # bug fixes, floor for runtime, healthy IPC
dc189359da269004ee043d74f75ab4a5064ae44d # Great benchmarks - need to review jitter
7fdb32f591c0530e825301f3492bdcab523ae87b # scx_cake: Implement Zero-Math Locality Arbiter
8296122d9ca2aca2cc36ef7d768cb45d25420cd9 # scx_cake: warning removed from build
44fcb0d95bf432ad95d70e01b12b3c024db557b0 # struct alignment 4 byte for clang 19+
1d3e8f027f6f376fbcf2476ae1ac657ac66d5919 # BPF compat: Use semantic macros for 32-bit atomics to fix Clang 19 crash
849e0ef98f338abc4b1c989fdb867b9b2da2f311 # BPF: Optimize MLP by migrating volatile loads to relaxed semantic macros
3c445817c2e7e99069e123a9baa424bf8f4697ef # BPF: Bulletproof compat layer with inline ASM for Clang < 21
8edcf5046a134ce7fc9bc62a3fa1c7091eeef8ec # Checkpoint: Immediate Consumption pattern - baseline 53 spills restored
2508596495df9b73c338b387558449f8a8cbb57a # Absolute Zero Spill: Eliminate remaining BPF spills in cake_select_cpu (53 -> 0). Verified with llvm-objdump.
aecd192783cde6e5e1c2f91102b23b4c74d83f10 # Optimize: Implement more aggressive warmth seeking (Stubborn Warmth)
dc0c5b67911eb39c752675f29a5f8a3d6feca370 # perf(cake): remove volatile from 15 rodata declarations for JIT constant folding
03221ecd112b4dc225b06ed32a016e8c68ddba42 # feat(cake): v1.01 with clean ANSI startup splash
e46ca8c61d173d24736c57f6cc9f18b805d00f37 # chore(cake): bump version to 1.0.1
5a558b320d75fe0a8f954b3e7844ce1d7ed7f046 # scx_cake: Remove LOCK prefixes from victim_mask ops (5 atomics → 2)
c9d2224c0b8dc1dbebbf90210402346aa47ae9d3 # scx_cake: Phase 2 tiered masks - 40-60% reduced atomic hit rate
a0130ba7d8cd86966c3eb51025450de4465998e5 # scx_cake: cake_update_idle zero-spill optimization
f62b29dee56cfc3b5ab25d14768351c8af09f496 # scx_cake: eliminate all BPF register spills via cold path optimization
06c2c0b84e210a0118ba74b92e175e29906bb9a1 # scx_cake: add cache-line padding to cold_scratch
35bc93611e24d9a0fa6ddcb177af2fadad475c11 # scx_cake: Empirical Topology Discovery (ETD) System Review
ab2d31db94b4fda232af3b158bf48b55db783ac1 # spill fixes
85d803686eb1a6629ada950f5b54fe7c59d14136 # fix(cake): prioritize cache warmth over ETD to reduce jitter
878e3350d20ddeb718910615463fc384295e0e4d # cold path optimizations
40b7c20b292038ca8130d10037eaf5c2ff275cfb # optimization leading to better fps, lower latency and better 50% frame distribution
0b1788921821ea91883e007f31188e06edbdfc40 # fuse optimization round 1 passed
77fc1286c81cf7a7fdf5b8faaa3c16ec9239799f # fuse round 2 pass
f63f42cdda76ef836586b2a0938e4f3ae81981a5 # MESI-friendly warm-tier updates: skip-if-unchanged optimization
918126d3b2db4fc8cb71e9bb80db22d2a00ef58a # skip-if-unchanged optimization for packed_info in cake_stopping
6dcc833f71b5ca48123e378ec2d4a85de3ccb630 # cache line separation for tiered_idle_mask to eliminate false sharing
1d84de858fc3ba1d531c9b5e570efddb73f4f774 # branchless clamp in compute_sparse_score using nested ternary
7183fc668e0fa57a0090118ddb5efa75a6df1ac9 # scx_cake: fix build errors in calibrate test and improve signal handling consistency
b766ec5cb566cfbd5fc0734b2d07025f81e4c02a # Refactor: Topology-Agnostic 'Physical-First' isolation and Zero-Math Arbiter implementation
94da7ee2ebc2e2751ce39697c69d14a1798c9893 # Perf: BPF atomic optimizations (TTAS) and build.rs flag adjustments
68658e36c8854c4960551aec15b2b30db05eb741 # scx_cake: resolved clippy warnings and applied cargo fmt
75cab8ad5024533bc8d834d0f5c416b25ad3db5e # spill fixes and jitter optimizations, new highscore benchmark arc raiders
a6675739298d38a146c15192b5f5c6c646a8dfbd # schbernch improvements round 1 passed
2fe903c9d71df01c0bcb6a5ec939585313c7e1f9 # Phase 1: Memory load hoisting in cake_running
7dbb42775865e90d9ab7225ba079f1e9ef8fcb7c # Phase 2: ANDN bit-clearing patterns in cake.bpf.c
43bc7600238940fd69632e70311f9d0c1da05069 # Phase 3: BEXTR Bitfield Extraction in cake.bpf.c
cb63a348121959e858bce8708aadf5dc9f8a879b # Phase 4: TZCNT Enforcement in cake.bpf.c
274ebdb7f7a02f707616e84abefc29fd836885b0 # Phase 5: CMOV Cascade in cake_select_cpu
ad5199a582768d5c326a04ba7ee640e55c6187aa # Phase 6: Constant Hoisting & Snapshotting in cake_select_cpu
9dfe0cca185947c46687a928fcf2dadd3facc503 # experimental optimization via assembly
d1ff75e1cc41304577b14ff3ebd864766774db5c # scx_cake: Implement Phase 1-3 Zen 5 micro-architectural optimizations
8e0e1651175c7d5bf756f7f18aada5e7d729a5e3 # checkpoint: BPF callback optimization (4 trampolines)
bb9a8ada7ae3941b4ffccf34bcde19fdb57a4621 # scx_cake: Implement Symphony of 2 bypass and resolve build/runtime conflicts
1ea2a8f09aa89c60a84c1118669eb9b638122e29 # bpf trampoline reduction + dark arts
562fd16efb519f9763941b162d09495e59b6e925 # fix(scx_cake): isolate dsq_peek legacy fallback to avoid stack spills
b4924ff4d746e57b9cf7a542ac771fa3753fbf17 # feat: scx_cake - Absolute Zero v15.4 (Surgical Snatch, Rent's Rule, ALU-Shift)
3f67b9073a4f1cc0db91119aea47361234393bcc # refactor(cake): Unified DSQ architecture with async accounting - Replace 7 tier DSQs with single vtime-ordered UNIFIED_DSQ, encode tier in vtime high bits, simplify cake_dispatch to 2 DSQ probes, move accounting to tick, remove D2A signal mask and surgical producer staging. ~250 lines removed. Lowest wakeup latency in cake testing (p99: 2µs).
1aad8556f1e51cad10f5bb15551fbf66e06aaf0f # chore: code formatting and comment cleanup
a45a0323eb1da7384d9b2f14cc55f7cd491cee0c # checkpoint: before userspace offload optimization
aaf08f43d9d429e2aaead9510ed63aa02295f7c6 # chore: comment cleanup
caef7f81c370bdddfed25a640f33e3cbe3d144e1 # chore: divison cleanup
)

_reverts=(
)

pkgver() {
  cd $_gitname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname

  local _c _l
   for _c in "${_backports[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git cherry-pick --mainline 1 --no-commit "${_c}"
   done
   for _c in "${_reverts[@]}"; do
     if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
     git log --oneline "${_l}" "${_c}"
     git revert --mainline 1 --no-commit "${_c}"
   done

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done

  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_gitname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build \
     --release \
     --frozen \
     --all-features \
     --workspace \
     --exclude scx_rlfifo \
     --exclude scx_wd40 \
     --exclude scx_mitosis \
     --exclude scxcash \
     --exclude xtask \
     --exclude vmlinux_docify \
     --exclude scx_arena_selftests
}

package() {
  cd $_gitname

  # Install all built executables (skip .so and .d files)
  find target/release \
    -maxdepth 1 -type f -executable ! -name '*.so' \
    -exec install -Dm755 -t "$pkgdir/usr/bin/" {} +
}
