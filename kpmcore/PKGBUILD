# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpmcore
pkgver=24.05.2
pkgrel=2
pkgdesc='Library for managing partitions'
arch=(x86_64)
url='https://apps.kde.org/partitionmanager/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcoreaddons
         ki18n
         kwidgetsaddons
         polkit-qt6
         qt6-base
         smartmontools
         util-linux-libs)
makedepends=(extra-cmake-modules)
optdepends=('bcachefs-tools: BCacheFS support'
            'dosfstools: FAT32 support'
            'e2fsprogs: ext2/3/4 support'
            'exfat-utils: exFAT support'
            'exfatprogs: exFAT support (alternative to exfat-utils)'
            'f2fs-tools: F2FS support'
            'fatresize: FAT resize support'
	    'jfsutils: JFS support'
            'nilfs-utils: nilfs support'
            'ntfs-3g: NTFS support'
            'reiserfsprogs: Reiser support'
            'udftools: UDF support'
            'xfsprogs: XFS support')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
       bcachefs.patch::https://invent.kde.org/system/kpmcore/-/commit/21abcf81fe8f049912daaed62468c4cfd95d4bfb.patch)
sha256sums=('8e8646bfe990834acbbe56149015aaecd3099cc2e9115ac7426a2d88747735db'
            'SKIP'
            '6f8bee907d564b9cd1bf20495811cd1240dc9a0ca3ad1112086ddbe29db9220c')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
	cd $pkgname-$pkgver
	patch -Np1 < ../bcachefs.patch
}
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
