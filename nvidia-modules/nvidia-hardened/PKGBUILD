# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
pkgname=nvidia-hardened
pkgver=545.29.02
pkgrel=1
epoch=1
pkgdesc="NVIDIA drivers for linux-hardened"
arch=('x86_64')
url="https://www.nvidia.com/"
makedepends=('linux-hardened-headers' "nvidia-dkms=$pkgver")
provides=('NVIDIA-MODULE')
license=('custom')
options=('!strip')

build() {
    _kernver=$(</usr/src/linux-hardened/version)

    fakeroot dkms build --dkmstree "${srcdir}" -m nvidia/${pkgver} -k ${_kernver}
}

package() {
    depends=('linux-hardened' "nvidia-utils=${pkgver}" 'libglvnd')

    _kernver="$(</usr/src/linux-hardened/version)"

    install -Dt "${pkgdir}/usr/lib/modules/${_kernver}/extramodules" -m644 nvidia/${pkgver}/${_kernver}/${CARCH}/module/*

    # compress each module individually
    find "$pkgdir" -name '*.ko' -exec xz -T1 {} +

    install -Dm644 /usr/share/licenses/nvidia-dkms/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
