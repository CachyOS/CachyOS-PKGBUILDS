# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Peter Jung <ptr1337@archlinux.org>
# Contributor: soloturn <soloturn@gmail.com>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-comp
pkgver=1.0.0.alpha.4
pkgrel=1
pkgdesc='Compositor for the COSMIC desktop environment'
arch=(x86_64)
url='https://github.com/pop-os/cosmic-comp'
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  fontconfig
  gcc-libs
  glibc
  libseat.so
  libinput
  libxcb
  libxkbcommon
  mesa
  pixman
  systemd-libs
  wayland
)
makedepends=(
  cargo
  git
  lld
)
_tag=e3b41c5c554cc5aadf44183866ff4df7c2957d09
source=(git+https://github.com/pop-os/cosmic-comp.git#tag=${_tag})
b2sums=('d9bb58edae3581f39076cc79fa17294490e84c1907c4ba484877592c632a997354778398d31058a9f8e656d8c73e64e3613734dd275b2c771a774dbf8f064590')

prepare() {
  cd cosmic-comp
  msg2 "Apply Explicit Sync to cosmic-comp"
  git cherry-pick -n 575b6611bc2e6c82c267b9e6e48af66875dc4f46
  cargo fetch --locked
  sed 's/lto = "fat"/lto = "thin"/' -i Cargo.toml
}

#pkgver() {
#  cd cosmic-comp
#  git describe --tags | sed 's/^epoch-//; s/-/./g'
#}

build() {
  cd cosmic-comp
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd cosmic-comp
  make prefix='/usr' libexecdir='/usr/lib' DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
