# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgbase=localsearch
pkgname=(
  localsearch
  localsearch-testutils
)
pkgver=3.8.0
pkgrel=1
pkgdesc="Filesystem indexer and metadata extractor"
url="https://gnome.pages.gitlab.gnome.org/localsearch/"
arch=(x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  exempi
  gcc-libs
  giflib
  glib2
  glibc
  gst-plugins-base-libs
  gstreamer
  icu
  libcue
  libexif
  libgexiv2
  libgsf
  libgudev
  libgxps
  libiptcdata
  libjpeg-turbo
  libosinfo
  libpng
  libseccomp
  libtiff
  libxml2
  poppler-glib
  tinysparql
  totem-plparser
  upower
)
makedepends=(
  asciidoc
  bash
  git
  glib2-devel
  gobject-introspection
  meson
  python
  python-gobject
)
source=("git+https://gitlab.gnome.org/GNOME/localsearch.git#tag=${pkgver/[a-z]/.&}")
b2sums=('75005c92fc3b949b063985f1dfa780a328ce66b58e51a824b6b2ed1b494d24bf15b7b9c62811923887197229b4dc29deee9898ab331f8d4a99e3c08127a02644')

prepare() {
  cd localsearch
}

build() {
  local meson_options=(
    -D landlock=enabled
    -D network_manager=disabled # Used only if miner_rss enabled
    -D tests_tap_protocol=true
  )

  arch-meson localsearch build "${meson_options[@]}"
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_localsearch() {
  depends+=(libtinysparql-3.0.so)
  provides=("tracker3-miners=$pkgver")
  replaces=('tracker3-miners<3.8.0')
  conflicts=('tracker3-miners<3.8.0')

  meson install -C build --destdir "$pkgdir"

  _pick testutils "$pkgdir"/usr/lib/localsearch-3.0/trackertestutils
}

package_localsearch-testutils() {
  pkgdesc+=" (test utilities)"
  depends=(
    bash
    glib2
    python
    python-gobject
    tinysparql
  )
  mv testutils/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
