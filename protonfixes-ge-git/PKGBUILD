# Maintainer: Vasiliy Stelmachenok <ventureo@yandex.ru>
_gitname=protonfixes
_setup_ver=1
pkgname=(protonfixes-ge-git)
pkgver=1.0.15.r546.gfc74674
pkgrel=1
pkgdesc="Protonfixes fork by GloriousEggroll (git)"
license=('bsd')
arch=('any')
url="https://github.com/GloriousEggroll/${_gitname}"
provides=('protonfixes')
conflicts=('protonfixes')
makedepends=(git python-setuptools)
depends=(libmspack winetricks)
source=($_gitname::git+https://github.com/GloriousEggroll/${_gitname}.git
        setup.py)
sha256sums=('SKIP'
            'ea220fffadfdf2748d5095e78958c50222f8c4c1f3aea40a66b386afbbcded74')

pkgver() {
    cd "${srcdir}/${_gitname}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${srcdir}"
    python setup.py build
}

package() {
    cd "${srcdir}"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
