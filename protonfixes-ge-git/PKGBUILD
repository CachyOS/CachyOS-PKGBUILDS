# Maintainer: Vasiliy Stelmachenok <ventureo@yandex.ru>
_gitname=protonfixes
_setup_ver=1
pkgname=(protonfixes-ge-git)
pkgver=1.0.15.r546.gfc74674
pkgrel=1
pkgdesc="Protonfixes fork by GloriousEggroll (git)"
license=('bsd')
arch=('any')
url="https://github.com/GloriousEggroll/${_gitname}"
provides=('protonfixes')
conflicts=('protonfixes')
makedepends=(git python-setuptools)
depends=(libmspack winetricks)
source=($pkgname::git+https://github.com/GloriousEggroll/${_gitname}.git
        setup.py)
sha256sums=('SKIP'
            '58561f68aea810b6440b354d7d66860e6086bd23c6fd80e069a9e478fc7e4600')

pkgver() {
    cd "${srcdir}/${pkgname}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${srcdir}"
    python setup.py build
}

package() {
    cd "${srcdir}"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
