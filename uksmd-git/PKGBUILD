# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Oleksandr Natalenko <oleksandr@natalenko.name>
# Contributor: Damian N. <nycko123 at gmail>

pkgname=uksmd-git
_pkgname=uksmd
pkgver=0.0.0.r37.gf10f38e
pkgrel=4
epoch=1
pkgdesc="Userspace KSM helper daemon"
url="https://codeberg.org/pf-kernel/uksmd"
license=(GPL3)
arch=(x86_64)
depends=('procps-ng' 'libcap-ng')
makedepends=('meson' 'git')
conflicts=('uksmd')
provides=("uksmd=${pkgver}")
source=('git+https://codeberg.org/pf-kernel/uksmd'
        'uksmdstats')
sha256sums=('SKIP'
            '0b09cbca4e1a4ceb68f678991f42290ed6fe4efe4f06a331a71d868df8b9f8a7')

pkgver() {
  cd $_pkgname
  _ver="$(cat meson.build | grep -o "[[:digit:]]*" | paste -sd'.')"
  printf "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $_pkgname

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $_pkgname
  arch-meson . build
  meson compile -C build
}

package() {
  depends+=(UKSMD-BUILTIN)

  cd $_pkgname
  meson install -C build --destdir "${pkgdir}"
  install -Dm 755 ${srcdir}/uksmdstats "${pkgdir}"/usr/bin/uksmdstats
  install -Dm 644 "COPYING" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
