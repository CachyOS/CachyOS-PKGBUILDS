# Maintainer: luadebug <lin@sz.cn.eu.org>

pkgname=qq
pkgver=3.2.19
_pkgdate=2025-09-04
pkgrel=1
pkgdesc="Messaging app"
arch=('x86_64' 'aarch64')
url="https://im.qq.com/index/"
license=('custom')
depends=('alsa-lib' 'util-linux-libs' 'cups' 'glib2' 'gtk3' 'libdrm' 'libpulse' 'libgcrypt' 'krb5' 'mesa' 'nss' 'libvips' 'libxdamage' 'systemd' 'libayatana-appindicator' 'libnotify' 'libssh2')
makedepends=('dpkg' 'patchelf')
provides=('qq')
options=('!strip')

source_x86_64=("QQ_${pkgver}_${_pkgdate}_amd64_01.deb::https://dldir1v6.qq.com/qqfile/qq/QQNT/Linux/QQ_3.2.19_250904_amd64_01.deb")
source_aarch64=("QQ_${pkgver}_${_pkgdate}_arm64_01.deb::https://dldir1v6.qq.com/qqfile/qq/QQNT/Linux/QQ_3.2.19_250904_arm64_01.deb")
sha256sums_x86_64=('e4cde4ca4242985659d154c6fcafa7611b7b488533bdcdceeb23d6f1e6f1e390')
sha256sums_aarch64=('3aa051c437d3cfdc3870578348a7b2b0f96ac5a26dae9f7a3d989e5e7b231a10')

prepare() {
    cd "$srcdir"
    if [[ "$CARCH" == "x86_64" ]]; then
        ar x QQ_${pkgver}_${_pkgdate}_amd64_01.deb
    elif [[ "$CARCH" == "aarch64" ]]; then
        ar x QQ_${pkgver}_${_pkgdate}_arm64_01.deb
    fi
    tar -xf data.tar.xz
    sed -i 's|/opt/QQ/qq|/usr/bin/qq|g;s|/usr/share|/usr/share|g' usr/share/applications/qq.desktop
}

package() {
    cd "$srcdir"
    install -dm755 "$pkgdir/opt"
    cp -r opt/QQ "$pkgdir/opt/"
    install -dm755 "$pkgdir/usr/bin"
    cat > "$pkgdir/usr/bin/qq" << 'EOF'
#!/bin/bash
exec /opt/QQ/qq "$@"
EOF
    chmod +x "$pkgdir/usr/bin/qq"
    install -Dm644 usr/share/applications/qq.desktop "$pkgdir/usr/share/applications/qq.desktop"
    install -Dm644 usr/share/icons/hicolor/512x512/apps/qq.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/qq.png"
    ln -sf /usr/lib/libayatana-appindicator3.so "$pkgdir/opt/QQ/libappindicator3.so"
    ln -sf /usr/lib/libnotify.so "$pkgdir/opt/QQ/libnotify.so"
    rm -rf "$pkgdir/opt/QQ/resources/app/sharp-lib"
    rm -f "$pkgdir/opt/QQ/resources/app/libssh2.so.1"
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/LICENSE" << 'EOF'
QQ for Linux

This is proprietary software distributed by Tencent.
Use of this software is subject to QQ's Terms of Service.
Homepage: https://im.qq.com/index/
EOF
}
