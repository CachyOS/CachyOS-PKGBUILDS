# Maintainer: Peter Jung <admin@ptr1337.dev>

pkgname=jupiter-hw-support
_srctag=jupiter-20240423.1
_srcver=${_srctag#jupiter-}
pkgver=${_srcver//-/.}
pkgrel=2
arch=('any')
license=('MIT')
depends=('python-evdev'
         'dmidecode' # for jupiter-biosupdate
         'python-crcmod' 'python-click' 'python-progressbar' 'python-hid'
         'jq' # for jupiter-controller-update, jupiter-biosupdate
         'alsa-utils' # for the sound workarounds
         'parted' 'e2fsprogs' # for sdcard formatting
         'udisks2>=2.9.4-1.1' # for mounting external drives with the 'as-user' option
        )
optdepends=('grub-steamos')
makedepends=('rsync' 'git' 'openssh' 'xorg-xcursorgen')
pkgdesc='CachyOS - Deckify Steam Deck HW Support'
source=("git+https://gitlab.com/evlaV/jupiter-hw-support.git#tag=$_srctag")
#install="$pkgname.install"
options=(!strip !debug)
sha256sums=('SKIP')
package() {
  rm -rf "$srcdir"/jupiter-hw-support/bin/jupiter-biosupdate
  rm -rf "$srcdir"/jupiter-hw-support/steam-http-loader
  rm -rf "$srcdir"/jupiter-hw-support/steamos-select-branch
  rm -rf "$srcdir"/jupiter-hw-support/steamos-session-select
  rm -rf "$srcdir"/jupiter-hw-support/steamos-update
  rm -rf "$srcdir"/jupiter-hw-support/steamos-polkit-helpers/jupiter-biosupdate
  rm -rf "$srcdir"/jupiter-hw-support/steamos-polkit-helpers/steamos-select-branch
  rm -rf "$srcdir"/jupiter-hw-support/steamos-polkit-helpers/steamos-update
  rsync -a "$srcdir"/jupiter-hw-support/* "$pkgdir"

  cd $pkgdir/usr/share/steamos/
  xcursorgen $pkgdir/usr/share/steamos/steamos-cursor-config $pkgdir/usr/share/icons/steam/cursors/default

  cd "$pkgdir/usr/share/jupiter_bios_updater"

  # Remove gtk2 binary and respective build/start script - unused
  # Attempts to use gtk2 libraries which are not on the device.
  rm h2offt-g H2OFFTx64-G.sh

  # Driver module -- doesn't currently build, and not supported
  rm -rf driver
}
