### CachyOS PKGBUILD Maintainers:
# Maintainer: Eric Naim <dnaim@cachyos.org>
# Maintainer: Peter Jung <admin@ptr1337.dev>


pkgname=(
  lib32-zlib-ng
  lib32-zlib-ng-compat
)
pkgver=2.3.1
pkgrel=2
pkgdesc='zlib replacement with optimizations for next generation systems - 32-bit'
url='https://github.com/zlib-ng/zlib-ng'
arch=('x86_64')
license=('custom:zlib')
depends=(
  lib32-glibc
)
makedepends=(
  git
  cmake
  ninja
  lib32-gcc-libs
)
source=(git+https://github.com/zlib-ng/zlib-ng#tag=${pkgver})
sha256sums=('00a9045fbfe68d032f37c834ee3afbe7be09984567bbf80bdff188ddf0f4c714')
b2sums=('6da200f1773c68b35302e6123d6542922bb55b43f5f5d1e6a9cb35aae5415b0ebc1d8b98143ff90017bc7b5247ef6570f060dbb2d7f37796178c1b1738369078')


build() {
  cd "zlib-ng"


  ## Build for 32-bit
  export CFLAGS+=" -m32 -fno-semantic-interposition"
  export CXXFLAGS+=" -m32 -fno-semantic-interposition"
  export LDFLAGS+=" -m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  # WITH_UNALIGNED - unaligned access invokes undefined behaviour,
  #   see https://github.com/gentoo/gentoo/pull/17167 for more info.
  local _options=(
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib32
    -Wno-dev
    -DWITH_GTEST=OFF
    -DBUILD_SHARED_LIBS=ON

    # https://github.com/zlib-ng/zlib-ng/issues/2029
    -DWITH_CRC32_CHORBA=OFF
  )

  echo "Building zlib-ng"
  cmake -B build \
    "${_options[@]}"
  cmake --build build

  echo "Building zlib-ng-compat"
  cmake -B build-compat \
    "${_options[@]}" \
    -DZLIB_COMPAT=ON
  cmake --build build-compat
}

check() {
  cd "zlib-ng"
  echo "Checking zlib-ng"
  ctest --output-on-failure --test-dir build
  echo "Checking zlib-ng-compat"
  ctest --output-on-failure --test-dir build-compat
}

package_lib32-zlib-ng() {
  provides=('libz-ng.so')
  depends=(zlib-ng)

  cd "zlib-ng"
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "$pkgdir"/usr/include
}

package_lib32-zlib-ng-compat() {
  pkgdesc+=" (zlib compat)"
  provides=('lib32-zlib' 'libz.so')
  depends=(zlib-ng-compat lib32-zlib-ng)
  conflicts=('lib32-zlib')
  replaces=('lib32-zlib')
  options=('staticlibs')

  cd "zlib-ng"
  DESTDIR="${pkgdir}" cmake --install build-compat
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "$pkgdir"/usr/include
}

# vim: ts=2 sw=2 et:
