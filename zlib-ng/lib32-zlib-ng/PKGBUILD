### CachyOS PKGBUILD Maintainers:
# Maintainer: Eric Naim <dnaim@cachyos.org>
# Maintainer: Peter Jung <admin@ptr1337.dev>


pkgname=(
  lib32-zlib-ng
  lib32-zlib-ng-compat
)
pkgver=2.2.4
pkgrel=1
pkgdesc='zlib replacement with optimizations for next generation systems - 32-bit'
url='https://github.com/zlib-ng/zlib-ng'
arch=('x86_64')
license=('custom:zlib')
depends=(
  lib32-glibc
)
makedepends=(
  cmake
  ninja
  lib32-gcc-libs
)
source=(git+https://github.com/zlib-ng/zlib-ng#tag=${pkgver})
sha256sums=('6cd6ba3b99790fda03df7b41d7f99ec010ca1b72abb62d03d2fcf5b015a74c9a')
b2sums=('51a58a4e5960fe111b2f58ed64aa3bf0b645e50cfb11a06ac53e320d7cc492e1707f607efe6fb8c6b1805fbd22f68a7ceb53e53863deebe9cf81e1270edb98a7')


build() {
  cd "zlib-ng"


  ## Build for 32-bit
  export CFLAGS+=" -m32 -fno-semantic-interposition"
  export CXXFLAGS+=" -m32 -fno-semantic-interposition"
  export LDFLAGS+=" -m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

	# WITH_UNALIGNED - unaligned access invokes undefined behaviour,
	#   see https://github.com/gentoo/gentoo/pull/17167 for more info.
  local _options=(
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib32
    -Wno-dev
    -DWITH_GTEST=OFF
  )

  msg2 "Building zlib-ng"
  cmake -B build \
    "${_options[@]}"
  cmake --build build -v

  msg2 "Building zlib-ng-compat"
  cmake -B build-compat \
    "${_options[@]}" \
    -DZLIB_COMPAT=ON
  cmake --build build-compat
}

check() {
  cd "zlib-ng"
  msg2 "Checking zlib-ng"
  ctest --output-on-failure --test-dir build
  msg2 "Checking zlib-ng-compat"
  ctest --output-on-failure --test-dir build-compat
}

package_lib32-zlib-ng() {
  provides=('libz-ng.so')
  depends=(zlib-ng)

  cd "zlib-ng"
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "$pkgdir"/usr/include
}

package_lib32-zlib-ng-compat() {
  pkgdesc+=" (zlib compat)"
  provides=('lib32-zlib' 'libz.so')
  depends=(zlib-ng-compat lib32-zlib-ng)
  conflicts=('lib32-zlib')
  replaces=('lib32-zlib')
  options=('staticlibs')

  cd "zlib-ng"
  DESTDIR="${pkgdir}" cmake --install build-compat
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "$pkgdir"/usr/include
}

# vim: ts=2 sw=2 et:
