# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=scx-manager
pkgver=1.15.5
pkgrel=1
pkgdesc="Simple GUI for managing sched-ext schedulers via scx_loader"
arch=(aarch64 x86_64)
url="https://github.com/cachyos/scx-manager"
license=(GPL-3.0-or-later)
depends=('qt6-base' 'polkit' 'scx-scheds')
makedepends=('cmake' 'ninja' 'git' 'qt6-tools' 'cargo' 'lld' 'clang' 'llvm')
groups=('cachyos')
source=("scx-manager::git+$url.git#tag=v${pkgver}?signed")
sha256sums=('14d61f419240110d4d856096cc023d16afb884871e4be31367fdd84958a29873')
validpgpkeys=(
  'B1B70BB1CD56047DEF31DE2EB62C3D10C54D5DA9'  # Vladislav Nepogodin <nepogodin.vlad@gmail.com>
)

build() {
  cd "$srcdir/$pkgname"

  # we are building with llvm stack here, due to rust
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}

package() {
  cd "$srcdir/$pkgname"

  # we are building with llvm stack here, due to rust
  export AR=llvm-ar
  export CC=clang
  export CXX=clang++
  export NM=llvm-nm
  export RANLIB=llvm-ranlib

  export RUSTFLAGS="-Cembed-bitcode -C opt-level=3 -Ccodegen-units=1 -Clinker=clang -C link-arg=-flto -Clink-arg=-fuse-ld=/usr/bin/ld.lld"

  DESTDIR="${pkgdir}" cmake --build build --target install
}

# vim:set sw=2 sts=2 et:
