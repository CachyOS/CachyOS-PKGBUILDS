# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>

pkgname=cachyos-sysctl-manager
pkgver=1.2.0
pkgrel=1
pkgdesc="Manage linux kernel sysctl options"
arch=(aarch64 x86_64)
url="https://github.com/cachyos/sysctl-manager"
license=(GPL-3.0-or-later)
depends=('qt6-base' 'polkit')
makedepends=('cmake' 'ninja' 'git' 'polkit-qt6')
groups=('cachyos')
source=("sysctl-manager::git+$url.git#tag=v${pkgver}?signed")
sha256sums=('a979dc93fd9977a73836dace391d58a6bf0f271e1f14d30f8e0d7a75e60f2d89')
validpgpkeys=(
  'B1B70BB1CD56047DEF31DE2EB62C3D10C54D5DA9'  # Vladislav Nepogodin <nepogodin.vlad@gmail.com>
)

build() {
  cd ${srcdir}/sysctl-manager

  cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}

package() {
  cd ${srcdir}/sysctl-manager
  DESTDIR="${pkgdir}" cmake --build build --target install
}

# vim:set sw=2 sts=2 et:
