# Maintainer: Peter Jung ptr1337 <admin@ptr1337.dev>
# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>

pkgname=scx-git
pkgver=0.1.0.r9.g5751f1c
pkgrel=1
pkgdesc="sched_ext schedulers and tools"
arch=(x86_64)
url="https://github.com/sched-ext/scx"
license=("GPL2")
depends=('libbpf' 'bpf')
makedepends=('git' 'python' 'meson' 'clang' 'llvm-libs' 'cargo')
source=("scx::git+https://github.com/sched-ext/scx"
		"scx-nest.patch::https://github.com/sched-ext/scx/commit/ca2184290865ae5a39634fe110f886c7979e009e.patch")
sha256sums=('SKIP'
            'e019a1eef801f3fa1ec60308334f6c96838d257d819412b9295a5dda1020446f')
options=(!lto)
_reponame="scx"

pkgver() {
    cd "$_reponame"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd $_reponame
	patch -Np1 < $srcdir/scx-nest.patch
}

build() {
	cd $_reponame
	export LDFLAGS="-Wl,--as-needed -Wl,--no-undefined -Wl,-O1 -Wl,--start-group /usr/lib/libbpf.so -Wl,--end-group"
	meson setup build -Dbuildtype=release
	cd build
	meson compile -v
}


package() {
	cd $_reponame/build
	DESTDIR="$pkgdir" meson install
}
