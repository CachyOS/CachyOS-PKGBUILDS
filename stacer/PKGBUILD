# Maintainer: VolRen <VolRencs@proton.me>

pkgname=stacer
pkgver=1.6.0
pkgrel=1
pkgdesc="Linux System Optimizer and Monitoring"
url="https://github.com/QuentiumYT/Stacer"
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('qt6-base' 'qt6-charts' 'qt6-svg')
makedepends=('cmake' 'qt6-tools')
provides=('stacer')
conflicts=('stacer-git' 'stacer-bin')
source=("https://github.com/QuentiumYT/Stacer/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('5aea4fe7b27a1f5d2531f9fdfc001c452b76fd80f586d7b0fd95f7b197dd0547')

build() {
    cd "Stacer-${pkgver}"

    mkdir -p build

    cmake -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_CXX_COMPILER=g++ \
          -B build -S .

    make -C build -j$(nproc)
}

package() {
    cd "Stacer-${pkgver}"

    install -Dm755 "build/stacer/stacer" -t "${pkgdir}/usr/share/stacer"
    install -Dm755 "build/stacer-core/libstacer-core.a" -t "${pkgdir}/usr/share/stacer/lib"
    install -Dm644 "build/stacer/translations"/*.qm -t "${pkgdir}/usr/share/stacer/translations"

    install -Dm644 "icons/hicolor/scalable/apps/stacer.svg" -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps"

    install -Dm644 "desktop/stacer.desktop" -t "${pkgdir}/usr/share/applications"
    install -Dm644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"

    mkdir -p "${pkgdir}/usr/bin"
    ln -sf "/usr/share/stacer/stacer" "${pkgdir}/usr/bin/stacer"
}

