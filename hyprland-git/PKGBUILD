# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>
# Contributor: ThatOneCalculator <kainoa@t1c.dev>

_pkgname="hyprland"
pkgname="${_pkgname}-git"
pkgver=0.22.0.r2533.489ef7c5
pkgrel=1
pkgdesc="A dynamic tiling Wayland compositor based on wlroots that doesn't sacrifice on its looks."
arch=(x86_64)
url="https://github.com/hyprwm/Hyprland"
license=('BSD-3-Clause')
depends=(
  libxcb
  xcb-proto
  xcb-util
  xcb-util-keysyms
  libxfixes
  libx11
  libxcomposite
  xorg-xinput
  libxrender
  pixman
  wayland-protocols
  cairo
  pango
  polkit
  glslang
  libinput
  libxcb
  libxkbcommon
  opengl-driver
  pixman
  wayland
  xcb-util-errors
  xcb-util-renderutil
  xcb-util-wm
  seatd
  vulkan-icd-loader
  vulkan-validation-layers
  xorg-xwayland
  libliftoff
  libdisplay-info
)
makedepends=(
  git
  cmake
  ninja
  meson
  jq
  vulkan-headers
  wayland-protocols
  xorgproto
)
source=("${_pkgname}::git+https://github.com/hyprwm/Hyprland.git")
conflicts=("${_pkgname}")
provides=('hyprland')
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  _pkgver="$(cat props.json | jq  -r ".version")"

  printf "${_pkgver}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}"
  git submodule update --init

  # With those flags set Hyprland does crash
  export CFLAGS=${CFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}
  export CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}

  make fixwlr

  cd "${srcdir}/${_pkgname}/subprojects/wlroots"
  meson setup \
      --prefix="${srcdir}/tmpwlr" \
      --buildtype plain \
      --auto-features enabled \
      --wrap-mode nodownload \
      -D b_lto=true \
      -D b_pie=true \
      -D examples=false \
      build
  ninja -C build

  mkdir -p "${srcdir}/tmpwlr"
  ninja -C build install

  # Build third-party library for plugin system
  cd "${srcdir}/${_pkgname}/subprojects/udis86"
  cmake -B build -S . \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=OFF
  cmake --build build

  # Build protocols
  cd "${srcdir}/${_pkgname}"
  make protocols

  # Build hyprland
  cd "${srcdir}/${_pkgname}"
  cmake -B build -S . \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=OFF
  cmake --build build

  # Build hyprctl
  make -C "${srcdir}/${_pkgname}/hyprctl"
}

package() {
    cd "${srcdir}/${_pkgname}"
    mkdir -p "${pkgdir}/usr/share/wayland-sessions"
    mkdir -p "${pkgdir}/usr/share/hyprland"

    install -Dm755 build/Hyprland -t "${pkgdir}/usr/bin"
    install -Dm755 hyprctl/hyprctl -t "${pkgdir}/usr/bin"
    install -Dm644 assets/*.png -t "${pkgdir}/usr/share/hyprland"
    install -Dm644 example/hyprland.desktop -t "${pkgdir}/usr/share/wayland-sessions"
    install -Dm644 example/hyprland.conf -t "${pkgdir}/usr/share/hyprland"
    install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}"
    install -Dm644 docs/*.1 -t "${pkgdir}/share/man/man1"
    install -Dm755 ../tmpwlr/lib/libwlroots.so.12032 -t "${pkgdir}/usr/lib"
}
