# Maintainer: Vladislav Nepogodin <nepogodin.vlad@gmail.com>
# Contributor: ThatOneCalculator <kainoa@t1c.dev>

_pkgname="hyprland"
pkgname="${_pkgname}-git"
pkgver=0.32.3.r3746.59cb0e20
pkgrel=1
pkgdesc='a highly customizable dynamic tiling Wayland compositor'
arch=(x86_64 aarch64)
url="https://github.com/hyprwm/Hyprland"
license=(BSD)
depends=(
  cairo
  gcc-libs
  glibc
  glslang
  libdisplay-info
  libdrm
  libglvnd
  libinput
  libliftoff
  libx11
  libxcb
  libxcomposite
  libxfixes
  libxkbcommon
  libxrender
  opengl-driver
  pango
  pixman
  polkit
  seatd
  systemd-libs
  vulkan-icd-loader
  vulkan-validation-layers
  wayland
  wayland-protocols
  xcb-proto
  xcb-util
  xcb-util-errors
  xcb-util-keysyms
  xcb-util-renderutil
  xcb-util-wm
  xorg-xinput
  xorg-xwayland
)
makedepends=(
  git
  gdb
  cmake
  ninja
  meson
  jq
  vulkan-headers
  xorgproto
)
source=("${_pkgname}::git+https://github.com/hyprwm/Hyprland.git"
        runtime-xwayland-disable.patch
)
conflicts=("${_pkgname}")
provides=('hyprland')
sha256sums=('SKIP'
            '32d7be2b15ba0fc61e168d95fba6e567a2eb6968e904bb96af4c7612a129816d')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  _pkgver="$(cat props.json | jq  -r ".version")"

  printf "${_pkgver}.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_pkgname}"
  git submodule update --init

  patch -Np1 < ../runtime-xwayland-disable.patch

  # With those flags set Hyprland does crash
  export CFLAGS=${CFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}
  export CXXFLAGS=${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}

  sed -i -e '/^release:/{n;s/-D/-DCMAKE_SKIP_RPATH=ON -D/}' Makefile

  cd "${srcdir}/${_pkgname}/subprojects/wlroots"
  meson setup \
      --prefix="${srcdir}/tmpwlr" \
      --buildtype plain \
      --auto-features enabled \
      --wrap-mode nodownload \
      -D b_lto=true \
      -D b_pie=true \
      -D examples=false \
      build
  ninja -C build

  mkdir -p "${srcdir}/tmpwlr"
  ninja -C build install

  # Build third-party library for plugin system
  cd "${srcdir}/${_pkgname}/subprojects/udis86"
  cmake -B build -S . \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=OFF
  cmake --build build

  # Build protocols
  cd "${srcdir}/${_pkgname}"
  make protocols

  # Build hyprland
  cd "${srcdir}/${_pkgname}"
  cmake -B build -S . \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_SHARED_LIBS=OFF
  cmake --build build

  # Build hyprctl
  make -C "${srcdir}/${_pkgname}/hyprctl"
}

package() {
  cd "${srcdir}/${_pkgname}"
  mkdir -p "${pkgdir}/usr/share/wayland-sessions"
  mkdir -p "${pkgdir}/usr/share/hyprland"

  find src -name '*.hpp' -exec install -Dm0644 {} "$pkgdir/usr/include/hyprland/{}" \;
  pushd subprojects/wlroots/include
  find . -name '*.h' -exec install -Dm0644 {} "$pkgdir/usr/include/hyprland/wlroots/{}" \;
  popd
  pushd subprojects/wlroots/build/include
  find . -name '*.h' -exec install -Dm0644 {} "$pkgdir/usr/include/hyprland/wlroots/{}" \;
  popd
  mkdir -p "$pkgdir/usr/include/hyprland/protocols"
  cp protocols/*-protocol.h "$pkgdir/usr/include/hyprland/protocols"

  install -Dm0644 -t "$pkgdir/usr/share/pkgconfig" build/hyprland.pc
  install -Dm0755 -t "$pkgdir/usr/bin" build/Hyprland
  install -Dm0755 -t "$pkgdir/usr/bin" hyprctl/hyprctl
  install -Dm0644 -t "$pkgdir/usr/share/$pkgname" assets/*.png
  install -Dm0644 -t "$pkgdir/usr/share/wayland-sessions" "example/$_pkgname.desktop"
  install -Dm0644 -t "$pkgdir/usr/share/$pkgname" "example/$_pkgname.conf"
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
  install -Dm0755 -t "$pkgdir/usr/lib" "$srcdir/hyprland/subprojects/wlroots/build/libwlroots.so.13032"
}
